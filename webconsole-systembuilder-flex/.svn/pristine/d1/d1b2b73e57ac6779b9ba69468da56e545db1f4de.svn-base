/**
 *                                  Apache License
 *                            Version 2.0, January 2004
 *                         http://www.apache.org/licenses/
 *
 *    TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION
 *
 *    1. Definitions.
 *
 *       "License" shall mean the terms and conditions for use, reproduction,
 *       and distribution as defined by Sections 1 through 9 of this document.
 *
 *       "Licensor" shall mean the copyright owner or entity authorized by
 *       the copyright owner that is granting the License.
 *
 *       "Legal Entity" shall mean the union of the acting entity and all
 *       other entities that control, are controlled by, or are under common
 *       control with that entity. For the purposes of this definition,
 *       "control" means (i) the power, direct or indirect, to cause the
 *       direction or management of such entity, whether by contract or
 *       otherwise, or (ii) ownership of fifty percent (50%) or more of the
 *       outstanding shares, or (iii) beneficial ownership of such entity.
 *
 *       "You" (or "Your") shall mean an individual or Legal Entity
 *       exercising permissions granted by this License.
 *
 *       "Source" form shall mean the preferred form for making modifications,
 *       including but not limited to software source code, documentation
 *       source, and configuration files.
 *
 *       "Object" form shall mean any form resulting from mechanical
 *       transformation or translation of a Source form, including but
 *       not limited to compiled object code, generated documentation,
 *       and conversions to other media types.
 *
 *       "Work" shall mean the work of authorship, whether in Source or
 *       Object form, made available under the License, as indicated by a
 *       copyright notice that is included in or attached to the work
 *       (an example is provided in the Appendix below).
 *
 *       "Derivative Works" shall mean any work, whether in Source or Object
 *       form, that is based on (or derived from) the Work and for which the
 *       editorial revisions, annotations, elaborations, or other modifications
 *       represent, as a whole, an original work of authorship. For the purposes
 *       of this License, Derivative Works shall not include works that remain
 *       separable from, or merely link (or bind by name) to the interfaces of,
 *       the Work and Derivative Works thereof.
 *
 *       "Contribution" shall mean any work of authorship, including
 *       the original version of the Work and any modifications or additions
 *       to that Work or Derivative Works thereof, that is intentionally
 *       submitted to Licensor for inclusion in the Work by the copyright owner
 *       or by an individual or Legal Entity authorized to submit on behalf of
 *       the copyright owner. For the purposes of this definition, "submitted"
 *       means any form of electronic, verbal, or written communication sent
 *       to the Licensor or its representatives, including but not limited to
 *       communication on electronic mailing lists, source code control systems,
 *       and issue tracking systems that are managed by, or on behalf of, the
 *       Licensor for the purpose of discussing and improving the Work, but
 *       excluding communication that is conspicuously marked or otherwise
 *       designated in writing by the copyright owner as "Not a Contribution."
 *
 *       "Contributor" shall mean Licensor and any individual or Legal Entity
 *       on behalf of whom a Contribution has been received by Licensor and
 *       subsequently incorporated within the Work.
 *
 *    2. Grant of Copyright License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       copyright license to reproduce, prepare Derivative Works of,
 *       publicly display, publicly perform, sublicense, and distribute the
 *       Work and such Derivative Works in Source or Object form.
 *
 *    3. Grant of Patent License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       (except as stated in this section) patent license to make, have made,
 *       use, offer to sell, sell, import, and otherwise transfer the Work,
 *       where such license applies only to those patent claims licensable
 *       by such Contributor that are necessarily infringed by their
 *       Contribution(s) alone or by combination of their Contribution(s)
 *       with the Work to which such Contribution(s) was submitted. If You
 *       institute patent litigation against any entity (including a
 *       cross-claim or counterclaim in a lawsuit) alleging that the Work
 *       or a Contribution incorporated within the Work constitutes direct
 *       or contributory patent infringement, then any patent licenses
 *       granted to You under this License for that Work shall terminate
 *       as of the date such litigation is filed.
 *
 *    4. Redistribution. You may reproduce and distribute copies of the
 *       Work or Derivative Works thereof in any medium, with or without
 *       modifications, and in Source or Object form, provided that You
 *       meet the following conditions:
 *
 *       (a) You must give any other recipients of the Work or
 *           Derivative Works a copy of this License; and
 *
 *       (b) You must cause any modified files to carry prominent notices
 *           stating that You changed the files; and
 *
 *       (c) You must retain, in the Source form of any Derivative Works
 *           that You distribute, all copyright, patent, trademark, and
 *           attribution notices from the Source form of the Work,
 *           excluding those notices that do not pertain to any part of
 *           the Derivative Works; and
 *
 *       (d) If the Work includes a "NOTICE" text file as part of its
 *           distribution, then any Derivative Works that You distribute must
 *           include a readable copy of the attribution notices contained
 *           within such NOTICE file, excluding those notices that do not
 *           pertain to any part of the Derivative Works, in at least one
 *           of the following places: within a NOTICE text file distributed
 *           as part of the Derivative Works; within the Source form or
 *           documentation, if provided along with the Derivative Works; or,
 *           within a display generated by the Derivative Works, if and
 *           wherever such third-party notices normally appear. The contents
 *           of the NOTICE file are for informational purposes only and
 *           do not modify the License. You may add Your own attribution
 *           notices within Derivative Works that You distribute, alongside
 *           or as an addendum to the NOTICE text from the Work, provided
 *           that such additional attribution notices cannot be construed
 *           as modifying the License.
 *
 *       You may add Your own copyright statement to Your modifications and
 *       may provide additional or different license terms and conditions
 *       for use, reproduction, or distribution of Your modifications, or
 *       for any such Derivative Works as a whole, provided Your use,
 *       reproduction, and distribution of the Work otherwise complies with
 *       the conditions stated in this License.
 *
 *    5. Submission of Contributions. Unless You explicitly state otherwise,
 *       any Contribution intentionally submitted for inclusion in the Work
 *       by You to the Licensor shall be under the terms and conditions of
 *       this License, without any additional terms or conditions.
 *       Notwithstanding the above, nothing herein shall supersede or modify
 *       the terms of any separate license agreement you may have executed
 *       with Licensor regarding such Contributions.
 *
 *    6. Trademarks. This License does not grant permission to use the trade
 *       names, trademarks, service marks, or product names of the Licensor,
 *       except as required for reasonable and customary use in describing the
 *       origin of the Work and reproducing the content of the NOTICE file.
 *
 *    7. Disclaimer of Warranty. Unless required by applicable law or
 *       agreed to in writing, Licensor provides the Work (and each
 *       Contributor provides its Contributions) on an "AS IS" BASIS,
 *       WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 *       implied, including, without limitation, any warranties or conditions
 *       of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
 *       PARTICULAR PURPOSE. You are solely responsible for determining the
 *       appropriateness of using or redistributing the Work and assume any
 *       risks associated with Your exercise of permissions under this License.
 *
 *    8. Limitation of Liability. In no event and under no legal theory,
 *       whether in tort (including negligence), contract, or otherwise,
 *       unless required by applicable law (such as deliberate and grossly
 *       negligent acts) or agreed to in writing, shall any Contributor be
 *       liable to You for damages, including any direct, indirect, special,
 *       incidental, or consequential damages of any character arising as a
 *       result of this License or out of the use or inability to use the
 *       Work (including but not limited to damages for loss of goodwill,
 *       work stoppage, computer failure or malfunction, or any and all
 *       other commercial damages or losses), even if such Contributor
 *       has been advised of the possibility of such damages.
 *
 *    9. Accepting Warranty or Additional Liability. While redistributing
 *       the Work or Derivative Works thereof, You may choose to offer,
 *       and charge a fee for, acceptance of support, warranty, indemnity,
 *       or other liability obligations and/or rights consistent with this
 *       License. However, in accepting such obligations, You may act only
 *       on Your own behalf and on Your sole responsibility, not on behalf
 *       of any other Contributor, and only if You agree to indemnify,
 *       defend, and hold each Contributor harmless for any liability
 *       incurred by, or claims asserted against, such Contributor by reason
 *       of your accepting any such warranty or additional liability.
 *
 *    END OF TERMS AND CONDITIONS
 *
 *    APPENDIX: How to apply the Apache License to your work.
 *
 *       To apply the Apache License to your work, attach the following
 *       boilerplate notice, with the fields enclosed by brackets "{}"
 *       replaced with your own identifying information. (Don't include
 *       the brackets!)  The text should be enclosed in the appropriate
 *       comment syntax for the file format. We also recommend that a
 *       file or class name and description of purpose be included on the
 *       same "printed page" as the copyright notice for easier
 *       identification within third-party archives.
 *
 *    Copyright {yyyy} {name of copyright owner}
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
package com.deleidos.rtws.systemcfg.views
{
	import com.deleidos.rtws.commons.model.auth.IUser;
	import com.deleidos.rtws.commons.model.datamodel.DataModelName;
	import com.deleidos.rtws.commons.model.status.StatusMessage;
	import com.deleidos.rtws.commons.model.status.StatusType;
	import com.deleidos.rtws.commons.tenantutils.model.system.System;
	import com.deleidos.rtws.commons.tenantutils.model.system.SystemState;
	import com.deleidos.rtws.commons.util.StringUtils;
	import com.deleidos.rtws.systemcfg.commands.DeleteDatasinkCommand;
	import com.deleidos.rtws.systemcfg.dao.DatasinkConfig;
	import com.deleidos.rtws.systemcfg.dao.InstanceTypes;
	import com.deleidos.rtws.systemcfg.dao.ProcessGroupConfig;
	import com.deleidos.rtws.systemcfg.events.EditEvent;
	import com.deleidos.rtws.systemcfg.events.ModelServiceEvent;
	import com.deleidos.rtws.systemcfg.events.PopupEvent;
	import com.deleidos.rtws.systemcfg.events.SystemConfigServiceEvent;
	import com.deleidos.rtws.systemcfg.models.DataModelModel;
	import com.deleidos.rtws.systemcfg.models.Datasink;
	import com.deleidos.rtws.systemcfg.models.DatasinkModel;
	import com.deleidos.rtws.systemcfg.models.ProcessGroup;
	import com.deleidos.rtws.systemcfg.models.ProcessGroupModel;
	import com.deleidos.rtws.systemcfg.models.ProcessGroupTypes;
	import com.deleidos.rtws.systemcfg.models.SplitterModel;
	import com.deleidos.rtws.systemcfg.models.SystemListModel;
	import com.deleidos.rtws.systemcfg.models.SystemPropertiesModel;
	import com.deleidos.rtws.systemcfg.models.Transport;
	import com.deleidos.rtws.systemcfg.models.TransportModel;
	
	import flash.utils.Dictionary;
	
	import mx.collections.ArrayCollection;
	import mx.events.CloseEvent;
	import mx.managers.PopUpManager;
	
	import org.robotlegs.core.IInjector;
	import org.robotlegs.mvcs.Mediator;
	
	import spark.events.GridSelectionEvent;
	
	public class ParameterViewMediator extends Mediator
	{
		[Inject] public var injector:IInjector;

		[Inject] public var view:ParameterView; 
		
		[Inject] public var splitterModel:SplitterModel;
		[Inject] public var dataModelModel:DataModelModel;
		[Inject] public var datasinkModel:DatasinkModel;
		[Inject] public var datasinkConfig:DatasinkConfig;
		[Inject] public var transportModel:TransportModel;
		[Inject] public var processGroupModel:ProcessGroupModel;
		[Inject] public var sysPropModel:SystemPropertiesModel;
		[Inject] public var systemListModel:SystemListModel;
		[Inject] public var processGroupConfig:ProcessGroupConfig;
		[Inject] public var instanceTypesTable:InstanceTypes;
		[Inject] public var userModel:IUser;

		// Stores all the data models that exist from opening system configuration
		private var onSysCfgLoadedDatasinkDataModels:Dictionary = new Dictionary();
		
		override public function onRegister():void {
			eventMap.mapListener(view, EditEvent.SELECT_DATASINK_PARAMETERS, handleDatasinkSelected);
			eventMap.mapListener(view, EditEvent.SELECT_TRANSPORT_PARAMETERS, handleTransportSelected);
			eventMap.mapListener(view, PopupEvent.SELECT_PERSISTENT_IP_BEGIN, handlePersistentIpPopupBegin);
			eventMap.mapListener(view, PopupEvent.MESSAGE_BEGIN, dispatch);
				
			eventMap.mapListener(eventDispatcher, EditEvent.DATASINK_DATA_MODELS_CHANGED, handleDatasinkDataModelsChanged);
			eventMap.mapListener(eventDispatcher, EditEvent.REFRESH_PROCESS_GROUP_PARAMS, handleRefreshProcessGroupParams);
			eventMap.mapListener(eventDispatcher, EditEvent.SELECTED_DATASINKS_CHANGED, handleDatasinkChange);
			eventMap.mapListener(eventDispatcher, EditEvent.SELECTED_TRANSPORTS_CHANGED, handleTransportChange);
			
			eventMap.mapListener(eventDispatcher, EditEvent.TRANSPORT_UPDATED, handleTransportUpdate);
			eventMap.mapListener(eventDispatcher, EditEvent.DATASINK_UPDATED, handleDatasinkUpdate);
			
			eventMap.mapListener(eventDispatcher, EditEvent.ADD_DATAMODEL, handleAddDataModel);
			eventMap.mapListener(eventDispatcher, EditEvent.REMOVE_DATAMODEL, handleRemoveDataModel);
			eventMap.mapListener(eventDispatcher, EditEvent.SELECTED_DATA_MODELS_CHANGED, handleDataModelChange);
			
			eventMap.mapListener(eventDispatcher, PopupEvent.SELECT_PERSISTENT_IP_BEGIN, handlePersistentIpPopupBegin);
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.NEW_SYSTEM_CONFIG_DONE, handleNewSystemConfigDone);	
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.SYSTEM_CONFIG_LOADED, handleSystemConfigLoaded);
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.PERSISTENT_IP_ALLOCATED, handlePersistentIpAllocated);

			view.selectedDMGrid.addEventListener(GridSelectionEvent.SELECTION_CHANGE, handleDatasinkDataModelsSelectionChange);
			
			eventMap.mapListener(view, PopupEvent.PROCESS_GROUP_CONFIG_BEGIN, handleProcessGroupConfigBegin);
			eventMap.mapListener(eventDispatcher, PopupEvent.PROCESS_GROUP_CONFIG_BEGIN, handleProcessGroupConfigBegin);
			
			eventMap.mapListener(view, PopupEvent.PROCESS_GROUP_SELECT_BEGIN, handleProcessGroupSelectBegin);
			eventMap.mapListener(view, EditEvent.REMOVE_PROCESS_GROUP, handleRemoveProcessGroup);
			
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.CREATE_PROCESS_GROUP_DONE, handleCreateProcessGroupDone);
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.DELETE_PROCESS_GROUP_DONE, handleDeleteProcessGroupDone);
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.RELOAD_PROCESS_CONFIG_DONE, handleReloadProcessGroupsDone);
			
			bindModel();
			
			handleNewSystemConfigDone(null);
			sysPropModel.clear();
		}
		
		private function setDatasinkParams(datasink:Datasink):void {
			view.datasinkParameters = datasink.params;
			setSelectedDatasinkDataModels(datasink);
			view.datasinkParameters.refresh();			
		}
		
		private function setTransportParams(transport:Transport):void {
			view.transportParameters = transport.params;
			view.transportParameters.refresh();			
		}
		
		private function setupDefaultDatasinkSelection():void {
			var selectedDS:ArrayCollection = datasinkModel.selectedDatasinks;
			if (selectedDS.length > 0) {
				view.datasinkCombo.selectedIndex = 0;
				var ds:Datasink = selectedDS.getItemAt(0) as Datasink;
				setDatasinkParams(ds);
				populateAllDataModels();
				setSelectedDatasinkDataModels(ds);
			} else {
				view.datasinkCombo.selectedIndex = -1;
				view.datasinkParameters = new ArrayCollection();
				view.datasinkParameters.refresh();
				view.allDataModels = new ArrayCollection();
				view.allDataModels.refresh();
			}	
		}
		
		/**
		 * Takes a selected datasink and mark all the datasink
		 * data models as selected in the datagrid.
		 */ 
		private function setSelectedDatasinkDataModels(ds:Datasink):void {
			var selected:Vector.<Object> = new Vector.<Object>();
			for (var j:int = 0; j < ds.dataModels.length; j++) {
				for (var k:int = 0; k < view.allDataModels.length; k++) {
					if ((ds.dataModels.getItemAt(j))["model"] == (view.allDataModels.getItemAt(k))["model"]) {
						selected.push(view.allDataModels.getItemAt(k));
						break;
					}
				}
			}
			
			view.selectedDMGrid.selectedItems = selected;
			view.selectedDMGrid.validateNow();
		}
		
		/**
		 * Populate the datasink data models datagrid with all the
		 * data models in the seltected list.
		 */ 
		private function populateAllDataModels():void {
			var dataModels:ArrayCollection = new ArrayCollection();
			for each (var model:DataModelName in dataModelModel.selectedDataModelNames) {
				dataModels.addItem({"model" : model.zipFilename});
			}
			view.allDataModels = dataModels;
			view.allDataModels.refresh();
		}
		
		private function setupDefaultTransportSelection():void {
			var selectedTP:ArrayCollection = transportModel.selectedTransports;
			if (selectedTP.length > 0) {
				view.transportCombo.selectedIndex = 0;
				setTransportParams(selectedTP.getItemAt(0) as Transport);
			}
			else {
				view.transportCombo.selectedIndex = -1;
				view.transportParameters = new ArrayCollection();
				view.transportParameters.refresh();			
			}			
		}
		
		protected function handleDatasinkChange( event:EditEvent ):void {
			setupDefaultDatasinkSelection();
		}		
		
		protected function handleTransportChange( event:EditEvent ):void {
			setupDefaultTransportSelection();
		}
		
		/**
		 * Need to update the details view for transports if the params changed in the overview section
		 */
		protected function handleTransportUpdate( event:EditEvent ):void {
			var transportName:String = view.transportCombo.textInput.text;
			if (transportName!= null && transportName.length > 0) {
				for each (var ts:Transport in transportModel.selectedTransports.source) {
					if (ts.name == transportName) {
						view.transportParameters = event.parameters.paramData;
						view.transportParameters.refresh();	
						break;
					}
				}
			}
		}
		
		/**
		 * Need to update the details view for datasinks if the params changed in the overview section
		 */
		protected function handleDatasinkUpdate( event:EditEvent ):void {			
			var dsName:String = view.datasinkCombo.textInput.text;
			if (dsName!= null && dsName.length > 0) {
				for each (var ds:Datasink in datasinkModel.selectedDatasinks.source) {
					if (ds.name == dsName) {
						view.datasinkParameters = event.parameters.paramData;
						view.datasinkParameters.refresh();	
						break;
					}
				}
			}
		}
		
		protected function handleDatasinkDataModelsSelectionChange( event:GridSelectionEvent ):void {
			var dataModels:ArrayCollection = new ArrayCollection();
			if (view.selectedDMGrid.selectedItems != null) {
				for (var i:int = 0; i < view.selectedDMGrid.selectedItems.length; i++) {
					dataModels.addItem(view.selectedDMGrid.selectedItems[i]);
				}
			}
			for each (var ds:Datasink in datasinkModel.selectedDatasinks.source) {
				if (ds.name == view.datasinkCombo.textInput.text) {
					ds.dataModels = dataModels;
					break;
				}
			}
			updateDatasinkDataModels();
		}
		
		/**
		 * A data model was added to the selected data models lists.
		 * Go through each datasink data models selection and add it
		 * to the list. If a datasink data models list doesn't have
		 * all the items from the selected data models lists display a
		 * popup notifying the user.
		 */ 
		protected function handleAddDataModel( event:EditEvent ):void {
			if (event.parameters == null || event.parameters.dataModelName == null) {
				return;
			}
			
			var dmn:DataModelName = DataModelName(event.parameters.dataModelName);
			if (onSysCfgLoadedDatasinkDataModels.hasOwnProperty(dmn.zipFilename)) {
				delete onSysCfgLoadedDatasinkDataModels[dmn.zipFilename];
				return;
			}
			
			var userConfiguredList:Array = new Array();
			for each (var ds:Datasink in datasinkModel.selectedDatasinks.source) {
				var idx:int = findDatasinkDataModelIdx(ds, dmn);
				if (idx == -1) {
					// The data model doesn't exist in this list, add it.
					ds.dataModels.addItem({"model" : dmn.zipFilename});
				}
				if (dataModelModel.selectedDataModelNames.length != ds.dataModels.length) {
					// This list stores all the datasink that was manually configured by the user.
					userConfiguredList.push(ds.name);
				}
			}
			
			if (userConfiguredList.length > 0) {
				var msg:String = "The following datasinks doesn't include all data models [";
				for (var j:int = 0; j < userConfiguredList.length; j++) {
					msg += (j == 0) ? userConfiguredList[j] : "," + userConfiguredList[j];
				}
				msg += "].";
				popupStatusMessage(StatusType.INFO, msg);
			}
			updateDatasinkDataModels();
		}
		
		/**
		 * A data model was removed from the selected data models lists.
		 * Go through each datasink data models selection and remove it
		 * from the list. If a datasink data models list doesn't have
		 * all the items from the selected data models lists display a
		 * popup notifying the user.
		 */ 
		protected function handleRemoveDataModel( event:EditEvent ):void {
			if (event.parameters == null || event.parameters == null) {
				return;
			}
			
			var userConfiguredList:Array = new Array();
			var dmn:DataModelName = DataModelName(event.parameters);
			for each (var ds:Datasink in datasinkModel.selectedDatasinks.source) {
				var idx:int = findDatasinkDataModelIdx(ds, dmn);
				if (idx != -1) {
					// The data model exist in this list, remove it.
					ds.dataModels.removeItemAt(idx);
				}
				if (dataModelModel.selectedDataModelNames.length != ds.dataModels.length) {
					// This list stores all the datasink that was manually configured by the user.
					userConfiguredList.push(ds.name);
				}
			}
			
			if (userConfiguredList.length > 0) {
				var msg:String = "The following datasinks doesn't include all data models [";
				for (var j:int = 0; j < userConfiguredList.length; j++) {
					msg += (j == 0) ? userConfiguredList[j] : "," + userConfiguredList[j];
				}
				msg += "].";
				popupStatusMessage(StatusType.INFO, msg);
			}
			updateDatasinkDataModels();
		}
		
		private function findDatasinkDataModelIdx(ds:Datasink, dmn:DataModelName ):int {
			var idx:int = -1;
			for (var i:int = 0; i < ds.dataModels.length; i++) {
				var dm:Object = ds.dataModels.getItemAt(i);
				if (dm.model != null && dm.model == dmn.zipFilename) {
					idx = i;
					break;
				}
			}
			return idx;
		}
		
		private function popupStatusMessage(type:StatusType, msg:String):void {
			var sm:StatusMessage = new StatusMessage(type, msg);
			dispatch(new PopupEvent(PopupEvent.MESSAGE_BEGIN, sm, null));			
		}
		
		protected function handleDataModelChange( event:EditEvent ):void {
			var comboBoxEnabledFields:Array = new Array();
			comboBoxEnabledFields[TransportModel.INPUT_FORMAT_FIELD_NAME] = dataModelModel.dataSourceNames;
			comboBoxEnabledFields[TransportModel.RECORD_FORMAT_FIELD_NAME] = splitterModel.splitters;
			view.comboBoxEnabledFields = comboBoxEnabledFields;
			
			updateDatasinkDataModels();
		}
		
		/**
		 * The datasink data models selection was changed using the popup option.
		 * Sync the data models selection in the parameter view.
		 */ 
		protected function handleDatasinkDataModelsChanged( event:EditEvent ):void {
			updateDatasinkDataModels();
		}
		
		/**
		 * Takes the currently selected datasink in the parameter view and
		 * adjust the datasink data models datagrid selection to match what
		 * is stored in the datasink model object.
		 */  
		private function updateDatasinkDataModels():void {
			var dsName:String = view.datasinkCombo.textInput.text;
			if (dsName!= null && dsName.length > 0) {
				populateAllDataModels();
				for each (var ds:Datasink in datasinkModel.selectedDatasinks.source) {
					if (ds.name == dsName) {
						setSelectedDatasinkDataModels(ds);
						break;
					}
				}
			}
		}

		/** Switches the datasink parameters in when a change in selection occurs */
		protected function handleDatasinkSelected( event:EditEvent ):void {
			var datasink:Datasink = event.parameters as Datasink;
			setDatasinkParams(datasink);
		}		
		
		/** Switches the transport parameters in when a change in selection occurs */
		protected function handleTransportSelected( event:EditEvent ):void {
			var transport:Transport = event.parameters as Transport;
			setTransportParams(transport);
		}		
		
		protected function bindModel():void {
			view.datasinksList = datasinkModel.selectedDatasinks;
			view.transportsList = transportModel.selectedTransports
			view.processGroupParams = processGroupModel.processGroups;
			view.customProcessGroups = processGroupConfig.getTenantOwnedProcessGroupsNoDatasinks(userModel.tenantId); 
			view.instanceTypes = instanceTypesTable.getAllInstanceTypes();
			
			/*
			setupDefaultDatasinkSelection();
			setupDefaultTransportSelection();
			setEditPermissions();
			*/

			// Testing
			//processGroupModel.addProcesGroup("jms.external");
			//processGroupModel.addProcesGroup("jms.internal");
		}
		
		private function setEditPermissions():void {
			var system:System = systemListModel.findSystem(sysPropModel.fullDomainName);
			view.allowVolumeEditing = (system == null) || (system.state == SystemState.NEW_SYSTEM);			
		}
		
		/**
		 * Every time a new system configuration is loaded we'll create a
		 * dictionary of datasink data models. When the data models are later
		 * added it will remove it from this dictionary. Once this dictionary
		 * is empty than we can start the add model and remove model logic.
		 */
		private function setSysLoadedDatasinkDataModels():void {
			for each (var ds:Datasink in datasinkModel.selectedDatasinks.source) {
				for (var i:int = 0; i < ds.dataModels.length; i++) {
					var dm:Object = ds.dataModels.getItemAt(i);
					if (dm.model != null) {
						onSysCfgLoadedDatasinkDataModels[dm.model] = true;
					}
				}
			}
		}
		
		protected function handleNewSystemConfigDone(event:SystemConfigServiceEvent):void {
			setupDefaultDatasinkSelection();
			setupDefaultTransportSelection();
			setEditPermissions();
		}
		
		protected function handleSystemConfigLoaded( event:SystemConfigServiceEvent ):void {
			setEditPermissions();
			setSysLoadedDatasinkDataModels();
		}
		
		protected var persistentIpAddressPopup:PersistentIpAddressPopup;
		
		protected function handlePersistentIpPopupBegin(event:PopupEvent):void {
			persistentIpAddressPopup = new PersistentIpAddressPopup();
			persistentIpAddressPopup.parameters = event.parameters;
			persistentIpAddressPopup.addEventListener(PopupEvent.SELECT_PERSISTENT_IP_END, handlePersistentIpPopupEnd);
			persistentIpAddressPopup.addEventListener(PopupEvent.MESSAGE_BEGIN, dispatch);
			persistentIpAddressPopup.addEventListener(CloseEvent.CLOSE, handlePersistentIpPopupClose);
			persistentIpAddressPopup.addEventListener(SystemConfigServiceEvent.ALLOCATE_PERSISTENT_IP, dispatch);
			PopUpManager.addPopUp(persistentIpAddressPopup, contextView, true);
			PopUpManager.centerPopUp(persistentIpAddressPopup);			
		}
		
		protected function handlePersistentIpPopupClose(event:CloseEvent):void {
			PopUpManager.removePopUp(persistentIpAddressPopup);
			handlePersistentIpPopupEnd(null);			
		}
		
		protected function handlePersistentIpPopupEnd(event:PopupEvent):void {
			persistentIpAddressPopup.removeEventListener(PopupEvent.SELECT_PERSISTENT_IP_END, handlePersistentIpPopupEnd);
			persistentIpAddressPopup.removeEventListener(PopupEvent.MESSAGE_BEGIN, dispatch);
			persistentIpAddressPopup.removeEventListener(CloseEvent.CLOSE, handlePersistentIpPopupClose);
			persistentIpAddressPopup.removeEventListener(SystemConfigServiceEvent.ALLOCATE_PERSISTENT_IP, dispatch);
			persistentIpAddressPopup = null;
			if ((event != null) && (event.parameters != null)) {
				var pg:ProcessGroup = view.processGroupParams.getItemAt(event.parameters.row) as ProcessGroup;
				pg.configPersistentIPAddress = (event.parameters.value as String);
				view.processGroupParams.refresh();
			}
		}
		
		protected function handlePersistentIpAllocated(event:SystemConfigServiceEvent):void {
			if (persistentIpAddressPopup != null) {
				persistentIpAddressPopup.acceptAllocatedIP(event.parameters as String);
			}
		}
		
		protected function handleRefreshProcessGroupParams(event:EditEvent):void {
			view.processGroupParams.refresh();
		}
		
		protected function handleRemoveProcessGroup(event:EditEvent):void {
			if (event.parameters != null) {
				var pgName:String = event.parameters as String;
				processGroupModel.removeProcessGroup(pgName);
			}
		}
		
		protected function handleCreateProcessGroupDone(event:SystemConfigServiceEvent):void {
			var params:Object = event.parameters;
			var message:String = "Custom Process Group Created";
			var creatingDatasink:Boolean = (params.datasink != null);
			if (creatingDatasink) {
				// Add the datasink to the in memory version
				
				var row:Object = DatasinkConfig.createRow(params.datasink.datasinkFqn,
														  params.datasink.autoScale,
														  params.datasink.usesBlockStorage,
														  params.datasink.scaleUpFactor,
														  params.datasink.pgDependencies,
														  params.datasink.threadsPerInstance);
				
				datasinkConfig.addRow(row);				
				datasinkModel.clear();
				datasinkModel.setup();
				datasinkModel.removeFromUnconfiguredList(params.datasink.datasinkFqn);
				
				message = "Custom Datasink and Process Group created";
			}
			
			var sm:StatusMessage = new StatusMessage(StatusType.INFO, message);
			var messagePopupEvent:PopupEvent = new PopupEvent(PopupEvent.MESSAGE_BEGIN, sm, null);			
			var reloadProcessGroupsDone:SystemConfigServiceEvent =
				new SystemConfigServiceEvent(SystemConfigServiceEvent.RELOAD_PROCESS_CONFIG_DONE, null, messagePopupEvent);
			dispatch(new SystemConfigServiceEvent(SystemConfigServiceEvent.RELOAD_PROCESS_CONFIG, null, reloadProcessGroupsDone));
		}
		
		protected function handleDeleteProcessGroupDone(event:SystemConfigServiceEvent):void {
			var params:Object = event.parameters;
			var message:String = "Custom Process Group deleted";
			var deletingDatasink:Boolean = (params.datasinkFqn != null);
			if (deletingDatasink) {
				var dsConfig:Object = datasinkConfig.lookupByFqn(params.datasinkFqn);
				if (dsConfig != null) {
					datasinkConfig.deleteRow(dsConfig);
					datasinkModel.clear();
					datasinkModel.setup();
				}
				message = "Custom Datasink and Process Group deleted";
			}
			var sm:StatusMessage = new StatusMessage(StatusType.INFO, message);
			var messagePopupEvent:PopupEvent = new PopupEvent(PopupEvent.MESSAGE_BEGIN, sm, null);			
			var reloadProcessGroupsDone:SystemConfigServiceEvent =
				new SystemConfigServiceEvent(SystemConfigServiceEvent.RELOAD_PROCESS_CONFIG_DONE, null, messagePopupEvent);
			dispatch(new SystemConfigServiceEvent(SystemConfigServiceEvent.RELOAD_PROCESS_CONFIG, null, reloadProcessGroupsDone));
		}
		
		protected function handleReloadProcessGroupsDone(event:SystemConfigServiceEvent):void {
			view.customProcessGroups = processGroupConfig.getTenantOwnedProcessGroupsNoDatasinks(userModel.tenantId);
			if (event.nestedEvent != null) {
				dispatch(event.nestedEvent);
			}
		}
		
		/************************ Process Group Configuration Popup *************************/
		
		protected var processGroupParametersPopup:ProcessGroupParametersPopup;
		
		protected function handleProcessGroupConfigBegin(event:PopupEvent):void {
			
			processGroupParametersPopup = new ProcessGroupParametersPopup();
			
			var paramSet:Object = event.parameters;
			if (paramSet == null) {
				paramSet = new Object();
			}
			paramSet.processGroup = null;
			processGroupParametersPopup.paramSet = paramSet;
			
			processGroupParametersPopup.addEventListener(
				PopupEvent.PROCESS_GROUP_CONFIG_END, handleProcessGroupConfigEnd);
			processGroupParametersPopup.addEventListener(
				CloseEvent.CLOSE, handleProcessGroupConfigClose );
			processGroupParametersPopup.addEventListener(
				PopupEvent.MESSAGE_BEGIN, dispatch );
			
			// Allows the processGroupParametersPopup to use the injector
			injector.injectInto(processGroupParametersPopup);
			
			PopUpManager.addPopUp(processGroupParametersPopup, contextView, true);
			PopUpManager.centerPopUp(processGroupParametersPopup);
		}
		
		protected function handleProcessGroupConfigClose(event:CloseEvent):void {
			PopUpManager.removePopUp(processGroupParametersPopup);
			handleProcessGroupConfigEnd(null);
		}
		
		protected function handleProcessGroupConfigEnd(event:PopupEvent):void {
			processGroupParametersPopup.removeEventListener(PopupEvent.PROCESS_GROUP_CONFIG_END, handleProcessGroupConfigEnd);
			processGroupParametersPopup.removeEventListener(CloseEvent.CLOSE, handleProcessGroupConfigClose );
			processGroupParametersPopup.removeEventListener(PopupEvent.MESSAGE_BEGIN, dispatch);
			if ((event != null) && (event.parameters != null)) {
				
				var params:Object = event.parameters;
				
				var createProcessGroupEvent:SystemConfigServiceEvent =
					new SystemConfigServiceEvent(SystemConfigServiceEvent.CREATE_PROCESS_GROUP, params.processGroup, null );
				
				var createProcessGroupDone:SystemConfigServiceEvent =
					new SystemConfigServiceEvent(SystemConfigServiceEvent.CREATE_PROCESS_GROUP_DONE, params, null );
								
				SpinnerPopup.createSpinnerPopup(contextView);
							
				var firstEvent:SystemConfigServiceEvent = createProcessGroupEvent;
				if (processGroupConfig.lookupByProcessGroupName(params.processGroup.pgName) != null) {
					// This process group already exists - it will need to be deleted and re-created
					
					var deleteProcessGroupEvent:SystemConfigServiceEvent =
						new SystemConfigServiceEvent(SystemConfigServiceEvent.DELETE_PROCESS_GROUP, params.processGroup, null);
					
					firstEvent = deleteProcessGroupEvent;
					
					deleteProcessGroupEvent.nestedEvent = createProcessGroupEvent;
				}
				
				var creatingJustProcessGroup:Boolean = (params.datasink == null);				
				if (creatingJustProcessGroup) {
					// Only creating a custom process group, no datasink
					
					params.processGroup.pgType = "CUSTOM";
					params.processGroup.canScale = "false";
					
					createProcessGroupEvent.nestedEvent = createProcessGroupDone;
				}
				else {
					// Creating a datasink plus a process group

					// Add the new process group as a dependency for the new datasink
					params.datasink.pgDependencies = StringUtils.trim(
						params.processGroup.pgName + ", " + params.datasink.pgDependencies);

					// If volumes are created for the process group, then this datasink uses block storage
					params.datasink.usesBlockStorage = (params.processGroup.numVolumes != "0");
					
					var createDatasinkEvent:SystemConfigServiceEvent =
						new SystemConfigServiceEvent(SystemConfigServiceEvent.CREATE_DATASINK, params.datasink, null );
					
					params.processGroup.pgType = "DATASINK";
					params.processGroup.canScale = (params.datasink.autoScale) ? "true" : "false";
						
					createProcessGroupEvent.nestedEvent = createDatasinkEvent;
					createDatasinkEvent.nestedEvent = createProcessGroupDone;
				}
				
				dispatch(firstEvent);
			}			
		}
		
		/************************ Process Group Selection (and Add) Popup *************************/
		
		protected var processGroupSelectorPopup:ProcessGroupSelectorPopup;
		
		protected function handleProcessGroupSelectBegin(event:PopupEvent):void {
			
			processGroupSelectorPopup = new ProcessGroupSelectorPopup();
			
			var operation:String = event.parameters as String;
			processGroupSelectorPopup.operation = operation;
			if (operation == view.ADD_PROCESS_GROUP) {
				processGroupSelectorPopup.title = "Select process group to add";
				processGroupSelectorPopup.dataProvider =
					processGroupConfig.getTenantOwnedProcessGroupsNoDatasinks(userModel.tenantId);				
			}
			else if (processGroupSelectorPopup.operation == view.DELETE_PROCESS_GROUP) {
				processGroupSelectorPopup.title = "Select process group to delete";
				processGroupSelectorPopup.dataProvider =
					processGroupConfig.getAllTenantOwnedProcessGroups(userModel.tenantId);				
			}
			
			processGroupSelectorPopup.addEventListener(
				PopupEvent.PROCESS_GROUP_SELECT_END, handleProcessGroupSelectEnd);
			processGroupSelectorPopup.addEventListener(
				CloseEvent.CLOSE, handleProcessGroupSelectClose );
			
			PopUpManager.addPopUp(processGroupSelectorPopup, contextView, true);
			PopUpManager.centerPopUp(processGroupSelectorPopup);
		}
		
		protected function handleProcessGroupSelectClose(event:CloseEvent):void {
			PopUpManager.removePopUp(processGroupSelectorPopup);
			handleProcessGroupSelectEnd(null);
		}
		
		protected function handleProcessGroupSelectEnd(event:PopupEvent):void {
			processGroupSelectorPopup.removeEventListener(PopupEvent.PROCESS_GROUP_CONFIG_END, handleProcessGroupSelectEnd);
			processGroupSelectorPopup.removeEventListener(CloseEvent.CLOSE, handleProcessGroupSelectClose );
			if ((event != null) && (event.parameters != null)) {
				var pgName:String = event.parameters as String;
				
				if (processGroupSelectorPopup.operation == view.ADD_PROCESS_GROUP) {
					processGroupModel.addProcessGroupWithErrorPopup(
						pgName, ProcessGroupTypes.CUSTOM, sysPropModel.selectedSize, sysPropModel.autoScale);
				}
				else if (processGroupSelectorPopup.operation == view.DELETE_PROCESS_GROUP) {
					SpinnerPopup.createSpinnerPopup(contextView);
					
					var params:Object = new Object();
					params.pgType = StringUtils.beforeFirst(pgName,".").toUpperCase();
					params.name = StringUtils.afterFirst(pgName,".");
					params.datasinkFqn = null;
					
					var deletePgDoneEvent:SystemConfigServiceEvent =
						new SystemConfigServiceEvent(SystemConfigServiceEvent.DELETE_PROCESS_GROUP_DONE, params, null);
					
					var nextEvent:SystemConfigServiceEvent = deletePgDoneEvent; 
					
					if (params.pgType == "DATASINK") {
						// Must delete the datasink row associated with this process group as well
						var datasinkConfig:Object = datasinkConfig.lookupByProcessGroupname(pgName);
						if (datasinkConfig != null) {
							var fqn:String = datasinkConfig[DatasinkConfig.FQN];
							var deleteDatasinkEvent:SystemConfigServiceEvent =
								new SystemConfigServiceEvent(SystemConfigServiceEvent.DELETE_DATASINK, fqn, deletePgDoneEvent);
							params.datasinkFqn = fqn;
							nextEvent = deleteDatasinkEvent;
						}
					}

					dispatch(new SystemConfigServiceEvent(SystemConfigServiceEvent.DELETE_PROCESS_GROUP, params, nextEvent));
				}
			}			
		}
	}
}
