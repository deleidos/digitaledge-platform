/**
 *                                  Apache License
 *                            Version 2.0, January 2004
 *                         http://www.apache.org/licenses/
 *
 *    TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION
 *
 *    1. Definitions.
 *
 *       "License" shall mean the terms and conditions for use, reproduction,
 *       and distribution as defined by Sections 1 through 9 of this document.
 *
 *       "Licensor" shall mean the copyright owner or entity authorized by
 *       the copyright owner that is granting the License.
 *
 *       "Legal Entity" shall mean the union of the acting entity and all
 *       other entities that control, are controlled by, or are under common
 *       control with that entity. For the purposes of this definition,
 *       "control" means (i) the power, direct or indirect, to cause the
 *       direction or management of such entity, whether by contract or
 *       otherwise, or (ii) ownership of fifty percent (50%) or more of the
 *       outstanding shares, or (iii) beneficial ownership of such entity.
 *
 *       "You" (or "Your") shall mean an individual or Legal Entity
 *       exercising permissions granted by this License.
 *
 *       "Source" form shall mean the preferred form for making modifications,
 *       including but not limited to software source code, documentation
 *       source, and configuration files.
 *
 *       "Object" form shall mean any form resulting from mechanical
 *       transformation or translation of a Source form, including but
 *       not limited to compiled object code, generated documentation,
 *       and conversions to other media types.
 *
 *       "Work" shall mean the work of authorship, whether in Source or
 *       Object form, made available under the License, as indicated by a
 *       copyright notice that is included in or attached to the work
 *       (an example is provided in the Appendix below).
 *
 *       "Derivative Works" shall mean any work, whether in Source or Object
 *       form, that is based on (or derived from) the Work and for which the
 *       editorial revisions, annotations, elaborations, or other modifications
 *       represent, as a whole, an original work of authorship. For the purposes
 *       of this License, Derivative Works shall not include works that remain
 *       separable from, or merely link (or bind by name) to the interfaces of,
 *       the Work and Derivative Works thereof.
 *
 *       "Contribution" shall mean any work of authorship, including
 *       the original version of the Work and any modifications or additions
 *       to that Work or Derivative Works thereof, that is intentionally
 *       submitted to Licensor for inclusion in the Work by the copyright owner
 *       or by an individual or Legal Entity authorized to submit on behalf of
 *       the copyright owner. For the purposes of this definition, "submitted"
 *       means any form of electronic, verbal, or written communication sent
 *       to the Licensor or its representatives, including but not limited to
 *       communication on electronic mailing lists, source code control systems,
 *       and issue tracking systems that are managed by, or on behalf of, the
 *       Licensor for the purpose of discussing and improving the Work, but
 *       excluding communication that is conspicuously marked or otherwise
 *       designated in writing by the copyright owner as "Not a Contribution."
 *
 *       "Contributor" shall mean Licensor and any individual or Legal Entity
 *       on behalf of whom a Contribution has been received by Licensor and
 *       subsequently incorporated within the Work.
 *
 *    2. Grant of Copyright License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       copyright license to reproduce, prepare Derivative Works of,
 *       publicly display, publicly perform, sublicense, and distribute the
 *       Work and such Derivative Works in Source or Object form.
 *
 *    3. Grant of Patent License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       (except as stated in this section) patent license to make, have made,
 *       use, offer to sell, sell, import, and otherwise transfer the Work,
 *       where such license applies only to those patent claims licensable
 *       by such Contributor that are necessarily infringed by their
 *       Contribution(s) alone or by combination of their Contribution(s)
 *       with the Work to which such Contribution(s) was submitted. If You
 *       institute patent litigation against any entity (including a
 *       cross-claim or counterclaim in a lawsuit) alleging that the Work
 *       or a Contribution incorporated within the Work constitutes direct
 *       or contributory patent infringement, then any patent licenses
 *       granted to You under this License for that Work shall terminate
 *       as of the date such litigation is filed.
 *
 *    4. Redistribution. You may reproduce and distribute copies of the
 *       Work or Derivative Works thereof in any medium, with or without
 *       modifications, and in Source or Object form, provided that You
 *       meet the following conditions:
 *
 *       (a) You must give any other recipients of the Work or
 *           Derivative Works a copy of this License; and
 *
 *       (b) You must cause any modified files to carry prominent notices
 *           stating that You changed the files; and
 *
 *       (c) You must retain, in the Source form of any Derivative Works
 *           that You distribute, all copyright, patent, trademark, and
 *           attribution notices from the Source form of the Work,
 *           excluding those notices that do not pertain to any part of
 *           the Derivative Works; and
 *
 *       (d) If the Work includes a "NOTICE" text file as part of its
 *           distribution, then any Derivative Works that You distribute must
 *           include a readable copy of the attribution notices contained
 *           within such NOTICE file, excluding those notices that do not
 *           pertain to any part of the Derivative Works, in at least one
 *           of the following places: within a NOTICE text file distributed
 *           as part of the Derivative Works; within the Source form or
 *           documentation, if provided along with the Derivative Works; or,
 *           within a display generated by the Derivative Works, if and
 *           wherever such third-party notices normally appear. The contents
 *           of the NOTICE file are for informational purposes only and
 *           do not modify the License. You may add Your own attribution
 *           notices within Derivative Works that You distribute, alongside
 *           or as an addendum to the NOTICE text from the Work, provided
 *           that such additional attribution notices cannot be construed
 *           as modifying the License.
 *
 *       You may add Your own copyright statement to Your modifications and
 *       may provide additional or different license terms and conditions
 *       for use, reproduction, or distribution of Your modifications, or
 *       for any such Derivative Works as a whole, provided Your use,
 *       reproduction, and distribution of the Work otherwise complies with
 *       the conditions stated in this License.
 *
 *    5. Submission of Contributions. Unless You explicitly state otherwise,
 *       any Contribution intentionally submitted for inclusion in the Work
 *       by You to the Licensor shall be under the terms and conditions of
 *       this License, without any additional terms or conditions.
 *       Notwithstanding the above, nothing herein shall supersede or modify
 *       the terms of any separate license agreement you may have executed
 *       with Licensor regarding such Contributions.
 *
 *    6. Trademarks. This License does not grant permission to use the trade
 *       names, trademarks, service marks, or product names of the Licensor,
 *       except as required for reasonable and customary use in describing the
 *       origin of the Work and reproducing the content of the NOTICE file.
 *
 *    7. Disclaimer of Warranty. Unless required by applicable law or
 *       agreed to in writing, Licensor provides the Work (and each
 *       Contributor provides its Contributions) on an "AS IS" BASIS,
 *       WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 *       implied, including, without limitation, any warranties or conditions
 *       of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
 *       PARTICULAR PURPOSE. You are solely responsible for determining the
 *       appropriateness of using or redistributing the Work and assume any
 *       risks associated with Your exercise of permissions under this License.
 *
 *    8. Limitation of Liability. In no event and under no legal theory,
 *       whether in tort (including negligence), contract, or otherwise,
 *       unless required by applicable law (such as deliberate and grossly
 *       negligent acts) or agreed to in writing, shall any Contributor be
 *       liable to You for damages, including any direct, indirect, special,
 *       incidental, or consequential damages of any character arising as a
 *       result of this License or out of the use or inability to use the
 *       Work (including but not limited to damages for loss of goodwill,
 *       work stoppage, computer failure or malfunction, or any and all
 *       other commercial damages or losses), even if such Contributor
 *       has been advised of the possibility of such damages.
 *
 *    9. Accepting Warranty or Additional Liability. While redistributing
 *       the Work or Derivative Works thereof, You may choose to offer,
 *       and charge a fee for, acceptance of support, warranty, indemnity,
 *       or other liability obligations and/or rights consistent with this
 *       License. However, in accepting such obligations, You may act only
 *       on Your own behalf and on Your sole responsibility, not on behalf
 *       of any other Contributor, and only if You agree to indemnify,
 *       defend, and hold each Contributor harmless for any liability
 *       incurred by, or claims asserted against, such Contributor by reason
 *       of your accepting any such warranty or additional liability.
 *
 *    END OF TERMS AND CONDITIONS
 *
 *    APPENDIX: How to apply the Apache License to your work.
 *
 *       To apply the Apache License to your work, attach the following
 *       boilerplate notice, with the fields enclosed by brackets "{}"
 *       replaced with your own identifying information. (Don't include
 *       the brackets!)  The text should be enclosed in the appropriate
 *       comment syntax for the file format. We also recommend that a
 *       file or class name and description of purpose be included on the
 *       same "printed page" as the copyright notice for easier
 *       identification within third-party archives.
 *
 *    Copyright {yyyy} {name of copyright owner}
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
package com.deleidos.rtws.tc.views.popups
{
	import com.deleidos.rtws.tc.events.LogEvent;
	import com.deleidos.rtws.tc.events.LogsEvent;
	import com.deleidos.rtws.tc.events.ParameterEvent;
	import com.deleidos.rtws.tc.models.Process;
	import com.deleidos.rtws.tc.models.ProcessGroup;
	import com.deleidos.rtws.tc.models.SystemModel;
	import com.deleidos.rtws.tc.utils.StatusMessageFactory;
	import com.deleidos.rtws.tc.utils.SystemUtilities;
	import com.deleidos.rtws.tc.utils.ViewUtilities;
	
	import flash.events.Event;
	import flash.events.FocusEvent;
	import flash.events.MouseEvent;
	import flash.utils.getTimer;
	
	import mx.collections.ArrayCollection;
	import mx.collections.Sort;
	import mx.collections.SortField;
	import mx.controls.Alert;
	import mx.controls.treeClasses.TreeItemRenderer;
	import mx.controls.treeClasses.TreeListData;
	import mx.events.CloseEvent;
	import mx.events.FlexEvent;
	import mx.events.ListEvent;
	import mx.managers.PopUpManager;
	import mx.rpc.events.FaultEvent;
	
	import org.robotlegs.mvcs.Mediator;
	
	import spark.components.VScrollBar;
	import spark.events.TextOperationEvent;
	import spark.formatters.NumberFormatter;

	public class LogViewerMediator extends Mediator
	{			
		[Inject] public var view:LogViewer;
		[Inject] public var systemModel:SystemModel;
		
		private const FORWARD:int = 0;
		private const BACKWARD:int = 1;
		
		private var selectedInstanceId:String;
		private var selectedParentPath:String;
		private var selectedFilePath:String;
		private var masterDns:String;
		private var instanceDns:String;
		private var treeSort:Sort;
		private var totalLines:int = -1;
		private var totalFiles:int = -1;
		private var retrieveDirection:int = -1;
		private var totalLinesRetrieved:int = 0;
		private var allLinesRetrieved:Boolean = false;
		private var lastSearchPosition:int = 0;
		
		private var startTime:int;
		
		private static const MAX_LINES :Number = 500;
		
		public override function onRegister():void
		{
			view.firstButton.addEventListener(MouseEvent.CLICK, handleFirstButton);
			view.lastButton.addEventListener(MouseEvent.CLICK, handleLastButton);
			view.closeButton.addEventListener(MouseEvent.CLICK, handleCloseButton);
			view.instanceListingTree.addEventListener(ListEvent.CHANGE, handleInstanceListingTreeChangeEvent);
			view.logListingTree.addEventListener(ListEvent.CHANGE, handleLogListingTreeChangeEvent);
			view.contentTextArea.scroller.verticalScrollBar.addEventListener(Event.CHANGE, handleVerticalScrollChangeEvent);
			view.findTextInput.addEventListener(TextOperationEvent.CHANGE, handleFindTextInputChangeEvent);
			view.findTextInput.addEventListener(FocusEvent.FOCUS_IN, handleFindTextFocusInEvent);
			view.findTextInput.addEventListener(FlexEvent.ENTER, handleFindTextEnterKeyEvent);
			view.searchForwardButton.addEventListener(MouseEvent.CLICK, handleSearchForwardButton);
			
			eventMap.mapListener(eventDispatcher, LogsEvent.RETRIEVE_LOG_FILES_LISTING_BY_PROXY_SUCCESS, handleRetrieveLogFilesListingByProxySuccess);
			eventMap.mapListener(eventDispatcher, LogsEvent.RETRIEVE_LOG_FILES_LISTING_BY_PROXY_ERROR, handleRetrieveLogFilesListingByProxyError);
			eventMap.mapListener(eventDispatcher, LogEvent.RETRIEVE_LOG_BY_PROXY_SUCCESS, handleRetrieveLogByProxySuccess);
			eventMap.mapListener(eventDispatcher, LogEvent.RETRIEVE_LOG_BY_PROXY_ERROR, handleRetrieveLogByProxyError);
			
			view.firstButton.enabled = false;
			view.lastButton.enabled = false;
			view.findTextInput.enabled = false;
			view.searchForwardButton.enabled = false;
			view.currentState = view.progressGroupOnly.name;
			
			this.treeSort = new Sort();
			treeSort.fields = [new SortField("label", true)];
			
			view.title = "Log Viewer for " + view.system.name;
			
			populateInstanceListingTree();
		}
		
		public override function onRemove():void
		{
			eventMap.unmapListener(eventDispatcher, LogsEvent.RETRIEVE_LOG_FILES_LISTING_BY_PROXY_SUCCESS, handleRetrieveLogFilesListingByProxySuccess);
			eventMap.unmapListener(eventDispatcher, LogsEvent.RETRIEVE_LOG_FILES_LISTING_BY_PROXY_ERROR, handleRetrieveLogFilesListingByProxyError);
			eventMap.unmapListener(eventDispatcher, LogEvent.RETRIEVE_LOG_BY_PROXY_SUCCESS, handleRetrieveLogByProxySuccess);
			eventMap.unmapListener(eventDispatcher, LogEvent.RETRIEVE_LOG_BY_PROXY_ERROR, handleRetrieveLogByProxyError);
			
			view.firstButton.removeEventListener(MouseEvent.CLICK, handleFirstButton);
			view.lastButton.removeEventListener(MouseEvent.CLICK, handleLastButton);
			view.closeButton.removeEventListener(MouseEvent.CLICK, handleCloseButton);
			view.instanceListingTree.removeEventListener(ListEvent.CHANGE, handleInstanceListingTreeChangeEvent);
			view.logListingTree.removeEventListener(ListEvent.CHANGE, handleLogListingTreeChangeEvent);
			view.contentTextArea.scroller.verticalScrollBar.removeEventListener(Event.CHANGE, handleVerticalScrollChangeEvent);
			view.findTextInput.removeEventListener(TextOperationEvent.CHANGE, handleFindTextInputChangeEvent);
			view.findTextInput.removeEventListener(FocusEvent.FOCUS_IN, handleFindTextFocusInEvent);
			view.findTextInput.removeEventListener(FlexEvent.ENTER, handleFindTextEnterKeyEvent);
			view.searchForwardButton.removeEventListener(MouseEvent.CLICK, handleSearchForwardButton);
		}
		
		protected function requestLogListingForCurrentInstance():void {
			
			view.firstButton.enabled = false;
			view.lastButton.enabled = false;
			view.findTextInput.enabled = false;
			view.searchForwardButton.enabled = false;				
			view.contentTextArea.text = "";
			view.logListingData = new ArrayCollection();
			
			var eventQueue:Array = new Array();
			eventQueue.push(new LogsEvent(LogsEvent.RETRIEVE_LOG_FILES_LISTING_BY_PROXY, 
				{"proxyhost" : this.masterDns, "targethost" : this.instanceDns}));
			
			var successEvents:Array = new Array();
			successEvents.push(LogsEvent.RETRIEVE_LOG_FILES_LISTING_BY_PROXY_SUCCESS);
			
			var errorEvents:Array = new Array();
			errorEvents.push(LogsEvent.RETRIEVE_LOG_FILES_LISTING_BY_PROXY_ERROR);
			
			ViewUtilities.dispatchProcessingView(mediatorMap, contextView, eventQueue, successEvents, errorEvents, false);			
		}

		/**
		 * Called when the user selects an instance.
		 */
		protected function handleInstanceListingTreeChangeEvent(event:ListEvent):void
		{
			var renderer:TreeItemRenderer = TreeItemRenderer(event.itemRenderer);
			var listData:TreeListData = TreeListData(renderer.listData);
			
			if (listData != null && ! listData.hasChildren) {
								
				var pg:String = String(view.instanceListingTree.getParentItem(listData));
				
				this.selectedInstanceId = listData.label;
				
				if (view.system.vpcId == null || view.system.vpcId == "-") {
					var master:Process = SystemUtilities.locateMasterNode(view.system);
					this.masterDns = master.privateIpAddress;
				} else {
					this.masterDns = view.system.name;
				}
				
				var process:Process = SystemUtilities.locateProcess(view.system, this.selectedInstanceId);
				this.instanceDns = process.privateIpAddress;
				
				requestLogListingForCurrentInstance();
			}
		}
		
		private function reset(direction:int):void {
			totalLines = -1;
			totalFiles = -1;
			totalLinesRetrieved = 0;
			retrieveDirection = direction;
			view.contentTextArea.text = "";
			allLinesRetrieved = false;
		}
		
		private function makeFaultString(faultEvent:FaultEvent):String {
			return faultEvent.fault.faultString + ", code=" + faultEvent.statusCode;
		}

		/**
		 * Called when the user selects a log file.
		 */
		protected function handleLogListingTreeChangeEvent(event:ListEvent):void
		{
			var renderer:TreeItemRenderer = TreeItemRenderer(event.itemRenderer);
			var listData:TreeListData = TreeListData(renderer.listData);

			if (! listData.hasChildren) {
				var parent:Object = view.logListingTree.getParentItem(listData);
				this.selectedParentPath = parent.label;
				this.selectedFilePath = parent.label + '/' + listData.label;

				reset(FORWARD);

				var start:Number = 1;
				var end:Number = MAX_LINES;

				view.retrievingLabel.visible = view.inlineSpinner.visible = false;
				
				var eventQueue:Array = new Array();
				eventQueue.push(new LogEvent(LogEvent.RETRIEVE_LOG_BY_PROXY, {"proxyhost" : this.masterDns, 
					"targethost" : this.instanceDns, "filepath" : this.selectedFilePath, "start" : start.toString(), "end" : end.toString()}));
				
				var successEvents:Array = new Array();
				successEvents.push(LogEvent.RETRIEVE_LOG_BY_PROXY_SUCCESS);
				
				var errorEvents:Array = new Array();
				errorEvents.push(LogEvent.RETRIEVE_LOG_BY_PROXY_ERROR);
				
				ViewUtilities.dispatchProcessingView(mediatorMap, contextView, eventQueue, successEvents, errorEvents, false);
			}
		}
		
		protected function handleRetrieveLogFilesListingByProxySuccess(event:LogsEvent):void
		{
			view.message = null;
			view.logListingData = event.parameters.logs;
			view.logListingData.sort = this.treeSort;
			
			for (var i:int = 0; i < view.logListingData.length; i++)
			{
				var directory:Object = view.logListingData.getItemAt(i);
				
				if (directory["children"] != null) 
				{
					var logs:ArrayCollection = ArrayCollection(directory["children"]);
					logs.sort = this.treeSort;
					logs.refresh();
				}
			}
			
			view.currentState = view.instanceSelected.name;
			
			view.logListingData.refresh();
		}
		
		protected function handleRetrieveLogFilesListingByProxyError(event:LogsEvent):void
		{
			var faultEvent:FaultEvent = event.parameters.faultEvent;
			view.message = StatusMessageFactory.createErrorMessage("Unable to retrieve log file listing from target host ("+ makeFaultString(faultEvent) + ").");
		}
		
		protected function fileCountChangedAlertHandler(event:CloseEvent):void {
			requestLogListingForCurrentInstance();			
		}
		
		protected function handleRetrieveLogByProxySuccess(event:LogEvent):void
		{
			view.message = null;
			
			var elapsed:int = getTimer() - startTime;
			// trace("Retrieval response time = " + elapsed);
			startTime = getTimer();
				
			var filepath:String = event.parameters.context.filepath;
			var startString:String = event.parameters.context.start;
			var endString:String = event.parameters.context.end;
			var vScrollBar:VScrollBar = view.contentTextArea.scroller.verticalScrollBar;
			var oldTotalLinesRetrieved:int = totalLinesRetrieved;
			var eventTotalFiles:int = totalFiles;

			// Re-enable controls
			view.retrievingLabel.visible = view.inlineSpinner.visible = false;
			view.instanceListingTree.enabled = true;
			view.logListingTree.enabled = true;
			
			// Get totalLines and totalFiles if the values make sense
			if (event.parameters.totalLines > 0) {
				totalLines = event.parameters.totalLines;				
			}
			if (event.parameters.totalFiles > 0) {
				eventTotalFiles = event.parameters.totalFiles;				
			}

			// Detect if the number of files has changed
			if ((totalFiles > 0) && (eventTotalFiles != totalFiles)) {
				Alert.show(
					"The number of files in " + selectedParentPath + " has changed, so the process group log list must be refreshed.",
					"Warning",
					Alert.OK,
					view,
					fileCountChangedAlertHandler);				
			}
			else {
				var retrievedLines:String = "";
				var originalContentLength:int = view.contentTextArea.text.length;
				var linesRetrieved:int = 0;
				var content:Object = event.parameters.content;
				var startKey:int = int.MAX_VALUE;
				var counter:int = 0;

				totalFiles = eventTotalFiles;

				// Get the lines, make sure we do it in order
				for (var key:String in content) {
					startKey = Math.min(parseInt(key), startKey);
					linesRetrieved++;
				}
				for (var i:int=0; i<linesRetrieved; i++) {
					retrievedLines += content[startKey++] + "\n";
				}
	
				// Total the lines retrieved, determine if there's no more file
				totalLinesRetrieved += linesRetrieved;
				allLinesRetrieved = ((totalLines > 0) && (totalLinesRetrieved >= totalLines)) || (linesRetrieved < MAX_LINES);

				// Markers
				var endOfFileMarker:String = "____________________ End of File ____________________\n";							
				if ((startString == "1") || ((startString=="-1") && allLinesRetrieved)) {
					retrievedLines = "____________________ Start of File ____________________\n" + retrievedLines;
				}
				
				if (retrieveDirection == FORWARD) {
					// Did we get to the end?
					if (allLinesRetrieved) {
						retrievedLines = retrievedLines + endOfFileMarker;
					}
					// Append to the end
					view.contentTextArea.text = view.contentTextArea.text + retrievedLines;
				}
				else {
					// When going in reverse the first time is EOF
					if (startString == "-1") {
						retrievedLines = retrievedLines + endOfFileMarker;
					}
					// Insert into the beginning
					view.contentTextArea.text = retrievedLines + view.contentTextArea.text;
				}
	
				// Determine the state of the log textarea and compute the percent complete
				if ((totalLinesRetrieved == 0) && (linesRetrieved == 0)) {
					view.currentState = view.logFileEmpty.name;
					view.percentInFileLabel.text = "100%";
				}
				else if (allLinesRetrieved) {
					view.percentInFileLabel.text = "100%";
					view.currentState = view.logFileSelected.name;					
				}
				else if (totalLines <= 0) {
					view.currentState = view.logFileSelected.name;
					view.percentInFileLabel.text = "";
				}
				else {
					view.currentState = view.logFileSelected.name;
					var percent:Number = ((totalLinesRetrieved as Number) / (totalLines as Number)) * 100.0;
					var numberFormatter:NumberFormatter = new NumberFormatter();
					numberFormatter.fractionalDigits = 0;
					view.percentInFileLabel.text = numberFormatter.format(percent) + "%"; 
				}
	
				view.contentTextArea.validateNow();
				
				if (startString == "-1") {
					// Scroll to the bottom if this is a "Last" request
					view.contentTextArea.scrollToRange(view.contentTextArea.text.length-1, view.contentTextArea.text.length);
				}
				else if (retrieveDirection == FORWARD) {
					// Scroll to around where the new text is
					view.contentTextArea.scrollToRange(originalContentLength-1, originalContentLength);
				}
				else if (retrieveDirection == BACKWARD) {
					// Scroll to ensure the new text is visible
					view.contentTextArea.scrollToRange(retrievedLines.length, retrievedLines.length+1);
				}
				
				view.firstButton.enabled = true;
				view.lastButton.enabled = true;			
				view.findTextInput.enabled = true;
				view.searchForwardButton.enabled = true;
			}
			
			elapsed = getTimer() - startTime;
			// trace("Refresh response time = " + elapsed);
		}
		
		protected function handleRetrieveLogByProxyError(event:LogEvent):void
		{
			var context:Object = event.parameters.context;
			var faultEvent:FaultEvent = event.parameters.faultEvent;
			view.contentTextArea.text = "";
			var msg:String = "Unable to retrieve log file from target host (" + makeFaultString(faultEvent) + ")"
			if ((faultEvent.statusCode == 500) && (retrieveDirection == BACKWARD)) {
				msg += ". Note that the Last button is only supported on systems running version 1.2 or greater";
			}
			msg += ".";
			view.message = StatusMessageFactory.createErrorMessage(msg);
			view.retrievingLabel.visible = view.inlineSpinner.visible = false;			
			view.firstButton.enabled = true;
			view.lastButton.enabled = true;
			view.instanceListingTree.enabled = true;
			view.logListingTree.enabled = true;
			view.findTextInput.enabled = true;
			view.searchForwardButton.enabled = true;
		}
		
		protected function handleFirstButton(event:MouseEvent):void
		{
			reset(FORWARD);
			
			var start:Number = 1;
			var end:Number = MAX_LINES;
			
			var eventQueue:Array = new Array();
			eventQueue.push(new LogEvent(LogEvent.RETRIEVE_LOG_BY_PROXY, {"proxyhost" : this.masterDns, 
				"targethost" : this.instanceDns, "filepath" : this.selectedFilePath, 
				"start" : start.toString(), "end" : end.toString()}));
			
			var successEvents:Array = new Array();
			successEvents.push(LogEvent.RETRIEVE_LOG_BY_PROXY_SUCCESS);
			
			var errorEvents:Array = new Array();
			errorEvents.push(LogEvent.RETRIEVE_LOG_BY_PROXY_ERROR);
			
			startTime = getTimer();
			
			ViewUtilities.dispatchProcessingView(mediatorMap, contextView, eventQueue, successEvents, errorEvents, false);
		}
		
		protected function handleLastButton(event:MouseEvent):void
		{
			reset(BACKWARD);
			
			var start:Number = -1;
			var end:Number = -1;
			
			var eventQueue:Array = new Array();
			eventQueue.push(new LogEvent(LogEvent.RETRIEVE_LOG_BY_PROXY, {"proxyhost" : this.masterDns, 
				"targethost" : this.instanceDns, "filepath" : this.selectedFilePath, 
				"start" : start.toString(), "end" : end.toString()}));
			
			var successEvents:Array = new Array();
			successEvents.push(LogEvent.RETRIEVE_LOG_BY_PROXY_SUCCESS);
			
			var errorEvents:Array = new Array();
			errorEvents.push(LogEvent.RETRIEVE_LOG_BY_PROXY_ERROR);
			
			startTime = getTimer();
			
			ViewUtilities.dispatchProcessingView(mediatorMap, contextView, eventQueue, successEvents, errorEvents, false);
		}
		
		protected function handleCloseButton(event:MouseEvent):void
		{
			closeView();
		}
		
		protected function requestLogData(start:Number, end:Number):void {
			view.retrievingLabel.visible = view.inlineSpinner.visible = true;
			view.firstButton.enabled = false;
			view.lastButton.enabled = false;
			view.instanceListingTree.enabled = false;
			view.logListingTree.enabled = false;
			view.findTextInput.enabled = false;
			view.searchForwardButton.enabled = false;
			
			startTime = getTimer();
			
			var retrieveLogEvent:LogEvent =
				new LogEvent( LogEvent.RETRIEVE_LOG_BY_PROXY,
					{"proxyhost":this.masterDns, "targethost":this.instanceDns, "filepath":this.selectedFilePath,
					 "start" : start.toString(), "end" : end.toString()});
			
			dispatch(retrieveLogEvent);
		}
		
		protected function handleVerticalScrollChangeEvent(event:Event):void {
			if ((!allLinesRetrieved) && (!view.inlineSpinner.visible)) {
				var vscroll:VScrollBar = event.currentTarget as VScrollBar;
				var start:Number;
				var end:Number;				
				var eventQueue:Array;
				var successEvents:Array;
				var errorEvents:Array;
				
				if ((vscroll.value == 0) && (retrieveDirection == BACKWARD)) {
					end = totalLines - totalLinesRetrieved - 1;
					start = Math.max(0, end - MAX_LINES) + 1;
					requestLogData(start, end);
				}
				else if ((vscroll.value == vscroll.maximum) && (retrieveDirection == FORWARD)) {
					start = totalLinesRetrieved+1;
					end = start + MAX_LINES - 1;
					requestLogData(start, end);
				}				
			}
		}
		
		protected function handleFindTextInputChangeEvent(event:TextOperationEvent):void {
			view.message = null;
			lastSearchPosition = -1;			
		}
		
		protected function handleFindTextEnterKeyEvent(event:FlexEvent):void {
			handleSearchForwardButton(null);
		}
		
		protected function handleFindTextFocusInEvent(event:FocusEvent):void {
			// trace("text focus in");
			view.findTextInput.selectAll();			
		}
		
		protected function handleSearchForwardButton(event:MouseEvent):void {
			var search:String = view.findTextInput.text;
			var anchor:int = view.contentTextArea.text.indexOf(search, lastSearchPosition+1);
			if (anchor < 0) {
				view.message = StatusMessageFactory.createWarnMessage("'" + search + "' not found.");				
			}
			else {
				view.contentTextArea.setFocus();
				view.contentTextArea.scrollToRange(anchor, anchor+(search.length));
				view.contentTextArea.selectRange(anchor, anchor+(search.length));
				lastSearchPosition = anchor;
				// view.contentTextArea.validateNow();
			}
		}
		
		private function populateInstanceListingTree():void 
		{
			var instanceListingData:ArrayCollection = new ArrayCollection();
			
			var pgs:ArrayCollection = view.system.groups;
			
			for (var i:int = 0; i < pgs.length; i++) {
				var pg:ProcessGroup = pgs[i];
				
				var group:Object = new Object();
				group["label"] = pg.groupName;
				group["tooltip"] = pg.groupName;
				
				var instances:ArrayCollection = new ArrayCollection();
				
				var processes:ArrayCollection = pg.processes;
				for (var j:int = 0; j < processes.length; j++) {
					var process:Process = processes[j];
					
					var instance:Object = new Object();
					instance["label"] = process.id;
					instance["tooltip"] = process.persistentDnsName;
					
					instances.addItem(instance);
				}
				
				group["children"] = instances;
				
				instanceListingData.addItem(group);
			}

			view.instanceListingData = instanceListingData;
			
			if (pgs.length == 0) {
				view.message = StatusMessageFactory.createErrorMessage("There are no instances available for this system.");				
			}
		}
		
		private function closeView():void 
		{
			PopUpManager.removePopUp(view);
			mediatorMap.removeMediator(this);	
		}
	}
}