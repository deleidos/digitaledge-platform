/**
 *                                  Apache License
 *                            Version 2.0, January 2004
 *                         http://www.apache.org/licenses/
 *
 *    TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION
 *
 *    1. Definitions.
 *
 *       "License" shall mean the terms and conditions for use, reproduction,
 *       and distribution as defined by Sections 1 through 9 of this document.
 *
 *       "Licensor" shall mean the copyright owner or entity authorized by
 *       the copyright owner that is granting the License.
 *
 *       "Legal Entity" shall mean the union of the acting entity and all
 *       other entities that control, are controlled by, or are under common
 *       control with that entity. For the purposes of this definition,
 *       "control" means (i) the power, direct or indirect, to cause the
 *       direction or management of such entity, whether by contract or
 *       otherwise, or (ii) ownership of fifty percent (50%) or more of the
 *       outstanding shares, or (iii) beneficial ownership of such entity.
 *
 *       "You" (or "Your") shall mean an individual or Legal Entity
 *       exercising permissions granted by this License.
 *
 *       "Source" form shall mean the preferred form for making modifications,
 *       including but not limited to software source code, documentation
 *       source, and configuration files.
 *
 *       "Object" form shall mean any form resulting from mechanical
 *       transformation or translation of a Source form, including but
 *       not limited to compiled object code, generated documentation,
 *       and conversions to other media types.
 *
 *       "Work" shall mean the work of authorship, whether in Source or
 *       Object form, made available under the License, as indicated by a
 *       copyright notice that is included in or attached to the work
 *       (an example is provided in the Appendix below).
 *
 *       "Derivative Works" shall mean any work, whether in Source or Object
 *       form, that is based on (or derived from) the Work and for which the
 *       editorial revisions, annotations, elaborations, or other modifications
 *       represent, as a whole, an original work of authorship. For the purposes
 *       of this License, Derivative Works shall not include works that remain
 *       separable from, or merely link (or bind by name) to the interfaces of,
 *       the Work and Derivative Works thereof.
 *
 *       "Contribution" shall mean any work of authorship, including
 *       the original version of the Work and any modifications or additions
 *       to that Work or Derivative Works thereof, that is intentionally
 *       submitted to Licensor for inclusion in the Work by the copyright owner
 *       or by an individual or Legal Entity authorized to submit on behalf of
 *       the copyright owner. For the purposes of this definition, "submitted"
 *       means any form of electronic, verbal, or written communication sent
 *       to the Licensor or its representatives, including but not limited to
 *       communication on electronic mailing lists, source code control systems,
 *       and issue tracking systems that are managed by, or on behalf of, the
 *       Licensor for the purpose of discussing and improving the Work, but
 *       excluding communication that is conspicuously marked or otherwise
 *       designated in writing by the copyright owner as "Not a Contribution."
 *
 *       "Contributor" shall mean Licensor and any individual or Legal Entity
 *       on behalf of whom a Contribution has been received by Licensor and
 *       subsequently incorporated within the Work.
 *
 *    2. Grant of Copyright License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       copyright license to reproduce, prepare Derivative Works of,
 *       publicly display, publicly perform, sublicense, and distribute the
 *       Work and such Derivative Works in Source or Object form.
 *
 *    3. Grant of Patent License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       (except as stated in this section) patent license to make, have made,
 *       use, offer to sell, sell, import, and otherwise transfer the Work,
 *       where such license applies only to those patent claims licensable
 *       by such Contributor that are necessarily infringed by their
 *       Contribution(s) alone or by combination of their Contribution(s)
 *       with the Work to which such Contribution(s) was submitted. If You
 *       institute patent litigation against any entity (including a
 *       cross-claim or counterclaim in a lawsuit) alleging that the Work
 *       or a Contribution incorporated within the Work constitutes direct
 *       or contributory patent infringement, then any patent licenses
 *       granted to You under this License for that Work shall terminate
 *       as of the date such litigation is filed.
 *
 *    4. Redistribution. You may reproduce and distribute copies of the
 *       Work or Derivative Works thereof in any medium, with or without
 *       modifications, and in Source or Object form, provided that You
 *       meet the following conditions:
 *
 *       (a) You must give any other recipients of the Work or
 *           Derivative Works a copy of this License; and
 *
 *       (b) You must cause any modified files to carry prominent notices
 *           stating that You changed the files; and
 *
 *       (c) You must retain, in the Source form of any Derivative Works
 *           that You distribute, all copyright, patent, trademark, and
 *           attribution notices from the Source form of the Work,
 *           excluding those notices that do not pertain to any part of
 *           the Derivative Works; and
 *
 *       (d) If the Work includes a "NOTICE" text file as part of its
 *           distribution, then any Derivative Works that You distribute must
 *           include a readable copy of the attribution notices contained
 *           within such NOTICE file, excluding those notices that do not
 *           pertain to any part of the Derivative Works, in at least one
 *           of the following places: within a NOTICE text file distributed
 *           as part of the Derivative Works; within the Source form or
 *           documentation, if provided along with the Derivative Works; or,
 *           within a display generated by the Derivative Works, if and
 *           wherever such third-party notices normally appear. The contents
 *           of the NOTICE file are for informational purposes only and
 *           do not modify the License. You may add Your own attribution
 *           notices within Derivative Works that You distribute, alongside
 *           or as an addendum to the NOTICE text from the Work, provided
 *           that such additional attribution notices cannot be construed
 *           as modifying the License.
 *
 *       You may add Your own copyright statement to Your modifications and
 *       may provide additional or different license terms and conditions
 *       for use, reproduction, or distribution of Your modifications, or
 *       for any such Derivative Works as a whole, provided Your use,
 *       reproduction, and distribution of the Work otherwise complies with
 *       the conditions stated in this License.
 *
 *    5. Submission of Contributions. Unless You explicitly state otherwise,
 *       any Contribution intentionally submitted for inclusion in the Work
 *       by You to the Licensor shall be under the terms and conditions of
 *       this License, without any additional terms or conditions.
 *       Notwithstanding the above, nothing herein shall supersede or modify
 *       the terms of any separate license agreement you may have executed
 *       with Licensor regarding such Contributions.
 *
 *    6. Trademarks. This License does not grant permission to use the trade
 *       names, trademarks, service marks, or product names of the Licensor,
 *       except as required for reasonable and customary use in describing the
 *       origin of the Work and reproducing the content of the NOTICE file.
 *
 *    7. Disclaimer of Warranty. Unless required by applicable law or
 *       agreed to in writing, Licensor provides the Work (and each
 *       Contributor provides its Contributions) on an "AS IS" BASIS,
 *       WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 *       implied, including, without limitation, any warranties or conditions
 *       of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
 *       PARTICULAR PURPOSE. You are solely responsible for determining the
 *       appropriateness of using or redistributing the Work and assume any
 *       risks associated with Your exercise of permissions under this License.
 *
 *    8. Limitation of Liability. In no event and under no legal theory,
 *       whether in tort (including negligence), contract, or otherwise,
 *       unless required by applicable law (such as deliberate and grossly
 *       negligent acts) or agreed to in writing, shall any Contributor be
 *       liable to You for damages, including any direct, indirect, special,
 *       incidental, or consequential damages of any character arising as a
 *       result of this License or out of the use or inability to use the
 *       Work (including but not limited to damages for loss of goodwill,
 *       work stoppage, computer failure or malfunction, or any and all
 *       other commercial damages or losses), even if such Contributor
 *       has been advised of the possibility of such damages.
 *
 *    9. Accepting Warranty or Additional Liability. While redistributing
 *       the Work or Derivative Works thereof, You may choose to offer,
 *       and charge a fee for, acceptance of support, warranty, indemnity,
 *       or other liability obligations and/or rights consistent with this
 *       License. However, in accepting such obligations, You may act only
 *       on Your own behalf and on Your sole responsibility, not on behalf
 *       of any other Contributor, and only if You agree to indemnify,
 *       defend, and hold each Contributor harmless for any liability
 *       incurred by, or claims asserted against, such Contributor by reason
 *       of your accepting any such warranty or additional liability.
 *
 *    END OF TERMS AND CONDITIONS
 *
 *    APPENDIX: How to apply the Apache License to your work.
 *
 *       To apply the Apache License to your work, attach the following
 *       boilerplate notice, with the fields enclosed by brackets "{}"
 *       replaced with your own identifying information. (Don't include
 *       the brackets!)  The text should be enclosed in the appropriate
 *       comment syntax for the file format. We also recommend that a
 *       file or class name and description of purpose be included on the
 *       same "printed page" as the copyright notice for easier
 *       identification within third-party archives.
 *
 *    Copyright {yyyy} {name of copyright owner}
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
package com.deleidos.rtws.systemcfg.views
{
	import com.deleidos.rtws.commons.model.auth.IUser;
	import com.deleidos.rtws.commons.model.datamodel.DataModelName;
	import com.deleidos.rtws.commons.model.status.StatusMessage;
	import com.deleidos.rtws.commons.model.status.StatusType;
	import com.deleidos.rtws.commons.tenantutils.model.system.System;
	import com.deleidos.rtws.commons.tenantutils.model.system.SystemState;
	import com.deleidos.rtws.commons.util.ObjectUtils;
	import com.deleidos.rtws.commons.util.StringUtils;
	import com.deleidos.rtws.systemcfg.dao.ProcessGroupConfig;
	import com.deleidos.rtws.systemcfg.events.EditEvent;
	import com.deleidos.rtws.systemcfg.events.ModelServiceEvent;
	import com.deleidos.rtws.systemcfg.events.PopupEvent;
	import com.deleidos.rtws.systemcfg.events.SystemConfigServiceEvent;
	import com.deleidos.rtws.systemcfg.models.DataModelModel;
	import com.deleidos.rtws.systemcfg.models.Datasink;
	import com.deleidos.rtws.systemcfg.models.DatasinkModel;
	import com.deleidos.rtws.systemcfg.models.DnsAvailability;
	import com.deleidos.rtws.systemcfg.models.GeneralParameter;
	import com.deleidos.rtws.systemcfg.models.LoadStateManager;
	import com.deleidos.rtws.systemcfg.models.NamedItem;
	import com.deleidos.rtws.systemcfg.models.ProcessGroup;
	import com.deleidos.rtws.systemcfg.models.ProcessGroupModel;
	import com.deleidos.rtws.systemcfg.models.SplitterModel;
	import com.deleidos.rtws.systemcfg.models.SystemBuilderPropertiesModel;
	import com.deleidos.rtws.systemcfg.models.SystemListModel;
	import com.deleidos.rtws.systemcfg.models.SystemPropertiesModel;
	import com.deleidos.rtws.systemcfg.models.Transport;
	import com.deleidos.rtws.systemcfg.models.TransportModel;
	import com.deleidos.rtws.systemcfg.models.WebappModel;
	
	import flash.events.Event;
	
	import mx.collections.ArrayCollection;
	import mx.collections.ArrayList;
	import mx.events.CloseEvent;
	import mx.managers.PopUpManager;
	import mx.utils.ObjectUtil;
	
	import org.robotlegs.mvcs.Mediator;
	
	public class HomeViewMediator extends Mediator
	{
		[Inject] public var view:HomeView;
		
		[Inject] public var dataModelModel:DataModelModel;
		[Inject] public var splitterModel:SplitterModel;
		[Inject] public var datasinkModel:DatasinkModel;
		[Inject] public var transportModel:TransportModel;
		[Inject] public var sysPropModel:SystemPropertiesModel;
		[Inject] public var webappModel:WebappModel;
		[Inject] public var systemBuilderProperties:SystemBuilderPropertiesModel;
		[Inject] public var systemListModel:SystemListModel;
		[Inject] public var processGroupModel:ProcessGroupModel;
		[Inject] public var processGroupConfig:ProcessGroupConfig;
		[Inject] public var userModel:IUser;
		[Inject] public var loadStateManager:LoadStateManager;
		
		private var selectionPopupInit:Array;
		
		override public function onRegister():void {
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.SYSTEM_CONFIG_LOADED, handleSystemConfigLoaded);
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.SYSTEM_FULLY_LOADED, handleSystemFullyLoaded);
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.SYSTEM_CONFIG_SAVED, handleSystemSaved);
			eventMap.mapListener(eventDispatcher, SystemConfigServiceEvent.NEW_SYSTEM_CONFIG_DONE, handleNewSystemConfig );
			eventMap.mapListener(eventDispatcher, EditEvent.SYS_PROP_SELECTIONS_CHANGED, handleSysPropSelectionsChanged);
			eventMap.mapListener(eventDispatcher, EditEvent.SELECTED_DATA_MODELS_CHANGED, handleSelectedDataModelsChanged );
			eventMap.mapListener(eventDispatcher, EditEvent.SELECTED_DATASINKS_CHANGED, handleSelectedDataSinksChanged );
			eventMap.mapListener(eventDispatcher, EditEvent.SELECTED_TRANSPORTS_CHANGED, handleSelectedTransportsChanged );	
			
			eventMap.mapListener(view, PopupEvent.SELECT_ITEM_DESCRIPTION_BEGIN, handleSelectItemDescriptionBegin );
			eventMap.mapListener(view, ModelServiceEvent.LOAD_MODEL_DIR, dispatch); 
			eventMap.mapListener(view, EditEvent.REMOVE_DATAMODEL,  dispatch);
			eventMap.mapListener(view, EditEvent.REMOVE_DATASINK,  dispatch);
			eventMap.mapListener(view, EditEvent.REMOVE_TRANSPORT, dispatch);
			eventMap.mapListener(view, EditEvent.REMOVE_WEBAPP,    dispatch);
			eventMap.mapListener(view, PopupEvent.MESSAGE_BEGIN, dispatch);
			
			eventMap.mapListener(view, EditEvent.SET_DOMAIN_NAME,    		handleSetDomainName);
			eventMap.mapListener(view, EditEvent.SET_APPLIANCE_DOMAIN_NAME,	handleSetApplianceDomainName);			
			eventMap.mapListener(view, EditEvent.SET_AUTOSCALE,      		handleSetAutoScale);
			eventMap.mapListener(view, EditEvent.SET_VPC,      		 		handleSetVpc);
			eventMap.mapListener(view, EditEvent.SET_APPLIANCE,      		handleSetAppliance);
			eventMap.mapListener(view, EditEvent.SET_SYSTEM_SIZE,    		handleSetSystemSize);
			eventMap.mapListener(view, EditEvent.SET_SW_VERSION,     		handleSetSwVersion);
			eventMap.mapListener(view, EditEvent.SET_SERVICE_REGION, 		handleSetServiceRegion);
			eventMap.mapListener(view, EditEvent.SET_SERVICE_ZONE,   		handleSetServiceZone);
			addViewListener(EditEvent.SET_RTWS_PUBLIC_DNS_AVAILABILITY, 	onRtwsPubDnsAvailChange);
			
			eventMap.mapListener(view, EditEvent.SET_CONFIG_MAJOR_VERSION, handleSetConfigMajorVersion);
			eventMap.mapListener(view, EditEvent.SET_CONFIG_MINOR_VERSION, handleSetConfigMinorVersion);
			
			eventMap.mapListener(view, EditEvent.EDIT_DATASINK_PARAMS, handleEditDatasinkParams);
			eventMap.mapListener(view, EditEvent.EDIT_TRANSPORT_PARAMS, handleEditTransportParams);
			
			eventMap.mapListener(view, PopupEvent.DATASINK_CONFIG_BEGIN, handleDatasinkConfigBegin);
			
			eventMap.mapListener(eventDispatcher, EditEvent.TRANSPORT_UPDATED, handleUpdateTransportParams);
			eventMap.mapListener(eventDispatcher, EditEvent.DATASINK_UPDATED, handleUpdateDatasinkParams);
			
			bindModel();

			/** Setup array of initialization data for selection popup */
			selectionPopupInit = new Array();
			
			selectionPopupInit[EditEvent.ADD_DATASINK]  =
				{ selectTitle  :"Select a Data Sink",
				  paramTitle   :"Set Datasink parameters",
				  list         :datasinkModel.allDatasinks,
				  selectionMode:"singleRow" };
			
			selectionPopupInit[EditEvent.ADD_TRANSPORT] =
				{ selectTitle  :"Select a Transport",
				  paramTitle   :"Set Transport Parameters",
				  list         :transportModel.allTransports,
				  selectionMode:"singleRow" };
			
			selectionPopupInit[EditEvent.ADD_WEBAPP] =
				{ selectTitle  :"Select a Webapp/REST API",
				  list         :webappModel.allWebapps,
			      selectionMode:"multipleRows" };
			
			handleNewSystemConfig(null);
			sysPropModel.clear();
		}
		
		public function getData():void {
			sysPropModel.domainNamePrefix = view.domainNameText.text;
			sysPropModel.selectedSize = view.systemSizesCombo.selectedItem as String;
		}
		
		protected function bindModel():void {
			
			view.selectedDataModels = dataModelModel.selectedDataModelNames;
			view.selectedTransports = transportModel.selectedTransports;
			view.selectedDatasinks  = datasinkModel.selectedDatasinks;
			view.selectedWebapps    = webappModel.selectedWebapps;
			
			view.systemSizes       = sysPropModel.allSystemSizes;
			view.softwareVersions  = sysPropModel.allSoftwareVersions;
			view.serviceRegions    = sysPropModel.allServiceRegions;
			view.serviceZones      = sysPropModel.allAvailZones;
			view.datasinksToConfig = datasinkModel.unconfiguredDatasinks;
			
			view.setApplianceDomain = sysPropModel.applianceDomain;
			view.setDomainPrefix = sysPropModel.domainNamePrefix;
			view.setDomainSuffix = sysPropModel.domainNameSuffix;
			view.setSystemSize = sysPropModel.selectedSize;
			view.setSwVersion  = sysPropModel.selectedSoftwareVersion;
			view.setAutoscale  = sysPropModel.autoScale;
			view.setVpc        = sysPropModel.vpcEnabled;
			manageVpcCheckbox();
			view.setAppliance  = sysPropModel.applianceEnabled;
			manageApplianceCheckbox();
			manageApplianceDomain();
			view.setProvider   = sysPropModel.selectedServiceProvider;
			view.setRegion     = sysPropModel.selectedServiceRegion;
			view.setZone       = sysPropModel.selectedAvailZone;
			view.defaultRtwsPubDnsAvailability = sysPropModel.defaultRtwsPubDnsAvailability;
			view.selectedRtwsPubDnsAvailability = sysPropModel.selectedRtwsPubDnsAvailability;

			view.majorVersionStepper.value = sysPropModel.configMajorVersion;
			view.minorVersionStepper.value = sysPropModel.configMinorVersion;
			
			view.maxDomainLength = sysPropModel.maxDomainNameLength;
		}
		
		protected function handleSetDomainName(event:EditEvent):void {
			sysPropModel.domainNamePrefix = event.parameters as String;
		}

		protected function handleSetApplianceDomainName(event:EditEvent):void {
			sysPropModel.applianceDomain = event.parameters as String;
		}
				
		protected function handleSetAutoScale(event:EditEvent):void {
			sysPropModel.autoScale = event.parameters as Boolean;
		}
		
		protected function handleSetVpc(event:EditEvent):void {
			sysPropModel.vpcEnabled = event.parameters as Boolean;
			if (sysPropModel.vpcEnabled) {
				var pgList:Array = processGroupModel.findProcessGroupsWithPersistentIP();
				if (pgList.length > 0) {
					var ips:String = "";
					for each (var pg:ProcessGroup in pgList) {
						ips = ips + pg.persistentIPAddress + ", ";
						pg.persistentIPAddress = ProcessGroup.NO_PUBLIC_IP;
					}
					ips = ips.replace(/, $/, "");
					
					var msg:StatusMessage =	new StatusMessage(StatusType.WARN,
						"Public IPs are not used by process groups when using a VPC. " +
						"The following public IPs have been unassigned from thier process groups (check Details tab): " + ips + ". " +
						"You may want to release them from the infrastructure provider as well.");
					
					dispatch(new PopupEvent(PopupEvent.MESSAGE_BEGIN, msg, null));
					dispatch(new EditEvent(EditEvent.REFRESH_PROCESS_GROUP_PARAMS, null, null));
				}
			}
		}
		
		protected function manageVpcCheckbox():void {
			if (sysPropModel.iaasServiceName == "AWS") {
				if (sysPropModel.vpcOnly == "Y") {
					view.allowVpc = false;
					view.vpcCheckBox.selected = true;
				} else {
					view.allowVpc = StringUtils.isEmpty(sysPropModel.domainNamePrefix);
				}
				if (view.allowVpc) {
					// The check box isn't disabled, make sure the check mark is the
					// default color
					view.vpcCheckBox.setStyle("symbolColor", "#F9F9F9");
				} else {
					if (view.vpcCheckBox.selected) {
						// Make the check mark for the vpcCheckBox black when the check box
						// is disabled and checked.
						// This is done programmatically because the settings for this
						// are not obvious in the CSS. It appears that this should be
						// the settings, but it must be overridden somewhere since the
						// check mark is set to a similar color and appears invisible 
						view.vpcCheckBox.setStyle("symbolColor", "#000000");
					}					
				}
			}
			else if(sysPropModel.iaasServiceName == "EUC") {
				view.allowVpc = false;
				view.vpcCheckBox.selected = false;
			} else {
				view.allowVpc = false;
			}
		}
		
		protected function manageApplianceCheckbox():void {
		
			view.allowAppliance = true;

			// The check box isn't disabled, make sure the check mark is the
			// default color
			view.applianceCheckBox.setStyle("symbolColor", "#F9F9F9");
		}
		
		protected function manageApplianceDomain():void {
			view.allowApplianceDomain = sysPropModel.applianceEnabled;
		}
		
		protected function handleSetAppliance(event:EditEvent):void {
			sysPropModel.applianceEnabled = event.parameters as Boolean;
			manageApplianceDomain();
		}
		
		protected function handleSetSystemSize(event:EditEvent):void {
			sysPropModel.selectedSize = event.parameters as String;
		}

		protected function handleSetSwVersion(event:EditEvent):void {
			sysPropModel.selectedSoftwareVersion = event.parameters as String;			
		}
		
		protected function handleSetServiceRegion(event:EditEvent):void {
			sysPropModel.selectedServiceRegion = event.parameters as String;
		}
		
		protected function handleSetServiceZone(event:EditEvent):void {
			sysPropModel.selectedAvailZone = event.parameters as String;									
		}
		
		private function onRtwsPubDnsAvailChange(event:EditEvent):void {
			sysPropModel.selectedRtwsPubDnsAvailability = (event.parameters as DnsAvailability);									
		}
		
		protected function handleSetConfigMajorVersion(event:EditEvent):void {
			sysPropModel.configMajorVersion = event.parameters as int;
		}
		
		protected function handleSetConfigMinorVersion(event:EditEvent):void {
			sysPropModel.configMinorVersion = event.parameters as int;
		}
		
		protected function handleSystemConfigLoaded( event:SystemConfigServiceEvent ):void {
			handleSysPropSelectionsChanged(null);
		}
		
		protected function handleSystemFullyLoaded( event:SystemConfigServiceEvent ):void {
			// Since the system has fully loaded, set the state of the autoscale checkbox
			manageAutoscaleCheckbox();
		}
		
		protected function handleSystemSaved( event:SystemConfigServiceEvent ):void {
			handleSysPropSelectionsChanged(null);			
		}
		
		protected function handleNewSystemConfig( event:SystemConfigServiceEvent ):void {
			view.focusManager.setFocus(view.domainNameText);
		}
		
		private function manageAutoscaleCheckbox():void {
			if(!loadStateManager.inLoadState) {
				var numDatasinks:int = datasinkModel.selectedDatasinks.length;
				var numDatamodels:int  = dataModelModel.selectedDataModelNames.length;
				view.allowAutoScaling = systemBuilderProperties.autoScalingEnabled && (numDatasinks > 0) && (numDatamodels > 0);
				if (!view.allowAutoScaling) {
					view.autoscaleCheckBox.selected = view.setAutoscale = sysPropModel.autoScale = false;
				}
			}
		}
		
		protected function handleSelectedDataModelsChanged(event:EditEvent):void {
			manageAutoscaleCheckbox();
			
			loadStateManager.verifyConfigurationLoaded(event);
		}
		
		protected function handleSelectedDataSinksChanged(event:EditEvent):void {
			manageAutoscaleCheckbox();
			
			loadStateManager.verifyConfigurationLoaded(event);
		}
		
		protected function handleSelectedTransportsChanged(event:EditEvent):void {
			loadStateManager.verifyConfigurationLoaded(event);
		}
			
		protected function handleSysPropSelectionsChanged(event:EditEvent):void {
			view.domainNameText.text = sysPropModel.domainNamePrefix; // Not sure why I have to do this
			view.setDomainPrefix = sysPropModel.domainNamePrefix;
			view.setApplianceDomain = sysPropModel.applianceDomain;
			view.setSystemSize = sysPropModel.selectedSize;
			view.systemSizesCombo.selectedItem = sysPropModel.selectedSize;
			view.setSwVersion  = sysPropModel.selectedSoftwareVersion;
			view.swVersionCombo.selectedItem = sysPropModel.selectedSoftwareVersion;
			
			view.setAutoscale  = sysPropModel.autoScale;
			view.setProvider   = sysPropModel.selectedServiceProvider;
			
			view.setRegion     = sysPropModel.selectedServiceRegion;
			view.regionCombo.selectedItem = sysPropModel.selectedServiceRegion;
			
			view.serviceZones  = sysPropModel.allAvailZones;			
			view.zoneCombo.selectedItem = sysPropModel.selectedAvailZone;
			view.setZone       = sysPropModel.selectedAvailZone;
			
			view.defaultRtwsPubDnsAvailability = sysPropModel.defaultRtwsPubDnsAvailability;
			view.selectedRtwsPubDnsAvailability = sysPropModel.selectedRtwsPubDnsAvailability;

			view.setVpc   = sysPropModel.vpcEnabled;
			manageVpcCheckbox();
			
			view.setAppliance = sysPropModel.applianceEnabled;
			manageApplianceCheckbox();
			manageApplianceDomain();
			
			view.majorVersionStepper.value = sysPropModel.configMajorVersion;
			view.minorVersionStepper.value = sysPropModel.configMinorVersion;
			
			var system:System = systemListModel.findSystem(sysPropModel.fullDomainName);
			view.allowServiceChange = (system == null) || (system.state == SystemState.NEW_SYSTEM);
			manageAutoscaleCheckbox();
		}
		
		protected var selectItemDescriptionPopup:SelectItemDescriptionPopup;
		protected var parameterPopup:ParameterPopup;
		
		protected function handleSelectItemDescriptionBegin( event:PopupEvent ):void {
			var editEventType:String = event.parameters as String;
			var popupInit:Object = selectionPopupInit[editEventType];
			
			selectItemDescriptionPopup = new SelectItemDescriptionPopup();
			selectItemDescriptionPopup.editEventType = editEventType;
			selectItemDescriptionPopup.popupTitle    = popupInit.selectTitle;
			selectItemDescriptionPopup.itemData      = popupInit.list;
			selectItemDescriptionPopup.selectionMode = popupInit.selectionMode;
			
			selectItemDescriptionPopup.addEventListener(
				PopupEvent.SELECT_ITEM_DESCRIPTION_END, handleSelectItemDescriptionEnd);
			selectItemDescriptionPopup.addEventListener(
				CloseEvent.CLOSE, handleSelectItemDescriptionClose );
			
			PopUpManager.addPopUp(selectItemDescriptionPopup, contextView, true);
			PopUpManager.centerPopUp(selectItemDescriptionPopup);
		}
		
		/**
		 * Make a deep copy of the GeneralParameter objects inside of an ArrayCollection 
		 */
		protected function deepCopy(source:ArrayCollection):ArrayCollection 
		{ 		
			// TODO make more generic?
			var temp:ArrayCollection = new ArrayCollection();	
			for each (var param:GeneralParameter in source) {
				var testing:GeneralParameter = param.clone();
				temp.addItem(testing);
			}

			return temp;
		}
		
		protected function createParameterPopup(
			nestedEvent:Event, 
			editEventType:String, 
			title:String, 
			params:ArrayCollection,
			dataModels:ArrayCollection=null):void {
			parameterPopup = new ParameterPopup();
			
			parameterPopup.editEventType 		= editEventType;
			parameterPopup.popupTitle    		= title;
			parameterPopup.paramData     		= params;
			parameterPopup.tempParamData     	= deepCopy(params);
			parameterPopup.datasinkDataModels 	= dataModels;
			parameterPopup.nestedEvent   		= nestedEvent;
			
			var comboBoxEnabledFields:Array = new Array();
			comboBoxEnabledFields[TransportModel.INPUT_FORMAT_FIELD_NAME] = dataModelModel.dataSourceNames;
			comboBoxEnabledFields[TransportModel.RECORD_FORMAT_FIELD_NAME] = splitterModel.splitters;
			parameterPopup.comboBoxEnabledFields = comboBoxEnabledFields;
			
			var currSelection:ArrayList = new ArrayList();
			for each (var model:DataModelName in dataModelModel.selectedDataModelNames) {
				currSelection.addItem(model.zipFilename);
			}
			parameterPopup.selectedDataModels = currSelection;
			
			parameterPopup.addEventListener(PopupEvent.PARAMETER_GRID_END, handlePopupParameterEnd);
			parameterPopup.addEventListener(CloseEvent.CLOSE, handlePopupParameterClose );
			
			PopUpManager.addPopUp(parameterPopup, contextView, true);
			PopUpManager.centerPopUp(parameterPopup);			
		}
		
		protected function handleSelectItemDescriptionEnd(event:PopupEvent):void {
			selectItemDescriptionPopup.removeEventListener(
				PopupEvent.SELECT_ITEM_DESCRIPTION_END, handleSelectItemDescriptionEnd);
			selectItemDescriptionPopup.removeEventListener(
				CloseEvent.CLOSE, handleSelectItemDescriptionClose );
			if ((event != null) && (event.parameters != null)) {
				if (selectItemDescriptionPopup.editEventType == EditEvent.ADD_WEBAPP) {
					dispatch(new EditEvent(EditEvent.ADD_WEBAPP, event.parameters, null));					
				}
				else {
					// For transports and datasinks, popup the parameters
					var items:Vector.<Object> = event.parameters as Vector.<Object>;
					var item:NamedItem = items[0] as NamedItem;
					var editEventType:String = selectItemDescriptionPopup.editEventType
					var popupInit:Object = selectionPopupInit[editEventType];
					
					var params:ArrayCollection = ObjectUtils.clone(item.params) as ArrayCollection;
					var dataModels:ArrayCollection = 
						(item is Datasink) ? ObjectUtils.clone(Datasink(item).dataModels) as ArrayCollection : null;
					
					createParameterPopup(
						event,
						editEventType,
						popupInit.paramTitle,
						params,
						dataModels);
				}
			}
		}
		
		protected function handleSelectItemDescriptionClose(event:CloseEvent):void {
			PopUpManager.removePopUp(selectItemDescriptionPopup);
			handleSelectItemDescriptionEnd(null);
		}
		
		protected function handlePopupParameterClose(event:CloseEvent):void {
			PopUpManager.removePopUp(parameterPopup);
			handlePopupParameterEnd(null);			
		}
		
		protected function handlePopupParameterEnd(event:PopupEvent):void {
			parameterPopup.removeEventListener(PopupEvent.PARAMETER_GRID_END, handlePopupParameterEnd);
			parameterPopup.removeEventListener(CloseEvent.CLOSE, handlePopupParameterClose );
			if ((event != null) && (event.parameters != null) && (parameterPopup.editEventType != null)) {
				var nestedEvent:PopupEvent = event.nestedEvent as PopupEvent;
				
				// Clone the item to add and update its parameters 
				var items:Vector.<Object> = nestedEvent.parameters as Vector.<Object>;
				var origItem:NamedItem = items[0] as NamedItem;
				var clonedItem:NamedItem = origItem.clone(event.parameters.paramData as ArrayCollection);
				if (clonedItem is Datasink && event.parameters.datasinkModel != null) {
					Datasink(clonedItem).dataModels = event.parameters.datasinkModel as ArrayCollection;
				}
				
				// Create a vector of the object because this is what the commands expect
				var clonedItems:Vector.<Object> = new Vector.<Object>;
				clonedItems.push(clonedItem);
				
				dispatch(new EditEvent(parameterPopup.editEventType, clonedItems, null));
			}
			
			// occurs on new and changed datasinks
			if (event != null && event.parameters != null && event.parameters.datasinkModel != null) {
				dispatch(new EditEvent(EditEvent.DATASINK_DATA_MODELS_CHANGED, null, null));
			} 
			// only for changing current datasinks / transports
			if (event != null && event.parameters != null && event.parameters.datasinkModel == null && (parameterPopup.editEventType == null)) {
				// The OK button has been selected for a transport
				dispatch(new EditEvent(EditEvent.TRANSPORT_UPDATED, {"paramData" : event.parameters.paramData}, null));
			}
			if (event != null && event.parameters != null && event.parameters.datasinkModel != null && (parameterPopup.editEventType == null)) {
				// The OK button has been selected for a datasink
				dispatch(new EditEvent(EditEvent.DATASINK_UPDATED, {"paramData" : event.parameters.paramData}, null));
			} 
		}
		
		/**
		 * The params for the transport have changed and need to be saved
		 */
		protected function handleUpdateTransportParams(event:EditEvent):void {
			view.transportList.selectedItem.params = event.parameters.paramData;
		}
		
		/**
		 * The params for the datasink have changed and need to be saved
		 */
		protected function handleUpdateDatasinkParams(event:EditEvent):void {
			view.datasinkList.selectedItem.params = event.parameters.paramData;
		}
		
		protected function handleEditDatasinkParams(event:EditEvent):void {
			var datasink:Datasink = event.parameters as Datasink;
			createParameterPopup(null, null, "Edit datasink parameters", datasink.params, datasink.dataModels);
		}
		
		protected function handleEditTransportParams(event:EditEvent):void {
			var transport:Transport = event.parameters as Transport;
			createParameterPopup(null, null, "Edit transport parameters", transport.params);
		}
		
		/************************ Datasink Configuration Popup *************************/
		
		protected var datasinkParametersPopup:DatasinkParametersPopup;
		
		protected function handleDatasinkConfigBegin(event:PopupEvent):void {
			var fqn:String = event.parameters as String;

			datasinkParametersPopup = new DatasinkParametersPopup();
			datasinkParametersPopup.fqn = fqn;
			datasinkParametersPopup.processGroupList =
				processGroupConfig.getTenantOwnedProcessGroupsNoDatasinks(userModel.tenantId);
			
			var paramSet:Object = new Object();
			paramSet.datasink = null;
			datasinkParametersPopup.paramSet = paramSet;
			
			datasinkParametersPopup.addEventListener(
				PopupEvent.DATASINK_CONFIG_END, handleDatasinkConfigEnd);
			datasinkParametersPopup.addEventListener(
				CloseEvent.CLOSE, handleDatasinkConfigClose );
			datasinkParametersPopup.addEventListener(
				PopupEvent.MESSAGE_BEGIN, dispatch);
			
			PopUpManager.addPopUp(datasinkParametersPopup, contextView, true);
			PopUpManager.centerPopUp(datasinkParametersPopup);
		}
		
		protected function handleDatasinkConfigClose(event:CloseEvent):void {
			PopUpManager.removePopUp(datasinkParametersPopup);
			handleDatasinkConfigEnd(null);
		}
		
		protected function handleDatasinkConfigEnd(event:PopupEvent):void {
			datasinkParametersPopup.removeEventListener(PopupEvent.DATASINK_CONFIG_END, handleDatasinkConfigEnd);
			datasinkParametersPopup.removeEventListener(CloseEvent.CLOSE, handleDatasinkConfigClose );
			datasinkParametersPopup.removeEventListener(PopupEvent.MESSAGE_BEGIN, dispatch);
			if ((event != null) && (event.parameters != null)) {
				dispatch(new PopupEvent(PopupEvent.PROCESS_GROUP_CONFIG_BEGIN, event.parameters, null));				
			}
		}
		
	}
}
