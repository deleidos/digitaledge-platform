/**
 *                                  Apache License
 *                            Version 2.0, January 2004
 *                         http://www.apache.org/licenses/
 *
 *    TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION
 *
 *    1. Definitions.
 *
 *       "License" shall mean the terms and conditions for use, reproduction,
 *       and distribution as defined by Sections 1 through 9 of this document.
 *
 *       "Licensor" shall mean the copyright owner or entity authorized by
 *       the copyright owner that is granting the License.
 *
 *       "Legal Entity" shall mean the union of the acting entity and all
 *       other entities that control, are controlled by, or are under common
 *       control with that entity. For the purposes of this definition,
 *       "control" means (i) the power, direct or indirect, to cause the
 *       direction or management of such entity, whether by contract or
 *       otherwise, or (ii) ownership of fifty percent (50%) or more of the
 *       outstanding shares, or (iii) beneficial ownership of such entity.
 *
 *       "You" (or "Your") shall mean an individual or Legal Entity
 *       exercising permissions granted by this License.
 *
 *       "Source" form shall mean the preferred form for making modifications,
 *       including but not limited to software source code, documentation
 *       source, and configuration files.
 *
 *       "Object" form shall mean any form resulting from mechanical
 *       transformation or translation of a Source form, including but
 *       not limited to compiled object code, generated documentation,
 *       and conversions to other media types.
 *
 *       "Work" shall mean the work of authorship, whether in Source or
 *       Object form, made available under the License, as indicated by a
 *       copyright notice that is included in or attached to the work
 *       (an example is provided in the Appendix below).
 *
 *       "Derivative Works" shall mean any work, whether in Source or Object
 *       form, that is based on (or derived from) the Work and for which the
 *       editorial revisions, annotations, elaborations, or other modifications
 *       represent, as a whole, an original work of authorship. For the purposes
 *       of this License, Derivative Works shall not include works that remain
 *       separable from, or merely link (or bind by name) to the interfaces of,
 *       the Work and Derivative Works thereof.
 *
 *       "Contribution" shall mean any work of authorship, including
 *       the original version of the Work and any modifications or additions
 *       to that Work or Derivative Works thereof, that is intentionally
 *       submitted to Licensor for inclusion in the Work by the copyright owner
 *       or by an individual or Legal Entity authorized to submit on behalf of
 *       the copyright owner. For the purposes of this definition, "submitted"
 *       means any form of electronic, verbal, or written communication sent
 *       to the Licensor or its representatives, including but not limited to
 *       communication on electronic mailing lists, source code control systems,
 *       and issue tracking systems that are managed by, or on behalf of, the
 *       Licensor for the purpose of discussing and improving the Work, but
 *       excluding communication that is conspicuously marked or otherwise
 *       designated in writing by the copyright owner as "Not a Contribution."
 *
 *       "Contributor" shall mean Licensor and any individual or Legal Entity
 *       on behalf of whom a Contribution has been received by Licensor and
 *       subsequently incorporated within the Work.
 *
 *    2. Grant of Copyright License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       copyright license to reproduce, prepare Derivative Works of,
 *       publicly display, publicly perform, sublicense, and distribute the
 *       Work and such Derivative Works in Source or Object form.
 *
 *    3. Grant of Patent License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       (except as stated in this section) patent license to make, have made,
 *       use, offer to sell, sell, import, and otherwise transfer the Work,
 *       where such license applies only to those patent claims licensable
 *       by such Contributor that are necessarily infringed by their
 *       Contribution(s) alone or by combination of their Contribution(s)
 *       with the Work to which such Contribution(s) was submitted. If You
 *       institute patent litigation against any entity (including a
 *       cross-claim or counterclaim in a lawsuit) alleging that the Work
 *       or a Contribution incorporated within the Work constitutes direct
 *       or contributory patent infringement, then any patent licenses
 *       granted to You under this License for that Work shall terminate
 *       as of the date such litigation is filed.
 *
 *    4. Redistribution. You may reproduce and distribute copies of the
 *       Work or Derivative Works thereof in any medium, with or without
 *       modifications, and in Source or Object form, provided that You
 *       meet the following conditions:
 *
 *       (a) You must give any other recipients of the Work or
 *           Derivative Works a copy of this License; and
 *
 *       (b) You must cause any modified files to carry prominent notices
 *           stating that You changed the files; and
 *
 *       (c) You must retain, in the Source form of any Derivative Works
 *           that You distribute, all copyright, patent, trademark, and
 *           attribution notices from the Source form of the Work,
 *           excluding those notices that do not pertain to any part of
 *           the Derivative Works; and
 *
 *       (d) If the Work includes a "NOTICE" text file as part of its
 *           distribution, then any Derivative Works that You distribute must
 *           include a readable copy of the attribution notices contained
 *           within such NOTICE file, excluding those notices that do not
 *           pertain to any part of the Derivative Works, in at least one
 *           of the following places: within a NOTICE text file distributed
 *           as part of the Derivative Works; within the Source form or
 *           documentation, if provided along with the Derivative Works; or,
 *           within a display generated by the Derivative Works, if and
 *           wherever such third-party notices normally appear. The contents
 *           of the NOTICE file are for informational purposes only and
 *           do not modify the License. You may add Your own attribution
 *           notices within Derivative Works that You distribute, alongside
 *           or as an addendum to the NOTICE text from the Work, provided
 *           that such additional attribution notices cannot be construed
 *           as modifying the License.
 *
 *       You may add Your own copyright statement to Your modifications and
 *       may provide additional or different license terms and conditions
 *       for use, reproduction, or distribution of Your modifications, or
 *       for any such Derivative Works as a whole, provided Your use,
 *       reproduction, and distribution of the Work otherwise complies with
 *       the conditions stated in this License.
 *
 *    5. Submission of Contributions. Unless You explicitly state otherwise,
 *       any Contribution intentionally submitted for inclusion in the Work
 *       by You to the Licensor shall be under the terms and conditions of
 *       this License, without any additional terms or conditions.
 *       Notwithstanding the above, nothing herein shall supersede or modify
 *       the terms of any separate license agreement you may have executed
 *       with Licensor regarding such Contributions.
 *
 *    6. Trademarks. This License does not grant permission to use the trade
 *       names, trademarks, service marks, or product names of the Licensor,
 *       except as required for reasonable and customary use in describing the
 *       origin of the Work and reproducing the content of the NOTICE file.
 *
 *    7. Disclaimer of Warranty. Unless required by applicable law or
 *       agreed to in writing, Licensor provides the Work (and each
 *       Contributor provides its Contributions) on an "AS IS" BASIS,
 *       WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 *       implied, including, without limitation, any warranties or conditions
 *       of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
 *       PARTICULAR PURPOSE. You are solely responsible for determining the
 *       appropriateness of using or redistributing the Work and assume any
 *       risks associated with Your exercise of permissions under this License.
 *
 *    8. Limitation of Liability. In no event and under no legal theory,
 *       whether in tort (including negligence), contract, or otherwise,
 *       unless required by applicable law (such as deliberate and grossly
 *       negligent acts) or agreed to in writing, shall any Contributor be
 *       liable to You for damages, including any direct, indirect, special,
 *       incidental, or consequential damages of any character arising as a
 *       result of this License or out of the use or inability to use the
 *       Work (including but not limited to damages for loss of goodwill,
 *       work stoppage, computer failure or malfunction, or any and all
 *       other commercial damages or losses), even if such Contributor
 *       has been advised of the possibility of such damages.
 *
 *    9. Accepting Warranty or Additional Liability. While redistributing
 *       the Work or Derivative Works thereof, You may choose to offer,
 *       and charge a fee for, acceptance of support, warranty, indemnity,
 *       or other liability obligations and/or rights consistent with this
 *       License. However, in accepting such obligations, You may act only
 *       on Your own behalf and on Your sole responsibility, not on behalf
 *       of any other Contributor, and only if You agree to indemnify,
 *       defend, and hold each Contributor harmless for any liability
 *       incurred by, or claims asserted against, such Contributor by reason
 *       of your accepting any such warranty or additional liability.
 *
 *    END OF TERMS AND CONDITIONS
 *
 *    APPENDIX: How to apply the Apache License to your work.
 *
 *       To apply the Apache License to your work, attach the following
 *       boilerplate notice, with the fields enclosed by brackets "{}"
 *       replaced with your own identifying information. (Don't include
 *       the brackets!)  The text should be enclosed in the appropriate
 *       comment syntax for the file format. We also recommend that a
 *       file or class name and description of purpose be included on the
 *       same "printed page" as the copyright notice for easier
 *       identification within third-party archives.
 *
 *    Copyright {yyyy} {name of copyright owner}
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
package com.deleidos.rtws.systemcfg.models
{
	import com.adobe.serialization.json.JSON;
	import com.deleidos.rtws.commons.model.status.StatusMessage;
	import com.deleidos.rtws.commons.model.status.StatusType;
	import com.deleidos.rtws.commons.tenantutils.model.system.SystemState;
	import com.deleidos.rtws.commons.util.DynaObjectUtils;
	import com.deleidos.rtws.commons.util.StringUtils;
	import com.deleidos.rtws.systemcfg.dao.AvailabilityZones;
	import com.deleidos.rtws.systemcfg.dao.CurrentDefaults;
	import com.deleidos.rtws.systemcfg.dao.IaasAccounts;
	import com.deleidos.rtws.systemcfg.dao.SoftwareReleases;
	import com.deleidos.rtws.systemcfg.dao.SystemSizing;
	import com.deleidos.rtws.systemcfg.events.EditEvent;
	import com.deleidos.rtws.systemcfg.events.PopupEvent;
	import com.deleidos.rtws.systemcfg.utils.ValidationUtils;
	
	import flash.events.Event;
	
	import flashx.textLayout.formats.Float;
	
	import mx.collections.ArrayCollection;
	import mx.collections.ArrayList;
	import mx.collections.Sort;
	import mx.collections.SortField;
	import mx.controls.Alert;
	
	import org.robotlegs.mvcs.Actor;
	
	public class SystemPropertiesModel extends Actor
	{
		[Inject] public var availZonesTable:AvailabilityZones;
		[Inject] public var systemSizingTable:SystemSizing;
		[Inject] public var swReleasesTable:SoftwareReleases;
		[Inject] public var processGroupModel:ProcessGroupModel;
		[Inject] public var currentDefaultsTable:CurrentDefaults;
		[Inject] public var iaasAccountsTable:IaasAccounts;
		[Inject] public var systemListModel:SystemListModel;
		[Inject] public var systemPropertiesModel:SystemBuilderPropertiesModel;
		[Inject] public var vpcNetwork:VpcNetworkModel;
		
		public static const CONFIG_PREFIX:String = "config_v";
		
		private var _accountId:String;
		private var _currentDefaults:Object = null;
		
		private var _domainNamePrefix:String;
		private var _domainNameSuffix:String;
		private var _applianceDomain:String;
		private var _domainIsDuplicate:Boolean;
		private var _autoScale:Boolean;
		private var _vpcEnabled:Boolean;
		private var _vpcOnly:String;
		private var _applianceEnabled:Boolean;
		private var _iaasServiceName:String;

		private var _configMajorVersion:int;
		private var _configMinorVersion:int;
				
		private var _allSystemSizes:ArrayCollection;
		private var _selectedSize:String;
		private var _originalSize:String;
		
		private var _allSoftwareVersions:ArrayCollection;
		private var _selectedSoftwareVersion:String;
		
		private var _allServiceProviders:ArrayCollection
		private var _selectedServiceProvider:String;

		private var _allServiceRegions:ArrayCollection;
		private var _selectedServiceRegion:String;

		private var _allAvailZones:ArrayCollection;
		private var _selectedAvailZone:String;
		
		private var _defaultRtwsPubDnsAvailability:DnsAvailability;
		private var _selectedRtwsPubDnsAvailability:DnsAvailability;
		
		private var _keypairName:String;
		
		private var _lastSystemState:SystemState;
		
		private var _longestProcessGroupNameLen:int;

		/**
		 * Return an index that represents the relative size of a system.
		 * It is expected that the size contain "small", "medium", or "large",
		 * and that "small" and "large" could be prefixed with a number of "x"
		 * characters indicating increasing degrees of smaller or larger.
		 */
		private function getSizeIndex(size:String):Number {
			var result:Number = 0.0;

			// Count the number of leading x'es
			var xcount:Number = 1.0;
			for(var i:int=0; i<size.length; i++) {
				if (size.charAt(i) == 'x')
					xcount += 1.0;
				else
					break;
			}
			
			if (size.indexOf("small") >= 0)
				result = 1.0 / xcount;				
			else if (size.indexOf("medium") >= 0)
				result = 2.0;
			else if (size.indexOf("large") >= 0)
				result = 3.0 * xcount;
			
			return result;
		}
			
		private function compareSystemSizes(a:Object, b:Object, fields:Array = null):int
		{
			var sizeA:String = a as String;
			var sizeB:String = b as String;
			var result:int = 0;
			
			var indexA:Number = getSizeIndex(sizeA);
			var indexB:Number = getSizeIndex(sizeB);
			
			// trace("a=" + sizeA + "("+indexA+")" + ", b=" + sizeB + "("+indexB+")" );
			
			if (indexA < indexB)
				result = -1;
			else if (indexA > indexB)
				result = 1;
			
			return result;
		}		
		
		public function SystemPropertiesModel()
		{
			_allSystemSizes = new ArrayCollection();
			_selectedSize = "";
			_originalSize = null;
			
			_allSoftwareVersions = new ArrayCollection();
			_selectedSoftwareVersion = "";
			
			_allServiceProviders = new ArrayCollection();
			_selectedServiceProvider = "";

			_allServiceRegions = new ArrayCollection();
			_selectedServiceRegion = "";

			_allAvailZones = new ArrayCollection();
			_selectedAvailZone = "";
			
			_defaultRtwsPubDnsAvailability = DnsAvailability.UNKNOWN;
			_selectedRtwsPubDnsAvailability = null;

			_applianceDomain = "";
			_domainNamePrefix = "";
			_domainNameSuffix = ".rtsaic.com";
			_domainIsDuplicate = false;
			_autoScale = false;
			_vpcEnabled = false;
			_vpcOnly = "N";
			_applianceEnabled = false;
			_iaasServiceName = "AWS";
			_selectedSize = "";
			_selectedSoftwareVersion = "";
			_selectedServiceRegion = "";
			_keypairName = "";
			_configMajorVersion = 1;
			_configMinorVersion = 0;
			_lastSystemState = SystemState.UNKNOWN;
		}
		
		public function clear():void {
			this.domainNamePrefix = "";
			this.applianceDomain = "";
			this.autoScale = false;
			
			_configMajorVersion = 1;
			_configMinorVersion = 0;
			
			this.selectedSize = "small";
			this.originalSize = null;
			this.lastSystemState = SystemState.UNKNOWN;
			this.selectedSoftwareVersion = _currentDefaults[CurrentDefaults.SW_VERSION_ID];		
			this.selectedServiceRegion   = _currentDefaults[CurrentDefaults.IAAS_REGION];
			this.selectedAvailZone       = _currentDefaults[CurrentDefaults.IAAS_AZONE];
			
			_selectedRtwsPubDnsAvailability = null;
			_vpcEnabled = false;
			_applianceEnabled = false;
			
			processGroupModel.adjustSystemSize(_selectedSize, _autoScale);
			
			dispatch(new EditEvent(EditEvent.SYS_PROP_SELECTIONS_CHANGED, null, null));
		}
		
		private function getDefaultArrayItem(arr:ArrayCollection):String {
			if (arr.length == 0)
				return "";
			else
				return arr.getItemAt(0) as String;
		}
		
		public function setup(accountId:String, selectedService:String, keypairName:String):void {
			
			var account:Object = iaasAccountsTable.lookupByAccountId(accountId);
			_domainNameSuffix = "." + account[IaasAccounts.DOMAIN_NAME_SUFFIX];
			
			var sort:Sort = new Sort();			
			
			_allSystemSizes = new ArrayCollection(
				systemSizingTable.simpleSelect(SystemSizing.SYSTEM_SIZE).source);
			sort = new Sort();
			sort.compareFunction = compareSystemSizes;
			_allSystemSizes.sort = sort;
			_allSystemSizes.refresh();
			
			_allSoftwareVersions = new ArrayCollection(
				swReleasesTable.simpleSelect(SoftwareReleases.SW_VERSION_ID).source);
			sort = new Sort();
			sort.fields = [new SortField(null,true,true)];
			_allSoftwareVersions.sort = sort;
			_allSoftwareVersions.refresh();
			
			_allServiceProviders = new ArrayCollection(
				availZonesTable.simpleSelectDistinct(AvailabilityZones.IAAS_SERVICE_NAME).source);
			
			selectedServiceProvider = selectedService;
			
			_currentDefaults = currentDefaultsTable.lookupDefaultsByService(selectedService);
			
			if (_currentDefaults == null) {
				_currentDefaults = new Object();
				_currentDefaults[CurrentDefaults.IAAS_SERVICE_NAME] = selectedServiceProvider
				_currentDefaults[CurrentDefaults.IAAS_REGION] = getDefaultArrayItem(_allServiceRegions);
				_currentDefaults[CurrentDefaults.IAAS_AZONE]  =  getDefaultArrayItem(_allAvailZones);
				_currentDefaults[CurrentDefaults.SW_VERSION_ID] = getDefaultArrayItem(_allSoftwareVersions);
			} else {
				// The current defaults are set, lets check to make sure the tenant has access to 
				// the region and availability zone and if not get default from the in memory array. 
				
				var azones:ArrayList = availZonesTable.lookupAzonesByRegion(_currentDefaults[CurrentDefaults.IAAS_REGION]);
				
				var azoneExist:Boolean = false;
				for (var i:int = 0; i < azones.length; i++) {
					if (_currentDefaults[CurrentDefaults.IAAS_AZONE] == azones.getItemAt(i)) {
						azoneExist = true;
					}
				}
				
				if (! azoneExist) {
					_currentDefaults[CurrentDefaults.IAAS_REGION] = getDefaultArrayItem(_allServiceRegions);
					_currentDefaults[CurrentDefaults.IAAS_AZONE]  =  getDefaultArrayItem(_allAvailZones);
				}
			}
			
			_currentDefaults[CurrentDefaults.SW_VERSION_ID] = "";

			_selectedServiceRegion   = _currentDefaults[CurrentDefaults.IAAS_REGION];
			_selectedAvailZone       = _currentDefaults[CurrentDefaults.IAAS_AZONE];
			_selectedSoftwareVersion = _currentDefaults[CurrentDefaults.SW_VERSION_ID]; 
			
			_selectedSize = "small"; // _allSystemSizes.getItemAt(0) as String;
			
			_keypairName = keypairName;
			_accountId = accountId;
			_vpcOnly = account[IaasAccounts.VPC_ONLY];
			_iaasServiceName = account[IaasAccounts.IAAS_SERVICE_NAME];
			
			processGroupModel.adjustSystemSize(_selectedSize, _autoScale);
			
			var processGroups:ArrayCollection = processGroupModel.processGroups;
			_longestProcessGroupNameLen = 0;
			for each (var pg:ProcessGroup in processGroups.source) {
				_longestProcessGroupNameLen = Math.max(pg.processGroupName.length, _longestProcessGroupNameLen);
			}
		}
		
		public function get maxDomainNameLength():int {
			return systemPropertiesModel.maxDomainLength - _longestProcessGroupNameLen - _domainNameSuffix.length;
		}
		
		public function get accountId():String {
			return _accountId;
		}
		
		public function get applianceDomain():String {
			return _applianceDomain;
		}		
		
		public function set applianceDomain(value:String):void {
			_applianceDomain = value;
		}
		
		public function get domainNamePrefix():String {
			return _domainNamePrefix;
		}		
		
		public function set domainNamePrefix(value:String):void {
			_domainNamePrefix = value;
		}
		
		public function get domainNameSuffix():String {
			return _domainNameSuffix;
		}
		
		public function set domainNameSuffix(value:String):void {
			_domainNameSuffix = value;
		}
		
		public function get fullDomainName():String {
			return _domainNamePrefix + _domainNameSuffix;
		}
		
		public function get domainIsDuplicate():Boolean {
			return _domainIsDuplicate;
		}
		
		public function setDomainDuplicate(response:String):void {
			var fullDomainName:String = this.fullDomainName;
			if ((response == "true") || (response == "false")) {
				// A domain is a duplicate if its not part of the current user's list of domains AND
				// and a query of ALL systems returns a count greater than zero. This way a user can
				// overwrite their own systems, but not stomp on anybody elses.
				
				_domainIsDuplicate = (response == "true") && (systemListModel.findSystem(fullDomainName) == null);
			}
			else {
				Alert.show("Error in response string: " + response, "Error");
				_domainIsDuplicate = true;
			}
		}
		
		public function get autoScale():Boolean {
			return _autoScale;
		}		
		public function set autoScale(value:Boolean):void {
			_autoScale = value;
			processGroupModel.setAutoscaleAll(value, _selectedSize, _autoScale);
		}
		public function get vpcEnabled():Boolean {
			return _vpcEnabled;	
		}
		public function set vpcEnabled(value:Boolean):void {
			_vpcEnabled = value;
		}
		public function get vpcOnly():String {
			return _vpcOnly;	
		}
		public function set vpcOnly(value:String):void {
			_vpcOnly = value;
		}
		public function get applianceEnabled():Boolean {
			return _applianceEnabled;	
		}
		public function set applianceEnabled(value:Boolean):void {
			_applianceEnabled = value;
		}
		public function get iaasServiceName():String {
			return _iaasServiceName;	
		}
		public function set iaasServiceName(value:String):void {
			_iaasServiceName = value;
		}
		public function get configMinorVersion():int
		{
			return _configMinorVersion;
		}		
		public function set configMinorVersion(value:int):void
		{
			_configMinorVersion = value;
		}		
		public function get configMajorVersion():int
		{
			return _configMajorVersion;
		}		
		public function set configMajorVersion(value:int):void
		{
			_configMajorVersion = value;
		}		
		public function get configVersion():String {
			return CONFIG_PREFIX + _configMajorVersion + "." + _configMinorVersion;
		}
		public function set configVersion(value:String):void {
			var versionString:String = StringUtils.remove(value,CONFIG_PREFIX);
			versionString = versionString.replace("v","");
			var numbers:Array = versionString.split(/\./);
			if (numbers.length == 2) {
				_configMajorVersion = parseInt(numbers[0]);
				_configMinorVersion = parseInt(numbers[1]);
			}
			else {
				_configMajorVersion = 0;
				_configMinorVersion = 0;				
			}
		}
		
		public function get selectedSize():String {
			return _selectedSize;
		}		
		public function set selectedSize(value:String):void {
			if (_allSystemSizes.getItemIndex(value) < 0) {
				throw Error("Runtime error: system size '" + value + "' is not valid.");
			}
			
			_selectedSize = value;

			processGroupModel.adjustSystemSize(_selectedSize, _autoScale);
		}
		
		public function get originalSize():String {
			return _originalSize;
		}		
		public function set originalSize(value:String):void	{
			_originalSize = value;
		}
		
		public function get selectedSoftwareVersion():String {
			return _selectedSoftwareVersion;
		}		
		public function set selectedSoftwareVersion(value:String):void {
			if (_allSoftwareVersions.getItemIndex(value) < 0) {
				// Software version does not exist - set it to a null string
				_selectedSoftwareVersion = "";
			}
			else {
				_selectedSoftwareVersion = value;
			}
		}
		public function softwareVersionExists(value:String):Boolean {
			return (_allSoftwareVersions.getItemIndex(value) >= 0);
		}
		
		public function get selectedServiceProvider():String {
			return _selectedServiceProvider;
		}		
		public function set selectedServiceProvider(value:String):void {
			if (_allServiceProviders.getItemIndex(value) < 0) {
				throw Error("Runtime error: service provider '" + value + "' is not valid.");
			}
			
			_selectedServiceProvider = value;
			
			_allServiceRegions.removeAll();
			_allServiceRegions.addAll(
				availZonesTable.simpleSelectDistinct(
					AvailabilityZones.IAAS_REGION,
					AvailabilityZones.IAAS_SERVICE_NAME,
					value));
				
			_selectedServiceRegion = "";
			if (_allServiceRegions.length > 0) {
				var item:Object = _allServiceRegions.getItemAt(0);
				selectedServiceRegion = item as String; 
			}
			
			if(_selectedServiceProvider == "AWS") {
				_defaultRtwsPubDnsAvailability = DnsAvailability.AVAILABLE;
				
				_selectedRtwsPubDnsAvailability = null;
			}
			else {
				_defaultRtwsPubDnsAvailability = DnsAvailability.UNKNOWN;
				_selectedRtwsPubDnsAvailability = null;
			}
		}
		
		public function get selectedServiceRegion():String {
			return _selectedServiceRegion;
		}		
		public function set selectedServiceRegion(value:String):void {
			if (_allServiceRegions.getItemIndex(value) < 0) {
				throw Error("Runtime error: service region '" + value + "' is not valid.");
			}			
			_selectedServiceRegion = value;
			updateAvailZones();
		}
		
		public function get selectedAvailZone():String {
			return _selectedAvailZone;
		}		
		public function set selectedAvailZone(value:String):void {
			if (_allAvailZones.getItemIndex(value) < 0) {
				throw Error("Runtime error: service zone '" + value + "' is not valid.");
			}
			_selectedAvailZone = value;
		}
		
		public function get defaultRtwsPubDnsAvailability():DnsAvailability {
			return _defaultRtwsPubDnsAvailability;
		}		
		public function set defaultRtwsPubDnsAvailability(value:DnsAvailability):void {
			_defaultRtwsPubDnsAvailability = value;
		}
		
		public function get selectedRtwsPubDnsAvailability():DnsAvailability {
			return _selectedRtwsPubDnsAvailability;
		}		
		public function set selectedRtwsPubDnsAvailability(value:DnsAvailability):void {
			_selectedRtwsPubDnsAvailability = value;
		}
		
		public function get allSystemSizes():ArrayCollection {
			return _allSystemSizes;
		}
		
		public function get allSoftwareVersions():ArrayCollection {
			return _allSoftwareVersions;
		}
		
		public function get allServiceRegions():ArrayCollection {
			return _allServiceRegions;
		}
		
		public function get allAvailZones():ArrayCollection {
			return _allAvailZones;
		}
		
		public function get lastSystemState():SystemState {
			return _lastSystemState;
		}
		public function set lastSystemState(value:SystemState):void {
			_lastSystemState = value;
		}
		
		/** Returns true if the system size was downsized from its original size */
		public function get systemDownsized():Boolean {
			var result:Boolean = false;
			if (_originalSize != null) {
				result = (getSizeIndex(_selectedSize) < getSizeIndex(_originalSize));
			}	
			return result;
		}
		
		private function updateAvailZones():void {
			_allAvailZones.removeAll();
			_allAvailZones.addAll(
				availZonesTable.simpleSelect(
					AvailabilityZones.IAAS_AZONE,
					AvailabilityZones.IAAS_REGION,
					_selectedServiceRegion));

			var item:Object = _allAvailZones.getItemAt(0);
			_selectedAvailZone = item as String;
			
			dispatch(new EditEvent(EditEvent.SYS_PROP_SELECTIONS_CHANGED, null, null));
		}
		
		public function validate():Boolean {
			var errMsg:String;
			var nestedEvent:Event = null;
			
			errMsg = ValidationUtils.domainNameCheck(_domainNamePrefix);
			
			if (errMsg == null) {
				if (_applianceEnabled) {
					errMsg = ValidationUtils.applianceDomainNameCheck(_applianceDomain);
				}
			}
			
			if (errMsg == null) {
				if (_domainNamePrefix.length > this.maxDomainNameLength) {
					errMsg = "Domain name is too long; maximum allowed is " + this.maxDomainNameLength + " characters.";
				}
			}
			
			if (errMsg == null) {
				if (_domainNamePrefix.toLowerCase() == "gateway") {
					errMsg = "Domain name cannot be 'gateway', this is reserved by DigitalEdge.";
				}
			}
			
			if (errMsg == null) {
				if (_domainIsDuplicate) {
					errMsg = "Domain name " + this.fullDomainName + " is in use by somebody else; please choose another one.";
				}
			}
			
			if (errMsg == null) {
				if (_selectedSize.length == 0) {
					errMsg = "No system size has been selected.";
				}
			}
			
			if (errMsg == null) {
				if (_selectedSoftwareVersion.length == 0) {
					errMsg = "No software version has been selected.";
				}
			}
			
			if (errMsg == null) {
				if (_allServiceProviders.length == 0) {
					errMsg = "No service provider has been selected.";
				}
			}
			
			if (errMsg == null) {
				if (_allServiceRegions.length == 0) {
					errMsg = "No region has been selected.";
				}
			}
			
			if (errMsg == null) {
				if (_allAvailZones.length == 0) {
					errMsg = "No zone has been selected.";
				}
			}
			
			if (errMsg == null) {
				if (_defaultRtwsPubDnsAvailability == null)
				{
					// This is a developer error.  This should not be null, but this fact is assumed below
					errMsg = "Cloud Environment DNS Availability is null";
				}
				else if
					(
						_defaultRtwsPubDnsAvailability == DnsAvailability.UNKNOWN &&
						(
							_selectedRtwsPubDnsAvailability == null ||
							_selectedRtwsPubDnsAvailability == DnsAvailability.UNKNOWN
						)
					)
				{
					errMsg = "No DigitalEdge DNS Entry is selected.";
				}
			}
			
			if (errMsg == null) {
				var pgList:Array = processGroupModel.findProcessGroupsWithPersistentIP();
				if ((pgList.length > 0) && _vpcEnabled) {
					errMsg = "Process groups should not have any persistent, public IPs assigned when VPC is enabled. " +
						     "Go to the details tab and clear them.";
				}
			}
			
			if (errMsg == null) {
				// Force a persistant IP on the webapp.default node if no DNS is available.
				var pg:ProcessGroup = processGroupModel.findProcessGroup(ProcessGroupModel.WEBAPPS_DEFAULT);				
				if(
					pg != null &&
					(pg.persistentIPAddress == null || pg.persistentIPAddress == ProcessGroup.NO_PUBLIC_IP) &&
					_vpcEnabled == false &&
					(
						_defaultRtwsPubDnsAvailability == DnsAvailability.UNAVAILABLE ||
						(
							_defaultRtwsPubDnsAvailability == DnsAvailability.UNKNOWN &&
							_selectedRtwsPubDnsAvailability == DnsAvailability.UNAVAILABLE
						)
					)
				) {
					errMsg = "Since DigitalEdge DNS entries are unavailable, you must setup a persistent, public IP address for the " +
						     ProcessGroupModel.WEBAPPS_DEFAULT + " instance. Click OK to continue.";
					var rowIndex:int = processGroupModel.processGroups.getItemIndex(pg);
					var params:Object = new Object();
					params = {row:rowIndex, value:pg.configPersistentIPAddress};
					nestedEvent = new PopupEvent(PopupEvent.SELECT_PERSISTENT_IP_BEGIN, params, null);				
				}
			}
			
			if (errMsg != null) {
				var msg:StatusMessage = new StatusMessage(StatusType.ERROR,errMsg);
				dispatch(new PopupEvent(PopupEvent.MESSAGE_BEGIN, msg, nestedEvent));
				return false;
			}
			
			return true;
		}
		
		public function get transferObject():Object {
			/*
			"applianceDomain" : "string",
			"systemDomain"    : "string",
			"serviceProvider" : "string",
			"region"          : "string",
			"availZone"       : "string",
			"sshKey"          : "string",
			"softwareVersion" : "string",
			"configVersion"   : "string",
			"systemSize"      : "string",
			"autoscale"       : "boolean",
			"vpcEnabled"      : "boolean",
			"applianceEnabled": "boolean",
			*/
			var obj:Object = new Object();
			obj.applianceDomain = this.applianceDomain
			obj.systemDomain = this.fullDomainName;
			obj.serviceProvider = _selectedServiceProvider;
			obj.region = _selectedServiceRegion;
			obj.availZone = _selectedAvailZone;
			obj.sshKey = _keypairName;
			obj.softwareVersion = _selectedSoftwareVersion;
			obj.configVersion = this.configVersion;
			obj.systemSize = _selectedSize;
			obj.autoScale = (_autoScale) ? "true" : "false";
			obj.vpcEnabled = (_vpcEnabled) ? "true" : "false";
			obj.applianceEnabled = (_applianceEnabled) ? "true" : "false";
			
			if(_defaultRtwsPubDnsAvailability == DnsAvailability.UNKNOWN && _selectedRtwsPubDnsAvailability != null) {
				if(_selectedRtwsPubDnsAvailability == DnsAvailability.AVAILABLE) {
					obj["externalDnsEnabled"] = true;
				}
				else if(_selectedRtwsPubDnsAvailability == DnsAvailability.UNAVAILABLE) {
					obj["externalDnsEnabled"] = false;
				}
			}
			
			return obj;
		}
		
		private function loadBoolean(x:Object):Boolean {
			var result:Boolean = false;
			if (x != null) {
				if (x is Boolean) {
					result = x as Boolean;
				}
				else if (x is String) {
					result = ((x as String) == "true");
				}
			}
			return result;
		}
		
		public function load(transferObject:Object):String {
			var errMsg:String = "";
			try {
				this._applianceDomain = transferObject.applianceDomain;
				this._domainNamePrefix = StringUtils.beforeFirst(transferObject.systemDomain,".");
				this._domainNameSuffix = "." + StringUtils.afterFirst(transferObject.systemDomain,".");
				this.selectedServiceRegion = transferObject.region;
				this.selectedAvailZone = transferObject.availZone;
				this._keypairName = transferObject.sshKey;
				this.selectedSoftwareVersion = transferObject.softwareVersion;
				if (this.selectedSoftwareVersion.length == 0) {
					errMsg += "Software version " + transferObject.softwareVersion + " does not exist." + "\n";
				}
				this.configVersion = transferObject.configVersion;
				this.selectedSize  = transferObject.systemSize;
				this.originalSize  = this.selectedSize;
				this.lastSystemState = SystemState.UNKNOWN;				
				_autoScale = loadBoolean(transferObject.autoScale);
				_vpcEnabled = loadBoolean(transferObject.vpcEnabled);
				_applianceEnabled = loadBoolean(transferObject.applianceEnabled);
				
				if (this._applianceDomain == null) {
					this._applianceDomain = "";
				}
				
				try {
					var userSpecifiedExtDnsValue:Object = DynaObjectUtils.getReqObject(transferObject, "externalDnsEnabled");
					
					if(userSpecifiedExtDnsValue == null) {
						_selectedRtwsPubDnsAvailability = null;
					}
					else if(userSpecifiedExtDnsValue is Boolean) {
						_selectedRtwsPubDnsAvailability = ((userSpecifiedExtDnsValue as Boolean) ? DnsAvailability.AVAILABLE : DnsAvailability.UNAVAILABLE);
					}
					else {
						errMsg += "Invalid DigitalEdge DNS Entry value.  Please re-set this value.\n";
						_selectedRtwsPubDnsAvailability = null;
					}
				}
				catch(refError:ReferenceError) {
					// external DNS field not found ... could be a valid case (was not required by user in the environment) or an old version
					_selectedRtwsPubDnsAvailability = null;
				}
			}
			catch (exception:Error) {
				errMsg = exception.message + "\n";
			}
			return errMsg;
		}
	}
}