/**
 *                                  Apache License
 *                            Version 2.0, January 2004
 *                         http://www.apache.org/licenses/
 *
 *    TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION
 *
 *    1. Definitions.
 *
 *       "License" shall mean the terms and conditions for use, reproduction,
 *       and distribution as defined by Sections 1 through 9 of this document.
 *
 *       "Licensor" shall mean the copyright owner or entity authorized by
 *       the copyright owner that is granting the License.
 *
 *       "Legal Entity" shall mean the union of the acting entity and all
 *       other entities that control, are controlled by, or are under common
 *       control with that entity. For the purposes of this definition,
 *       "control" means (i) the power, direct or indirect, to cause the
 *       direction or management of such entity, whether by contract or
 *       otherwise, or (ii) ownership of fifty percent (50%) or more of the
 *       outstanding shares, or (iii) beneficial ownership of such entity.
 *
 *       "You" (or "Your") shall mean an individual or Legal Entity
 *       exercising permissions granted by this License.
 *
 *       "Source" form shall mean the preferred form for making modifications,
 *       including but not limited to software source code, documentation
 *       source, and configuration files.
 *
 *       "Object" form shall mean any form resulting from mechanical
 *       transformation or translation of a Source form, including but
 *       not limited to compiled object code, generated documentation,
 *       and conversions to other media types.
 *
 *       "Work" shall mean the work of authorship, whether in Source or
 *       Object form, made available under the License, as indicated by a
 *       copyright notice that is included in or attached to the work
 *       (an example is provided in the Appendix below).
 *
 *       "Derivative Works" shall mean any work, whether in Source or Object
 *       form, that is based on (or derived from) the Work and for which the
 *       editorial revisions, annotations, elaborations, or other modifications
 *       represent, as a whole, an original work of authorship. For the purposes
 *       of this License, Derivative Works shall not include works that remain
 *       separable from, or merely link (or bind by name) to the interfaces of,
 *       the Work and Derivative Works thereof.
 *
 *       "Contribution" shall mean any work of authorship, including
 *       the original version of the Work and any modifications or additions
 *       to that Work or Derivative Works thereof, that is intentionally
 *       submitted to Licensor for inclusion in the Work by the copyright owner
 *       or by an individual or Legal Entity authorized to submit on behalf of
 *       the copyright owner. For the purposes of this definition, "submitted"
 *       means any form of electronic, verbal, or written communication sent
 *       to the Licensor or its representatives, including but not limited to
 *       communication on electronic mailing lists, source code control systems,
 *       and issue tracking systems that are managed by, or on behalf of, the
 *       Licensor for the purpose of discussing and improving the Work, but
 *       excluding communication that is conspicuously marked or otherwise
 *       designated in writing by the copyright owner as "Not a Contribution."
 *
 *       "Contributor" shall mean Licensor and any individual or Legal Entity
 *       on behalf of whom a Contribution has been received by Licensor and
 *       subsequently incorporated within the Work.
 *
 *    2. Grant of Copyright License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       copyright license to reproduce, prepare Derivative Works of,
 *       publicly display, publicly perform, sublicense, and distribute the
 *       Work and such Derivative Works in Source or Object form.
 *
 *    3. Grant of Patent License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       (except as stated in this section) patent license to make, have made,
 *       use, offer to sell, sell, import, and otherwise transfer the Work,
 *       where such license applies only to those patent claims licensable
 *       by such Contributor that are necessarily infringed by their
 *       Contribution(s) alone or by combination of their Contribution(s)
 *       with the Work to which such Contribution(s) was submitted. If You
 *       institute patent litigation against any entity (including a
 *       cross-claim or counterclaim in a lawsuit) alleging that the Work
 *       or a Contribution incorporated within the Work constitutes direct
 *       or contributory patent infringement, then any patent licenses
 *       granted to You under this License for that Work shall terminate
 *       as of the date such litigation is filed.
 *
 *    4. Redistribution. You may reproduce and distribute copies of the
 *       Work or Derivative Works thereof in any medium, with or without
 *       modifications, and in Source or Object form, provided that You
 *       meet the following conditions:
 *
 *       (a) You must give any other recipients of the Work or
 *           Derivative Works a copy of this License; and
 *
 *       (b) You must cause any modified files to carry prominent notices
 *           stating that You changed the files; and
 *
 *       (c) You must retain, in the Source form of any Derivative Works
 *           that You distribute, all copyright, patent, trademark, and
 *           attribution notices from the Source form of the Work,
 *           excluding those notices that do not pertain to any part of
 *           the Derivative Works; and
 *
 *       (d) If the Work includes a "NOTICE" text file as part of its
 *           distribution, then any Derivative Works that You distribute must
 *           include a readable copy of the attribution notices contained
 *           within such NOTICE file, excluding those notices that do not
 *           pertain to any part of the Derivative Works, in at least one
 *           of the following places: within a NOTICE text file distributed
 *           as part of the Derivative Works; within the Source form or
 *           documentation, if provided along with the Derivative Works; or,
 *           within a display generated by the Derivative Works, if and
 *           wherever such third-party notices normally appear. The contents
 *           of the NOTICE file are for informational purposes only and
 *           do not modify the License. You may add Your own attribution
 *           notices within Derivative Works that You distribute, alongside
 *           or as an addendum to the NOTICE text from the Work, provided
 *           that such additional attribution notices cannot be construed
 *           as modifying the License.
 *
 *       You may add Your own copyright statement to Your modifications and
 *       may provide additional or different license terms and conditions
 *       for use, reproduction, or distribution of Your modifications, or
 *       for any such Derivative Works as a whole, provided Your use,
 *       reproduction, and distribution of the Work otherwise complies with
 *       the conditions stated in this License.
 *
 *    5. Submission of Contributions. Unless You explicitly state otherwise,
 *       any Contribution intentionally submitted for inclusion in the Work
 *       by You to the Licensor shall be under the terms and conditions of
 *       this License, without any additional terms or conditions.
 *       Notwithstanding the above, nothing herein shall supersede or modify
 *       the terms of any separate license agreement you may have executed
 *       with Licensor regarding such Contributions.
 *
 *    6. Trademarks. This License does not grant permission to use the trade
 *       names, trademarks, service marks, or product names of the Licensor,
 *       except as required for reasonable and customary use in describing the
 *       origin of the Work and reproducing the content of the NOTICE file.
 *
 *    7. Disclaimer of Warranty. Unless required by applicable law or
 *       agreed to in writing, Licensor provides the Work (and each
 *       Contributor provides its Contributions) on an "AS IS" BASIS,
 *       WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 *       implied, including, without limitation, any warranties or conditions
 *       of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
 *       PARTICULAR PURPOSE. You are solely responsible for determining the
 *       appropriateness of using or redistributing the Work and assume any
 *       risks associated with Your exercise of permissions under this License.
 *
 *    8. Limitation of Liability. In no event and under no legal theory,
 *       whether in tort (including negligence), contract, or otherwise,
 *       unless required by applicable law (such as deliberate and grossly
 *       negligent acts) or agreed to in writing, shall any Contributor be
 *       liable to You for damages, including any direct, indirect, special,
 *       incidental, or consequential damages of any character arising as a
 *       result of this License or out of the use or inability to use the
 *       Work (including but not limited to damages for loss of goodwill,
 *       work stoppage, computer failure or malfunction, or any and all
 *       other commercial damages or losses), even if such Contributor
 *       has been advised of the possibility of such damages.
 *
 *    9. Accepting Warranty or Additional Liability. While redistributing
 *       the Work or Derivative Works thereof, You may choose to offer,
 *       and charge a fee for, acceptance of support, warranty, indemnity,
 *       or other liability obligations and/or rights consistent with this
 *       License. However, in accepting such obligations, You may act only
 *       on Your own behalf and on Your sole responsibility, not on behalf
 *       of any other Contributor, and only if You agree to indemnify,
 *       defend, and hold each Contributor harmless for any liability
 *       incurred by, or claims asserted against, such Contributor by reason
 *       of your accepting any such warranty or additional liability.
 *
 *    END OF TERMS AND CONDITIONS
 *
 *    APPENDIX: How to apply the Apache License to your work.
 *
 *       To apply the Apache License to your work, attach the following
 *       boilerplate notice, with the fields enclosed by brackets "{}"
 *       replaced with your own identifying information. (Don't include
 *       the brackets!)  The text should be enclosed in the appropriate
 *       comment syntax for the file format. We also recommend that a
 *       file or class name and description of purpose be included on the
 *       same "printed page" as the copyright notice for easier
 *       identification within third-party archives.
 *
 *    Copyright {yyyy} {name of copyright owner}
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
package com.deleidos.rtws.dmeditor.services.rest
{
	import com.deleidos.rtws.commons.event.session.ClientSessionTimerEvent;
	import com.deleidos.rtws.commons.model.datamodel.DataModelZipFile;
	import com.deleidos.rtws.commons.model.status.StatusMessage;
	import com.deleidos.rtws.commons.model.status.StatusType;
	import com.deleidos.rtws.commons.service.rest.SimpleRestService;
	import com.deleidos.rtws.commons.util.StringUtils;
	import com.deleidos.rtws.dmeditor.events.ModelServiceEvent;
	import com.deleidos.rtws.dmeditor.events.PopupEvent;
	import com.deleidos.rtws.dmeditor.models.AppParameterModel;
	import com.deleidos.rtws.dmeditor.models.EnrichmentDirectory;
	import com.deleidos.rtws.dmeditor.models.EnrichmentModel;
	import com.deleidos.rtws.dmeditor.models.InputModel;
	import com.deleidos.rtws.dmeditor.models.ParserDirectory;
	import com.deleidos.rtws.dmeditor.services.IDataModelWizard;
	import com.deleidos.rtws.dmeditor.utils.LoadSaveUtils;
	import com.deleidos.rtws.dmeditor.views.SpinnerPopup;
	
	import flash.events.Event;
	import flash.events.HTTPStatusEvent;
	import flash.events.IOErrorEvent;
	import flash.events.ProgressEvent;
	import flash.events.SecurityErrorEvent;
	import flash.events.TextEvent;
	import flash.events.TimerEvent;
	import flash.net.URLLoader;
	import flash.net.URLLoaderDataFormat;
	import flash.net.URLRequest;
	import flash.net.URLRequestMethod;
	import flash.net.URLStream;
	import flash.net.URLVariables;
	import flash.utils.ByteArray;
	import flash.utils.Timer;
	
	import mx.controls.Alert;
	import mx.rpc.events.FaultEvent;
	import mx.rpc.events.ResultEvent;
	
	public class DataModelWizard extends SimpleRestService implements IDataModelWizard
	{
		public static const START_CSV:String      = "startCsv";
		public static const START_JSON:String     = "startJson";
		public static const CHECK_PROGRESS:String = "checkProgress";
		public static const LOAD_MODEL:String     = "loadModel";
		public static const GET_DETAIL:String     = "getDetail";

		private static const DEFAULT_SERVICE_TIMEOUT:Number = 60;
		
		[Inject] public var appParams:AppParameterModel;
		[Inject] public var inputModel:InputModel;
		[Inject] public var enrichModel:EnrichmentModel;
		[Inject] public var parserDir:ParserDirectory;
		[Inject] public var enrichDir:EnrichmentDirectory;
		
		private static const PROGRESS_INDICATOR:String = "PROCESSING:";
		private static const FINISHED_INDICATOR:String = "FINISHED";
		private static const ERROR_INDICATOR   :String = "ERROR:";
		
		private var _uploadContext:Object = null;
		private var _uploadStream:URLStream = null;
		private var _uploadTimer:Timer = null;

		private var _downloadContext:Object = null;
		private var _urlLoader:URLLoader = null;
		private var _downloadTimer:Timer = null;
		
		public function DataModelWizard()
		{
			super(ServiceConstants.CONTEXT_REL_INGEST_API_PROXY_PATH, DEFAULT_SERVICE_TIMEOUT);
		}

		/**
		 * Upload the sample data as CSV for data model creation. Only one upload can run at a time.
		 * If the upload completes successfully, a token is returned that can be used to track progress
		 * of the data model creation.
		 */
		public function createModelStartCsv(numHeaders:int, numRows:int, threshold:Number, separator:String, sampleData:ByteArray):void
		{
			if (_uploadContext == null) {
				_uploadContext = new Object();
				_uploadContext.operation = START_CSV;
				_uploadContext.errorPrefix = "Error uploading CSV: ";
				
				var vars:URLVariables = new URLVariables();
				vars.numHeaders = numHeaders;
				vars.numRows = numRows;
				vars.decisionThreshold = threshold;
				vars.columnSeparator = separator;			
				vars.file = new URLFileVariable(sampleData, "file");

				var url:String = ServiceConstants.CONTEXT_REL_INGEST_API_PROXY_PATH + "/json/model/create/csv";
				
				startUpload(url,vars);
			}
			else {
				var statusMsg:StatusMessage =
					new StatusMessage(StatusType.WARN, "Upload already in progress." );
				dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));				
			}
		}

		/**
		 * Upload the sample data as JSON for data model creation. Only one upload can run at a time.
		 * If the upload completes successfully, a token is returned that can be used to track progress
		 * of the data model creation.
		 */
		public function createModelStartJson(numRows:int, threshold:Number, sampleData:ByteArray):void
		{
			if (_uploadContext == null) {
				_uploadContext = new Object();
				_uploadContext.operation = START_JSON;
				_uploadContext.errorPrefix = "Error uploading JSON: ";
				
				var vars:URLVariables = new URLVariables();
				vars.numRows = numRows;
				vars.decisionThreshold = threshold;
				vars.file = new URLFileVariable(sampleData, "file");
				
				var url:String = ServiceConstants.CONTEXT_REL_INGEST_API_PROXY_PATH + "/json/model/create/json";
				
				startUpload(url,vars);
			}
			else {
				var statusMsg:StatusMessage =
					new StatusMessage(StatusType.WARN, "Upload already in progress." );
				dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));				
			}
		}
		
		/** Internal function to start an upload */
		private function startUpload(url:String, vars:URLVariables):void {
			var req:URLRequest = new URLRequestBuilder(vars).build();
			req.method = URLRequestMethod.POST;
			req.url = url;
			
			_uploadTimer = new Timer(10*60*1000);
			_uploadTimer.addEventListener(TimerEvent.TIMER, onUploadTimeout);
			
			_uploadStream = new URLStream();
			_uploadStream.addEventListener(Event.COMPLETE, onUploadComplete);
			_uploadStream.addEventListener(IOErrorEvent.IO_ERROR, onUploadError);
			_uploadStream.addEventListener(SecurityErrorEvent.SECURITY_ERROR, onUploadError);
			
			_uploadStream.load(req);
			_uploadTimer.start();			
		}

		/** Called when the upload completes successfully */
		private function onUploadComplete(event:Event):void
		{
			if(dispatchClientSessionTimerEvents)
			{
				this.dispatch(new ClientSessionTimerEvent(ClientSessionTimerEvent.RESTART_SESSION_TIMER));
			}
			
			var responseBytes:ByteArray = new ByteArray();
			_uploadStream.readBytes(responseBytes);
			var responseString:String = responseBytes.toString();

			// trace("DataModelWizard.onUploadComplete, response=" + responseString);
			dispatch(new ModelServiceEvent(ModelServiceEvent.MODEL_WIZARD_STARTED, responseString, null));
			
			cleanUpUploadOp();			
		}
		
		/** Called when the upload times out. */
		private function onUploadTimeout(event:Event):void
		{
			var statusMsg:StatusMessage =
				new StatusMessage(StatusType.ERROR, _uploadContext.errorPrefix + "Timeout waiting for the server." );
			dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));				
			cleanUpUploadOp(true);
		}
		
		/** Called on upload error. */
		private function onUploadError(event:TextEvent):void
		{
			var statusMsg:StatusMessage =
				new StatusMessage(StatusType.ERROR, _uploadContext.errorPrefix + event.text );
			dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));				
			cleanUpUploadOp();			
		}
		
		/** Clean up the current upload and reset for another upload. */
		private function cleanUpUploadOp(isCancellation:Boolean=false):void
		{
			SpinnerPopup.destroySpinnerPopup();
			
			if(_uploadTimer != null)
			{
				_uploadTimer.reset();
				_uploadTimer.removeEventListener(TimerEvent.TIMER, onUploadTimeout);
			}
			
			if(_uploadStream != null)
			{
				_uploadStream.removeEventListener(Event.COMPLETE, onUploadComplete);
				_uploadStream.removeEventListener(IOErrorEvent.IO_ERROR, onUploadError); 
				_uploadStream.removeEventListener(SecurityErrorEvent.SECURITY_ERROR, onUploadError);
				
				if(isCancellation)
				{
					_uploadStream.close();
				}
			}

			_uploadContext = null;
			_uploadTimer = null;
			_uploadStream = null;
		}
		
		/**
		 * Get the data model that was created by the wizard.
		 */
		public function createModelLoad(token:String):void
		{
			// trace("DataModelWizard.createModelLoad, token=" + token);
			if (_downloadContext == null) {
				_downloadContext = new Object();
				_downloadContext.operation = LOAD_MODEL;
				_downloadContext.errorPrefix = "Data Model Wizard: Error downloading the data model: ";
				
				var url:String = ServiceConstants.CONTEXT_REL_INGEST_API_PROXY_PATH + "/rest/model/zip/" + encodeURIComponent(token);;
				startDownload(url);
			}
			else {
				var statusMsg:StatusMessage =
					new StatusMessage(StatusType.WARN, "Data Model Wizard: Data model download already in progress." );
				dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));				
			}
		}
		
		/** Internal function to start a download */
		private function startDownload(url:String):void {
			var urlRequest:URLRequest = new URLRequest(url);
			
			_urlLoader = new URLLoader(urlRequest);
			_urlLoader.addEventListener(Event.COMPLETE, onDownloadComplete);
			_urlLoader.addEventListener(IOErrorEvent.IO_ERROR, onDownloadError); 
			_urlLoader.addEventListener(SecurityErrorEvent.SECURITY_ERROR, onDownloadError);
			_urlLoader.dataFormat = URLLoaderDataFormat.BINARY;
			
			_downloadTimer = new Timer(appParams.serverTimeout*1000);
			_downloadTimer.addEventListener(TimerEvent.TIMER, onDownloadTimeout);
			
			_urlLoader.load(urlRequest);
			_downloadTimer.start();						
		}

		/** Called when the data model download finishes successfully */
		private function onDownloadComplete(event:Event):void
		{
			// trace("DataModelWizard.onDownloadComplete");
			if(dispatchClientSessionTimerEvents)
			{
				this.dispatch(new ClientSessionTimerEvent(ClientSessionTimerEvent.RESTART_SESSION_TIMER));
			}
			
			//
			// The data bytes retrieved from the server should be a zip file.
			//
			var zipBytes:ByteArray = _urlLoader.data as ByteArray;
			var dmZipFile:DataModelZipFile = new DataModelZipFile(zipBytes);
			var statusMsg:StatusMessage;
			
			try {
				inputModel.clear();
				
				var messages:Array = LoadSaveUtils.loadDataModelZip(
					inputModel.dataModelName, inputModel, enrichModel, parserDir, enrichDir, dmZipFile);
				
				if (messages.length > 0) {
					statusMsg = new StatusMessage(StatusType.WARN, LoadSaveUtils.buildErrorsAndWarnings(messages));
					dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));
				}
			}
			catch (e:Error) {
				statusMsg = new StatusMessage(StatusType.ERROR, "Data Model Wizard: Data model cannot be properly loaded: " + e.toString() );
				dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));
			}
			
			cleanUpDownloadOp();
			
			dispatch(new ModelServiceEvent(ModelServiceEvent.MODEL_WIZARD_LOADED, null, null));
		}
		
		/** Called when the download times out. */
		private function onDownloadTimeout(event:Event):void
		{
			var statusMsg:StatusMessage =
				new StatusMessage(StatusType.ERROR, _downloadContext.errorPrefix + "Timeout waiting for the server." );
			dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));				
			cleanUpDownloadOp(true);
		}

		/** Called when an error occurs during a download */
		private function onDownloadError(event:TextEvent):void
		{
			var statusMsg:StatusMessage =
				new StatusMessage(StatusType.ERROR, _downloadContext.errorPrefix + event.text );
			dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));				
			cleanUpDownloadOp();
		}
		
		/** Clean up the current download and reset for another download. */
		private function cleanUpDownloadOp(isCancellation:Boolean=false):void
		{
			SpinnerPopup.destroySpinnerPopup();
			
			if(_downloadTimer != null)
			{
				_downloadTimer.reset();
				_downloadTimer.removeEventListener(TimerEvent.TIMER, onDownloadTimeout);
			}
			
			if(_urlLoader != null)
			{
				_urlLoader.removeEventListener(Event.COMPLETE, onDownloadComplete);
				_urlLoader.removeEventListener(IOErrorEvent.IO_ERROR, onDownloadError); 
				_urlLoader.removeEventListener(SecurityErrorEvent.SECURITY_ERROR, onDownloadError);
				
				if(isCancellation)
				{
					_urlLoader.close();
				}
			}
			
			_urlLoader = null;
			_downloadTimer = null;
			_downloadContext = null;
		}
		
		private const POLL_INTERVAL:int = 2; // in seconds 
		private const POLL_ITERATIONS:Number = (450/POLL_INTERVAL);
		private var tokenToPoll:String;
		private var pollTimer:Timer
		private var pollCountdown:int;
		private var pollInProgress:Boolean;
		
		private function pollProgress():void {
			if (!pollInProgress) {
				var url:String = "/json/model/status/" + encodeURIComponent(tokenToPoll);
				var context:Object = new Object();
				context.operation = CHECK_PROGRESS;
				context.errorMsg = "Data Model Wizard: Error polling for status.";			
				this.get(url, null, context, ((appParams != null && appParams.serverTimeout >= 0) ? appParams.serverTimeout : NaN));
				pollInProgress = true;
			}
		}
		
		private function pollTimerEvent(event:TimerEvent):void {
			// trace("DataModelWizard.pollTimerEvent");
			pollCountdown--;
			if (pollCountdown <= 0) {
				pollTimer.stop();
				var statusMsg:StatusMessage = new StatusMessage(StatusType.ERROR, "Data Model Wizard: Failed due to timeout." );
				var errorEvent:PopupEvent = new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg);				
				dispatch(new PopupEvent(PopupEvent.PROGRESS_BAR_POPUP_END, errorEvent, null));				
			}
			else {
				pollProgress();
			}
		}
		
		/**
		 * Start polling for progress, and update the progress bar as necessary.
		 */
		public function createModelPollProgress(token:String):void
		{
			// trace("DataModelWizard.createModelPollProgress");
			tokenToPoll = token;
			pollTimer = new Timer(POLL_INTERVAL*1000);
			pollTimer.addEventListener(TimerEvent.TIMER, pollTimerEvent);
			pollCountdown = POLL_ITERATIONS;
			pollTimer.start();
			pollInProgress = false;
			pollProgress();
		}
		
		/**
		 * Get the detailed field information that describes what was found during data model creation.
		 */
		public function createModelGetDetail(token:String):void
		{
			// trace("DataModelWizard.CreateModelGetDetail");
			var url:String = "/json/model/information/" + encodeURIComponent(token);
			var context:Object = new Object();
			context.operation = GET_DETAIL;
			context.token = token;
			context.errorMsg = "Data Model Wizard: Error retrieving model information.";
			this.get(url, null, context, ((appParams != null && appParams.serverTimeout >= 0) ? appParams.serverTimeout : NaN));
		}
		
		override protected function handleSuccess(event:ResultEvent, context:Object):void
		{
			var response:String = event.message.body.toString();
			
			// trace("DataModelWizard.handleSuccess, response=" + response);
			
			if (context.operation == CHECK_PROGRESS) {
				pollInProgress = false;
				if (StringUtils.beginsWith(response,FINISHED_INDICATOR)) {
					pollTimer.stop();
					var wizardDone:ModelServiceEvent = new ModelServiceEvent(ModelServiceEvent.MODEL_WIZARD_DONE, tokenToPoll, null);
					dispatch(new PopupEvent(PopupEvent.PROGRESS_BAR_POPUP_END, wizardDone, null));
				}
				else if (StringUtils.beginsWith(response,PROGRESS_INDICATOR)) {
					// Progress indicator, extract out the progress value (0 to 100)
					var parts:Array = response.split(/\s*:\s*/);
					if (parts.length >= 1) {
						var progress:Number = parseFloat(parts[1]);
						dispatch(new PopupEvent(PopupEvent.PROGRESS_BAR_POPUP_UPDATE, null, progress));							
					}
				}
				else if (StringUtils.beginsWith(response,ERROR_INDICATOR)) {
					// Error indicator, parse the error message
					pollTimer.stop();
					var message:String = response.substr(ERROR_INDICATOR.length+1);
					var index:int = response.indexOf("Exception info:");
					if (index >= 0) {
						message = response.substring(ERROR_INDICATOR.length+1, index);
					}
					message = "Data Model Wizard: Failed due to: " + message;
					var statusMsg:StatusMessage = new StatusMessage(StatusType.ERROR, message );
					var errorEvent:PopupEvent = new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg);				
					dispatch(new PopupEvent(PopupEvent.PROGRESS_BAR_POPUP_END, errorEvent, null));
				}
			}
			else if (context.operation == GET_DETAIL) {
				SpinnerPopup.destroySpinnerPopup();
				var result:Object = new Object();
				result.token = context.token;
				result.response = response;
				dispatch(new ModelServiceEvent(ModelServiceEvent.MODEL_WIZARD_DETAIL_GOT, result, null));
			}
		}
		
		override protected function handleFailure(event:FaultEvent, context:Object):void
		{
			if (pollTimer != null) {
				pollTimer.stop();
			}
			SpinnerPopup.destroySpinnerPopup();
			var statusMsg:StatusMessage =
				new StatusMessage(StatusType.ERROR, context.errorMsg + " " + event.fault.faultString + "." );
			dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));
		}
		
	}
}