/**
 *                                  Apache License
 *                            Version 2.0, January 2004
 *                         http://www.apache.org/licenses/
 *
 *    TERMS AND CONDITIONS FOR USE, REPRODUCTION, AND DISTRIBUTION
 *
 *    1. Definitions.
 *
 *       "License" shall mean the terms and conditions for use, reproduction,
 *       and distribution as defined by Sections 1 through 9 of this document.
 *
 *       "Licensor" shall mean the copyright owner or entity authorized by
 *       the copyright owner that is granting the License.
 *
 *       "Legal Entity" shall mean the union of the acting entity and all
 *       other entities that control, are controlled by, or are under common
 *       control with that entity. For the purposes of this definition,
 *       "control" means (i) the power, direct or indirect, to cause the
 *       direction or management of such entity, whether by contract or
 *       otherwise, or (ii) ownership of fifty percent (50%) or more of the
 *       outstanding shares, or (iii) beneficial ownership of such entity.
 *
 *       "You" (or "Your") shall mean an individual or Legal Entity
 *       exercising permissions granted by this License.
 *
 *       "Source" form shall mean the preferred form for making modifications,
 *       including but not limited to software source code, documentation
 *       source, and configuration files.
 *
 *       "Object" form shall mean any form resulting from mechanical
 *       transformation or translation of a Source form, including but
 *       not limited to compiled object code, generated documentation,
 *       and conversions to other media types.
 *
 *       "Work" shall mean the work of authorship, whether in Source or
 *       Object form, made available under the License, as indicated by a
 *       copyright notice that is included in or attached to the work
 *       (an example is provided in the Appendix below).
 *
 *       "Derivative Works" shall mean any work, whether in Source or Object
 *       form, that is based on (or derived from) the Work and for which the
 *       editorial revisions, annotations, elaborations, or other modifications
 *       represent, as a whole, an original work of authorship. For the purposes
 *       of this License, Derivative Works shall not include works that remain
 *       separable from, or merely link (or bind by name) to the interfaces of,
 *       the Work and Derivative Works thereof.
 *
 *       "Contribution" shall mean any work of authorship, including
 *       the original version of the Work and any modifications or additions
 *       to that Work or Derivative Works thereof, that is intentionally
 *       submitted to Licensor for inclusion in the Work by the copyright owner
 *       or by an individual or Legal Entity authorized to submit on behalf of
 *       the copyright owner. For the purposes of this definition, "submitted"
 *       means any form of electronic, verbal, or written communication sent
 *       to the Licensor or its representatives, including but not limited to
 *       communication on electronic mailing lists, source code control systems,
 *       and issue tracking systems that are managed by, or on behalf of, the
 *       Licensor for the purpose of discussing and improving the Work, but
 *       excluding communication that is conspicuously marked or otherwise
 *       designated in writing by the copyright owner as "Not a Contribution."
 *
 *       "Contributor" shall mean Licensor and any individual or Legal Entity
 *       on behalf of whom a Contribution has been received by Licensor and
 *       subsequently incorporated within the Work.
 *
 *    2. Grant of Copyright License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       copyright license to reproduce, prepare Derivative Works of,
 *       publicly display, publicly perform, sublicense, and distribute the
 *       Work and such Derivative Works in Source or Object form.
 *
 *    3. Grant of Patent License. Subject to the terms and conditions of
 *       this License, each Contributor hereby grants to You a perpetual,
 *       worldwide, non-exclusive, no-charge, royalty-free, irrevocable
 *       (except as stated in this section) patent license to make, have made,
 *       use, offer to sell, sell, import, and otherwise transfer the Work,
 *       where such license applies only to those patent claims licensable
 *       by such Contributor that are necessarily infringed by their
 *       Contribution(s) alone or by combination of their Contribution(s)
 *       with the Work to which such Contribution(s) was submitted. If You
 *       institute patent litigation against any entity (including a
 *       cross-claim or counterclaim in a lawsuit) alleging that the Work
 *       or a Contribution incorporated within the Work constitutes direct
 *       or contributory patent infringement, then any patent licenses
 *       granted to You under this License for that Work shall terminate
 *       as of the date such litigation is filed.
 *
 *    4. Redistribution. You may reproduce and distribute copies of the
 *       Work or Derivative Works thereof in any medium, with or without
 *       modifications, and in Source or Object form, provided that You
 *       meet the following conditions:
 *
 *       (a) You must give any other recipients of the Work or
 *           Derivative Works a copy of this License; and
 *
 *       (b) You must cause any modified files to carry prominent notices
 *           stating that You changed the files; and
 *
 *       (c) You must retain, in the Source form of any Derivative Works
 *           that You distribute, all copyright, patent, trademark, and
 *           attribution notices from the Source form of the Work,
 *           excluding those notices that do not pertain to any part of
 *           the Derivative Works; and
 *
 *       (d) If the Work includes a "NOTICE" text file as part of its
 *           distribution, then any Derivative Works that You distribute must
 *           include a readable copy of the attribution notices contained
 *           within such NOTICE file, excluding those notices that do not
 *           pertain to any part of the Derivative Works, in at least one
 *           of the following places: within a NOTICE text file distributed
 *           as part of the Derivative Works; within the Source form or
 *           documentation, if provided along with the Derivative Works; or,
 *           within a display generated by the Derivative Works, if and
 *           wherever such third-party notices normally appear. The contents
 *           of the NOTICE file are for informational purposes only and
 *           do not modify the License. You may add Your own attribution
 *           notices within Derivative Works that You distribute, alongside
 *           or as an addendum to the NOTICE text from the Work, provided
 *           that such additional attribution notices cannot be construed
 *           as modifying the License.
 *
 *       You may add Your own copyright statement to Your modifications and
 *       may provide additional or different license terms and conditions
 *       for use, reproduction, or distribution of Your modifications, or
 *       for any such Derivative Works as a whole, provided Your use,
 *       reproduction, and distribution of the Work otherwise complies with
 *       the conditions stated in this License.
 *
 *    5. Submission of Contributions. Unless You explicitly state otherwise,
 *       any Contribution intentionally submitted for inclusion in the Work
 *       by You to the Licensor shall be under the terms and conditions of
 *       this License, without any additional terms or conditions.
 *       Notwithstanding the above, nothing herein shall supersede or modify
 *       the terms of any separate license agreement you may have executed
 *       with Licensor regarding such Contributions.
 *
 *    6. Trademarks. This License does not grant permission to use the trade
 *       names, trademarks, service marks, or product names of the Licensor,
 *       except as required for reasonable and customary use in describing the
 *       origin of the Work and reproducing the content of the NOTICE file.
 *
 *    7. Disclaimer of Warranty. Unless required by applicable law or
 *       agreed to in writing, Licensor provides the Work (and each
 *       Contributor provides its Contributions) on an "AS IS" BASIS,
 *       WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 *       implied, including, without limitation, any warranties or conditions
 *       of TITLE, NON-INFRINGEMENT, MERCHANTABILITY, or FITNESS FOR A
 *       PARTICULAR PURPOSE. You are solely responsible for determining the
 *       appropriateness of using or redistributing the Work and assume any
 *       risks associated with Your exercise of permissions under this License.
 *
 *    8. Limitation of Liability. In no event and under no legal theory,
 *       whether in tort (including negligence), contract, or otherwise,
 *       unless required by applicable law (such as deliberate and grossly
 *       negligent acts) or agreed to in writing, shall any Contributor be
 *       liable to You for damages, including any direct, indirect, special,
 *       incidental, or consequential damages of any character arising as a
 *       result of this License or out of the use or inability to use the
 *       Work (including but not limited to damages for loss of goodwill,
 *       work stoppage, computer failure or malfunction, or any and all
 *       other commercial damages or losses), even if such Contributor
 *       has been advised of the possibility of such damages.
 *
 *    9. Accepting Warranty or Additional Liability. While redistributing
 *       the Work or Derivative Works thereof, You may choose to offer,
 *       and charge a fee for, acceptance of support, warranty, indemnity,
 *       or other liability obligations and/or rights consistent with this
 *       License. However, in accepting such obligations, You may act only
 *       on Your own behalf and on Your sole responsibility, not on behalf
 *       of any other Contributor, and only if You agree to indemnify,
 *       defend, and hold each Contributor harmless for any liability
 *       incurred by, or claims asserted against, such Contributor by reason
 *       of your accepting any such warranty or additional liability.
 *
 *    END OF TERMS AND CONDITIONS
 *
 *    APPENDIX: How to apply the Apache License to your work.
 *
 *       To apply the Apache License to your work, attach the following
 *       boilerplate notice, with the fields enclosed by brackets "{}"
 *       replaced with your own identifying information. (Don't include
 *       the brackets!)  The text should be enclosed in the appropriate
 *       comment syntax for the file format. We also recommend that a
 *       file or class name and description of purpose be included on the
 *       same "printed page" as the copyright notice for easier
 *       identification within third-party archives.
 *
 *    Copyright {yyyy} {name of copyright owner}
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
package com.deleidos.rtws.dmeditor.models
{
	import com.deleidos.rtws.commons.model.status.StatusMessage;
	import com.deleidos.rtws.commons.model.status.StatusType;
	import com.deleidos.rtws.dmeditor.events.InputModelEditEvent;
	import com.deleidos.rtws.dmeditor.events.PopupEvent;
	import com.deleidos.rtws.dmeditor.services.RepositoryConstants;
	import com.deleidos.rtws.dmeditor.utils.DmeValidationUtils;
	import com.deleidos.rtws.dmeditor.utils.JsonXMLConversions;
	import com.deleidos.rtws.dmeditor.utils.StringUtils;
	import com.deleidos.rtws.dmeditor.utils.XMLUtilities;
	
	import mx.collections.ArrayCollection;
	import mx.collections.XMLListCollection;
	import mx.utils.ObjectUtil;
	
	import org.robotlegs.mvcs.Actor;
	
	/*
	* The InputModel consists of the following:
	* 1) CanonicalModel     - Fields, data types, and thier structure
	* 2) dataSourceNameList - List of available data sources
	* 3) SelectedDataSource - Currently selected data source 
	* The canonical model fields, types, structure, and the CURRENT translation
	* are represented using an XML structure where each element has the form:
	*     <field @name @path @type @xlate>
	* If a field does not have a translation, the its xlate attribute is missing.
	*/
	public class InputModel extends Actor
	{
		public static const TYPE_STRING     :String = "string";
		public static const TYPE_NUMBER     :String = "number";
		public static const TYPE_DATESTRING :String = "datestring";
		public static const TYPE_OBJECT     :String = "OBJECT";
		public static const TYPE_ARRAY      :String = "ARRAY";
		public static const TYPE_NONE       :String = "NONE";
		
		public static const XLATE_GET       :String = "get";
		public static const XLATE_GET_ARRAY :String = "arrget";
		public static const XLATE_CONVERT   :String = "convert";
		public static const XLATE_SCRIPT    :String = "script";
		public static const XLATE_CONSTANT  :String = "=";
		public static const XLATE_NONE      :String = "none";
		
		public static var xlateKeywords:Array = [ XLATE_GET, XLATE_GET_ARRAY, XLATE_CONVERT, XLATE_SCRIPT ];
		
		private var _dataModelName      : DataModelName;
		private var _canonicalModel     : XMLListCollection;
		private var _dataSourceNameList : ArrayCollection;
		private var _selectedDataSrcObj : Object;
		private var _dataSources        : Array;
		private var _dataModelIsNew     : Boolean;
		
		// The marker strings should agree on the following two lines
		public static const XLATE_ARRAY_MARKER : String = "(no elements)";
		public static const arrayMarker:XML = <marker fieldname="(no elements)" type="NONE"/>;
		
		public function InputModel()
		{
			clear();
		}
		
		/**
		 * Clear the input model, and set the new flag to true.
		 */
		public function clear():void {
			_canonicalModel = new XMLListCollection();
			_selectedDataSrcObj = null;
			_dataSourceNameList = new ArrayCollection();
			_dataModelName = new DataModelName("Untitled", RepositoryConstants.PRIVATE, "v0.0");
			_dataModelIsNew = true;
			clearDataSources();
		}
		
		/* Return the canonical model. This model enumerates fields and thier data types */
		public function get canonicalModel():XMLListCollection {
			return _canonicalModel;
		}
		
		/* The name of the current data model */
		public function get dataModelName():DataModelName {
			return _dataModelName;
		}
		
		public function set dataModelName(value:DataModelName):void {
			_dataModelName = value;
		}
		
		/* Get the object describing the currently selected data source. */
		public function get selectedDataSource():Object {
			return _selectedDataSrcObj;
		}
		
		/* Get the data source name list */
		public function get dataSourceNameList():ArrayCollection {
			return _dataSourceNameList;
		}
		
		public function get dataSourcesList():Array {
			return _dataSources;
		}
		
		/* Set the data source name list */
		public function set dataSourceNameList(value:ArrayCollection):void {
			_dataSourceNameList = value;
		}
		
		public function set dataModelIsNew(value : Boolean):void {
			// TODO: Put new flag into object?
			_dataModelIsNew = value;
		}
		
		public function get dataModelIsNew():Boolean {
			// TODO: Put new flag into object?
			return _dataModelIsNew;
		}
		
		/**
		 * Returns the XML object within the input model that matches the given path.
		 */
		public function getCanonicalField(path:String):XML {
			return XMLUtilities.findField(_canonicalModel.source, path);
		}
		
		/**
		 * Removes all data sources
		 */
		public function clearDataSources():void {
			_dataSources = new Array();
			_dataSourceNameList.removeAll();
			_selectedDataSrcObj = null;			
		}
		
		// Create a blank input mapping
		private static function createBlankMapping(inputModel:XMLList):XMLListCollection {
			var result:XML = <root />;
			for each (var xml:XML in inputModel) {
				recurseBlankInputMapping(result,xml);
			}
			return new XMLListCollection(result.field as XMLList);
		}
		
		// Recursive function to creaet a blank input mapping
		private static function recurseBlankInputMapping(result:XML, sourceXML:XML):void {
			for each (var field:XML in sourceXML) {
				var mapping:XML = <field />;
				mapping.@fieldname = field.@fieldname;
				mapping.@type = field.@type;
				mapping.@path = field.@path;
				// trace(field.@fieldname);
				if (field.hasComplexContent()) {
					if (field.@type == "OBJECT") {
						var childList:XMLList = field.children();
						for each (var xml:XML in childList) {
							recurseBlankInputMapping(mapping,xml);
						}
					}
					else {
						mapping.appendChild(arrayMarker.copy());
					}
					mapping.@xlate = field.@type;
				}
				else {
					mapping.@xlate = XLATE_NONE;
				}
				result.appendChild(mapping);
			}	
		}
		
		/**
		 * Adds a data source. If mapping==null, then a blank mapping is created
		 */
		public function addDataSource(name:String, parser:String, inputFields:ArrayCollection, parserParams:ArrayCollection, mapping:XMLListCollection):int {
			var dataSource:Object = new Object();
			dataSource.name = name;
			dataSource.parser = parser;
			dataSource.inputFields = inputFields;
			dataSource.parserParams = parserParams;
			dataSource.mapping = mapping;
			if (mapping == null) {
				dataSource.mapping = createBlankMapping(_canonicalModel.source);
			}
			_dataSources[name] = dataSource;
			_dataSourceNameList.addItem(name);
			return (_dataSourceNameList.getItemIndex(name));
		}
		
		/**
		 * Edits the currently selected data source with the passed values.
		 */
		public function editDataSource(name:String, parser:String, inputFields:ArrayCollection, parserParams:ArrayCollection):void {
			if (_selectedDataSrcObj != null) {
				var nameListIndex:int = _dataSourceNameList.getItemIndex(_selectedDataSrcObj.name);
				if (nameListIndex >= 0) {
					_dataSourceNameList.setItemAt(name, nameListIndex);
					_selectedDataSrcObj.name = name;
					_selectedDataSrcObj.parser = parser;
					_selectedDataSrcObj.inputFields = inputFields;
					_selectedDataSrcObj.parserParams = parserParams;
					// dispatch(new InputModelEditEvent(InputModelEditEvent.MODEL_SELECT_DATASRC, null, index, dataSource));
				}
			}
		}
		
		/**
		 * Removes the given data source
		 */
		public function removeDataSource(index:int):void {
			var name:String = _dataSourceNameList.getItemAt(index) as String;
			var dataSource:Object = _dataSources[name];
			delete _dataSources[name];
			_dataSourceNameList.removeItemAt(index);
			if (_dataSourceNameList.length == 0) {
				// All data sources are deleted
				clearDataSources();									
				dispatch(new InputModelEditEvent(InputModelEditEvent.MODEL_SELECT_DATASRC, null, -1, null));
			}
			else if (_selectedDataSrcObj == dataSource) {
				// Deleted the currently selected data source
				selectDataSource(Math.max(0, index-1));
			}
		}
		
		/**
		 * Selects the given data source
		 */
		public function selectDataSource(index:int):void {
			var name:String = _dataSourceNameList.getItemAt(index) as String;
			var dataSource:Object = _dataSources[name];
			if (dataSource != null) {
				_selectedDataSrcObj = dataSource;
				dispatch(new InputModelEditEvent(InputModelEditEvent.MODEL_SELECT_DATASRC, null, index, dataSource));
			}
		}
		
		/** Deep copy of a data source object */
		public function copyDataSource(dataSource:Object):Object {
			var copiedDataSource:Object = null;
			
			if(dataSource != null) {
				copiedDataSource = new Object();
				
				copiedDataSource.name = dataSource.name;
				copiedDataSource.parser = dataSource.parser;
				
				// Can copy directly using ObjectUtil.copy() since it is an array of Strings
				copiedDataSource.inputFields = ObjectUtil.copy(dataSource.inputFields) as ArrayCollection;
				
				// Need to copy each object in the collection independently because they are
				// complex objects
				copiedDataSource.parserParams = new ArrayCollection();
				for(var i:int = 0; i < dataSource.parserParams.length; ++i) {
					var param:ParserParameter = dataSource.parserParams.getItemAt(i) as ParserParameter;
					copiedDataSource.parserParams.addItem(param.clone());
				}
				
				copiedDataSource.mapping = new XMLListCollection(dataSource.mapping.copy());
			}
			
			return copiedDataSource;
		}
		
		/**
		 * Return the path string for the given name combined with the selection
		 */
		public function fieldPath(selection:XML, name:String):String {
			return XMLUtilities.fieldPath(selection, name);
		}
		
		/**
		 * Create a field.
		 */
		public function createField(name:String, path:String, type:String, xlate:String=null) : XML {
			var field : XML = <field />;
			field.@fieldname = name;
			field.@path = path;
			field.@type = type;
			if (xlate != null) {
				field.@xlate = xlate;
			}
			return field;
		}
		
		/**
		 * Computes the initial xlate string for a new field given its type
		 */
		private function computeInitialXlate(type:String):String {
			if ((type == TYPE_OBJECT) || (type == TYPE_ARRAY)) {
				return type;
			}
			else {
				return XLATE_NONE;
			}
		}
		
		/**
		 * Insert a field into the canonical input model
		 */
		private function canonicalInsertField(selection:XML,
											  model:XMLListCollection,
											  field:XML,
											  type:String,
											  elementType:String):void
		{
			XMLUtilities.insertChildAfter(model, selection, field);
			
			// Now figure out what to do with objects and arrays 
			
			var newPath:String = field.@path;
			var objectChild:XML = null;
			if (type == TYPE_OBJECT) {
				// Insert the child field (string for now)
				/*
				newPath = newPath + "." + childName;
				objectChild = createField(childName, newPath, InputModel.TYPE_STRING);
				field.appendChild(objectChild);
				*/
			}
			else if (type == TYPE_ARRAY) {
				// Arrays have an index field beneath the array field that matches the
				// user selected type
				newPath += "[0]";
				var arrayParent : XML = createField("0", newPath, elementType);
				field.appendChild(arrayParent);
				
				// If the array element type is an object, add a child node to it
				/*
				if (elementType == InputModel.TYPE_OBJECT) {
				newPath = newPath + "." + childName;
				objectChild = createField(childName, newPath, InputModel.TYPE_STRING);
				arrayParent.appendChild(objectChild);
				}
				*/
			}
			
		}
		
		/**
		 * Callback function to process the insertion of each item into the data model mapping 
		 */ 
		private function processInsertMapping(dataModelName:String, field:XML, path:String, model:XMLListCollection, params:Object):void {
			
			var fieldPrototype:XML = params.fieldPrototype;
			var newField:XML = fieldPrototype.copy();
			
			newField.@path = XMLUtilities.fieldPath(field, newField.@fieldname);
			
			// trace("Insert " + newField.@path + " at " + newField.@path);
			
			XMLUtilities.insertChildAfter(model, field, newField);
			
			/*
			if (atRoot)
			model.addItem(newField);
			else if (field != null) {
			//
			// THE BELOW IS NOT WORKING RELIABLY
			//
			insertChildAfter(model, field, newField);
			// var parent:XML = field.parent();
			// var result:XML = parent.insertChildAfter(field, newField);
			//field.parent().insertChildAfter(field, newField);
			}
			*/
			
			if (params.type == TYPE_OBJECT) {
				// Adds the child too
				/*
				var childMapping:XML = <field />;
				childMapping.@fieldname = params.childName;
				childMapping.@type = TYPE_STRING;
				childMapping.@path = newField.@path + "." + params.childName;
				childMapping.@xlate = XLATE_NONE;
				newField.appendChild(childMapping);
				*/
			}
			else if (params.type == TYPE_ARRAY) {
				// Adds the no elements marker
				// var marker:XML = <marker fieldname="(no elements)" type="NONE"/>;
				var marker:XML = arrayMarker.copy();
				newField.appendChild(marker);
			}			
		}
		
		/**
		 * Inserts the item into the input model and all data source mappings.
		 */
		private function insertFieldInternal(selection:XML, field:XML, type:String, elementType:String):void {
			
			// var selectionParent:XML = (selection == null) ? (null) : selection.parent();
			// var appendItem:Boolean = (selection == null) || (selection.parent() == null);
			
			// Need to determine an insertion point for the data source mappings before
			// inserting an item 
			var insertPath:String = null;
			if (selection == null) {
				var len:int = _canonicalModel.length;
				if (len > 0) {
					var item:XML = _canonicalModel.getItemAt(len-1) as XML; 
					insertPath = item.@path;
				}
			}
			else {
				insertPath = selection.@path;
			}
			
			// Insert into canonical input model
			canonicalInsertField(selection, _canonicalModel, field, type, elementType);
			
			// var insertPath:String = (selection == null) ? (null) : selection.@path;
			
			// Create a blank mapping for this field
			var newField:XML = <field />;
			newField.@fieldname = field.@fieldname;
			newField.@type = field.@type;
			newField.@path = field.@path;
			newField.@xlate = computeInitialXlate(field.@type);
			
			// Create extra parameters for insertion
			var params:Object = new Object();
			params.fieldPrototype = newField;		
			params.type = type;
			// params.childName = childName;
			params.elementType = elementType;
			
			// Insert into all data sources
			InputModelUtilities.processDataSources(insertPath, _dataSources, processInsertMapping, params);			
		}
		
		/**
		 * Insert a field after the selection. Performs validation and then delegates the
		 * operation to insertFieldInternal.
		 * 
		 * selection   - currently selected field in the canonical model - insertion point
		 * field       - the field to insert into the canonical model
		 * type        - the type of field being inserted (see TYPE_* constants above)
		 * elementType - For TYPE_ARRAY, the type of the array elements (TYPE_STRING, TYPE_NUMBER, TYPE_OBJECT)
		 */
		public function insertField(selection:XML, field:XML, type:String, elementType:String):Boolean {
			
			var insertValid:Boolean = false;
			
			if (selection == null) {
				insertValid = true;
			}
			else {
				var selectionIsIndex:Boolean = (selection.@fieldname == "0");
				var selectionParent:XML = selection.parent();
				var selectionParentIsIndex:Boolean = false;
				var selectionParentIsObject:Boolean = true;
				
				if (selectionParent != null) {
					selectionParentIsIndex = (selectionParent.@fieldname == "0");
					selectionParentIsObject = (selectionParent.@type == TYPE_OBJECT);					
				}
				
				// Rules:
				// 1) You cannot insert into the array index
				// 2) You cannot insert any more fields into an array of numbers or strings,
				//    but you can insert more fields into an array of object
				
				insertValid = true;					
				if (selectionIsIndex || (selectionParentIsIndex && !selectionParentIsObject)) {
					insertValid = false;					
				}
			}
			
			if (insertValid) {
				// See if the field already exists
				if (XMLUtilities.findField(_canonicalModel.source, field.@path) != null) {
					insertValid = false;
					var statusMsg:StatusMessage = new StatusMessage(StatusType.ERROR, "Field already exists");
					dispatch(new PopupEvent(PopupEvent.MESSAGE_POPUP, null, statusMsg));
				}
			}
			
			if (insertValid) {
				insertFieldInternal(selection, field, type, elementType);				
			}
			
			return insertValid;
		}
		
		/**
		 * Insert a DataField into the canonical input model
		 */
		private function canonicalInsertDataField(selection:XML,
												  model:XMLListCollection,
												  field:XML,
												  type:String,
												  elementType:String):void
		{
			selection.appendChild(field);
			
			// Now figure out what to do with objects and arrays 
			
			var newPath:String = field.@path;
			var objectChild:XML = null;
			if (type == TYPE_ARRAY) {
				// Arrays have an index field beneath the array field that matches the
				// user selected type
				newPath += "[0]";
				var arrayParent : XML = createField("0", newPath, elementType);
				field.appendChild(arrayParent);				
			}
			
		}
		
		/**
		 * Callback function to process the insertion of each item into the data model mapping 
		 */ 
		private function processDataFieldInsertMapping(dataModelName:String, field:XML, path:String, model:XMLListCollection, params:Object):void {
			
			var fieldPrototype:XML = params.fieldPrototype;
			var newField:XML = fieldPrototype.copy();
			
			field.appendChild(newField);
			
			if (params.type == TYPE_ARRAY) {
				// Adds the no elements marker
				// var marker:XML = <marker fieldname="(no elements)" type="NONE"/>;
				var marker:XML = arrayMarker.copy();
				newField.appendChild(marker);
			}			
		}
		
		/**
		 * Inserts a data into the input model and all data source mappings.
		 */
		private function insertDataFieldInternal(selection:XML, field:XML, type:String, elementType:String):void {
			
			// Need to determine an insertion point for the data source mappings before
			// inserting an item 
			var insertPath:String = selection.@path;
			
			// Insert into canonical input model
			canonicalInsertDataField(selection, _canonicalModel, field, type, elementType);
			
			// var insertPath:String = (selection == null) ? (null) : selection.@path;
			
			// Create a blank mapping for this field
			var newField:XML = <field />;
			newField.@fieldname = field.@fieldname;
			newField.@type = field.@type;
			newField.@path = field.@path;
			newField.@xlate = computeInitialXlate(field.@type);
			
			// Create extra parameters for insertion
			var params:Object = new Object();
			params.fieldPrototype = newField;		
			params.type = type;
			params.elementType = elementType;
			
			// Insert into all data sources
			InputModelUtilities.processDataSources(insertPath, _dataSources, processDataFieldInsertMapping, params);			
		}
		
		/**
		 * Inserts the DataField object into the model. Note that the DataField may contain
		 * sub-objects and subarrays, so the function operates recursively.
		 */
		public function insertDataField(selection:XML, dataField:DataField, inner:Boolean=false):XML {
			
			var name:String = dataField.field;
			
			var path:String;
			if (inner)
				path = selection.@path + "." + name;
			else
				path = fieldPath(selection, name);
			
			var type:String = dataField.type;
			if (type == TYPE_DATESTRING) {
				type = type + " " + dataField.param;
			}
			
			var newField:XML = createField(name, path, type);
			
			var success:Boolean = true;
			if (inner)
				insertDataFieldInternal(selection, newField, type, dataField.param);
			else
				success = insertField(selection, newField, type, dataField.param);
			
			if (success) {
				if (dataField.members != null) {
					var parent:XML = newField;
					if (type == TYPE_ARRAY) {
						parent = (new XMLListCollection(newField.children())).getItemAt(0) as XML;
					}
					for each (var element:DataField in dataField.members) {
						insertDataField(parent, element, true);
					}
				}
				return newField;				
			}
			else {
				return null;
			}
		}
		
		/**
		 * Removes the field from a data mapping model.
		 */
		private function processDeleteMapping(dataModelName:String, field:XML, path:String, model:XMLListCollection, params:Object):void {
			
			XMLUtilities.deleteInXMLListCollection(model, field);
		}
		
		/**
		 * Removes the field from both the input model and all data source mappings.  
		 */
		public function removeField(field:XML):void {
			
			if (field != null) {
				var parent:XML = field.parent();
				if ((parent != null) && (parent.@type == TYPE_OBJECT) && (parent.children().length() == 1)) {
					// If it's the last field of an object, delete the whole object
					XMLUtilities.deleteInXMLListCollection(_canonicalModel, parent);					
					InputModelUtilities.processDataSources(parent.@path, _dataSources, processDeleteMapping, null);
				}
				else {
					XMLUtilities.deleteInXMLListCollection(_canonicalModel, field);
					InputModelUtilities.processDataSources(field.@path, _dataSources, processDeleteMapping, null);
				}
			}
		}
		
		// Callback from processDataSources, uses the params object as an array of data source/mapping names
		// and increments it
		private function processMappingReferences(dataModelName:String, field:XML, path:String, model:XMLListCollection, result:Array):void {
			result[dataModelName] += 1;
		}
		
		// Computes the number of times the given selection is referenced in a data mapping
		public function computeMappingReferences(selection:XML):Array {
			var result:Array = new Array();
			for each (var dataSource:Object in _dataSources) {
				result[dataSource.name] = 0;
			}
			if (selection != null) {
				InputModelUtilities.processDataSources(selection.@path, _dataSources, processMappingReferences, result);
			}
			return result;
		}
		
		private function recursiveFindDataSourceFieldReference(mapping:XMLList, srcFieldName:String, result:Array):void {
			for each (var field:XML in mapping) {
				var xlate:Object = xlateParse(field.@xlate);
				if (xlate != null) {
					var fields:ArrayCollection;
					if (xlate.fieldName != null) {
						if (xlate.fieldName == srcFieldName) {
							result.push(field);
						}
					}
					if (xlate.arguments != null) {
						fields = new ArrayCollection(xlate.arguments.split(/\s*,\s*/));
						if (fields.contains(srcFieldName)) {
							result.push(field);
						}
					}
					if (xlate.fieldNames != null) {
						fields = new ArrayCollection(xlate.fieldNames.split(/\s*\+\s*/));
						if (fields.contains(srcFieldName)) {
							result.push(field);
						}
					}					
				}
				if (field.children().length()> 0) {
					recursiveFindDataSourceFieldReference(field.children(), srcFieldName, result);
				}
			}
		}
		
		/**
		 * Given a data source and a source field name, return any input model fields (XML) that
		 * reference that data source field name in thier translation directive. 
		 */
		public function findDataSourceFieldReference(dataSourceName:String, srcFieldName:String):Array {
			
			var dataSource:Object = _dataSources[dataSourceName];
			var result:Array = new Array();
			
			if (dataSource != null) {
				var mappingModel:XMLListCollection = dataSource.mapping as XMLListCollection;
				recursiveFindDataSourceFieldReference(mappingModel.source, srcFieldName, result);
				for each (var x:XML in result) {
					trace(x);
				}
			}
			
			return result;
		}
		
		/** Updates the field names and paths to the new fieldname */
		private function updateFieldnames(dataModelName:String, field:XML, path:String, model:XMLListCollection, newFieldName:String):void {
			// field.@fieldname = newFieldName;
			// field.@path = StringUtils.beforeLast(field.@path, ".") + "." + newFieldName;
			updateFieldNameInternal(field, newFieldName);
		}
		
		/** Updates the field paths of subordinate fields */
		private function updateChildPaths(children:XMLList, oldFieldPath:String, newFieldPath:String):void {
			for each (var item:XML in children) {
				var path:String = item.@path;
				item.@path = path.replace(oldFieldPath,newFieldPath);
				if (item.hasComplexContent()) {
					updateChildPaths(item.children(),oldFieldPath,newFieldPath);
				}
			}
		}
		
		private function updateFieldNameInternal(field:XML, newFieldname:String):void {
			var oldFieldPath:String = field.@path;
			
			if (StringUtils.contains(field.@path,"."))
				field.@path = StringUtils.beforeLast(field.@path, ".") + "." + newFieldname;
			else
				field.@path = newFieldname; 
			
			field.@fieldname = newFieldname;
			
			if (field.hasComplexContent()) {
				var trailer:String = ".";
				if (field.@type == TYPE_ARRAY) {
					trailer = "[";
				}
				updateChildPaths(field.children(), oldFieldPath+trailer, field.@path+trailer);
			}			
		}
		
		/**
		 * Updates the input model fieldname in both the input model and the data mappings.
		 */
		public function updateFieldname(field:XML, newFieldname:String):void {
			var oldFieldPath:String = field.@path;
			updateFieldNameInternal(field, newFieldname);
			InputModelUtilities.processDataSources(oldFieldPath, _dataSources, updateFieldnames, newFieldname);
		}
		
		/** Updates the field types to the new type */
		private function updateFieldtypes(dataModelName:String, field:XML, path:String, model:XMLListCollection, newType:String):void {
			field.@type = newType;
		}
		
		/**
		 * Updates the input model type.
		 */
		public function updateDataType(field:XML, newDatatype:String):void {
			field.@type = newDatatype;
			InputModelUtilities.processDataSources(field.@path, _dataSources, updateFieldtypes, newDatatype);
		}
		
		/**
		 * Parses a translation directive and returns an object with dynamically assigned
		 * fields of parsed values. Returns null if there is a syntax error with the directive.
		 */
		public function xlateParse(xlateString:String):Object {
			var validateRegex:RegExp;
			var result:Object = null;
			var keyword:String = xlateKeyword(xlateString);
			var params:String;
			var comma:int;
			
			if (keyword == XLATE_GET) {
				// get(field[,default])
				validateRegex = DmeValidationUtils.getFieldRegEx;
				if (xlateString.match(validateRegex)) {
					result = new Object();
					params = xlateString.substring(XLATE_GET.length+1, xlateString.length-1);
					var values:Array = params.split(",");
					comma = params.indexOf(",");
					result.keyword = keyword;
					result.fieldName    = StringUtils.trim((comma >= 0) ? params.slice(0,comma) : params);
					result.defaultValue = StringUtils.trim((comma >= 0) ? params.slice(comma+1) : "");
				}				
			}
			else if (keyword == XLATE_GET_ARRAY) {
				validateRegex = DmeValidationUtils.arrgetFieldRegEx;
				if (xlateString.match(validateRegex)) {
					result = new Object();
					params = xlateString.substring(XLATE_GET_ARRAY.length+1, xlateString.length-1);
					if (StringUtils.endsWith(params,"[*]")) {
						params = params.substring(0, params.length-3);
					}
					result.keyword = keyword;
					result.fieldName = params;
				}				
			}
			else if (keyword == XLATE_CONSTANT) {
				// =value
				validateRegex = DmeValidationUtils.setConstantRegEx;
				if (xlateString.match(validateRegex)) {
					result = new Object();
					result.keyword = keyword;
					result.constant = xlateString.substr(1);
				}
			}
			else if (keyword == XLATE_SCRIPT) {
				// script(name[,field]*)
				validateRegex = DmeValidationUtils.runScriptRegEx;
				if (xlateString.match(validateRegex)) {
					result = new Object();
					params = xlateString.substring(XLATE_SCRIPT.length+1, xlateString.length-1);
					comma = params.indexOf(",");
					result.keyword = keyword;
					result.scriptName = StringUtils.trim((comma >= 0) ? params.slice(0,comma) : params);
					result.arguments  = StringUtils.trim((comma >= 0) ? params.slice(comma+1) : "");
				}
			}
			else if (keyword == XLATE_CONVERT) {
				// convert(field1[+field2]*,<format>)
				validateRegex = DmeValidationUtils.doConvertRegEx;
				if (xlateString.match(validateRegex)) {
					result = new Object();
					params = xlateString.substring(XLATE_CONVERT.length+1, xlateString.length-1);
					comma = params.indexOf(",");
					result.keyword = keyword;
					result.fieldNames = StringUtils.trim(params.slice(0,comma));
					result.dateFormat = StringUtils.trim(params.slice(comma+1));
				}
			}
			
			return result;
		}
		
		/**
		 * Examines the translation string and returns the translation keyword
		 * it corresponds to or null if no match. 
		 */
		public function xlateKeyword(xlateString:String):String {
			if (xlateString.charAt(0) == XLATE_CONSTANT)
				return XLATE_CONSTANT;
			else {
				var keyword:String = StringUtils.beforeFirst(xlateString,"(");
				keyword = StringUtils.trim(keyword.toLowerCase());
				for each (var kw:String in xlateKeywords) {
					if (keyword == kw) {
						return kw;
					}
				}
				return null;
			}
		}
		
		/**
		 * Set the translation of the selected field to get a field from the source.
		 */
		public function xlateGetField(field:XML, fieldName:String, defaultValue:String):void {
			if ((defaultValue == null) || (defaultValue.length == 0)) {
				field.@xlate = XLATE_GET + "(" + fieldName + ")";				
			}
			else {
				field.@xlate = XLATE_GET + "(" + fieldName + "," + defaultValue + ")";				
			}			
		}
		
		public function xlateGetArray(field:XML, fieldName:String):void {
			var name:String = fieldName;
			if (!StringUtils.endsWith(name,"[*]")) {
				name = name + "[*]";
			}
			field.@xlate = XLATE_GET_ARRAY + "(" + name + ")";
			
			// Remove the marker
			var childList:XMLListCollection = new XMLListCollection(field.children());
			if  (childList.length > 0) {
				var firstElement:XML = childList.getItemAt(0) as XML;
				var name:String = firstElement.name();
				if (name == "marker") {
					XMLUtilities.deleteNode(childList.source, firstElement);
				}
			}
		}
		
		/**
		 * Set the translation of the selected field to a constant value.
		 */
		public function xlateUseConstant(field:XML, constant:String):void {
			field.@xlate = XLATE_CONSTANT + constant;			
		}
		
		/**
		 * Set the translation of the selected field to a date conversion.
		 */
		public function xlateConvertDate(field:XML, inputFields:String, formatString:String):void {
			field.@xlate = XLATE_CONVERT + "(" + inputFields + ", " + formatString + ")";
		}
		
		/**
		 * Set the translation of the selected field to call a script.
		 */
		public function xlateCallScript(field:XML, scriptName:String, inputFields:String):void {
			if ((inputFields == null) || (inputFields.length == 0)) {
				// Ask Richard about this, can a script have no input parameters?
				field.@xlate = XLATE_SCRIPT + "(" + scriptName + ")";					
			}
			else {
				field.@xlate = XLATE_SCRIPT + "(" + scriptName + ", " + inputFields + ")";							
			}			
		}
		
		/**
		 * Replace the path prefix on all fields in the XML tree
		 */
		private static function replacePathPrefix(oldPathPrefix:String, newPathPrefix:String, sourceXML:XML):void {
			for each (var field:XML in sourceXML) {
				var path:String = field.@path;
				path = path.replace(oldPathPrefix, newPathPrefix);
				field.@path = path;
				if (field.hasComplexContent()) {
					var childList:XMLList = field.children();
					for each (var xml:XML in childList) {
						replacePathPrefix(oldPathPrefix, newPathPrefix, xml);
					}
				}
			}
		}
		
		/**
		 * Adds an input mapping array element to the selected array.
		 */
		public function xlateAddArrayElement(arrayField:XML):void {
			var arrayFieldPath:String = arrayField.@path;
			var canonicalPath:String = arrayFieldPath.replace(/\[[0-9*]\]/g,"[0]");
			var inputModelField:XML = getCanonicalField(canonicalPath);
			if (inputModelField != null) {
				// Check to see if the translation mapping has a marker field only,
				// meaning this array has not yet been mapped. If so, remove it.
				var childList:XMLListCollection = new XMLListCollection(arrayField.children());
				var numChildren:int = childList.length;
				if (numChildren == 0) {
					arrayField.@xlate = "";
				}
				else {
					var firstElement:XML = childList.getItemAt(0) as XML;
					var name:String = firstElement.name();
					if (name == "marker") {
						XMLUtilities.deleteNode(childList.source, firstElement);
						numChildren--;
					}
				}
				
				// Now create a blank translation of the single element in
				// the input model
				var inputModelElement:XML = inputModelField.children()[0];
				var newChildRoot:XML = <root />;
				recurseBlankInputMapping(newChildRoot, inputModelElement);
				
				// Rename the path expression
				var newChild:XML = (newChildRoot.field as XMLList)[0];
				var oldPath:String = newChild.@path;
				var newPath:String = arrayField.@path + "[" + numChildren.toString() + "]";
				replacePathPrefix(oldPath, newPath, newChild);
				
				// Rename the field name from 0 to whatever the next index is
				newChild.@fieldname = numChildren.toString();
				
				arrayField.appendChild(newChild);
				// trace(newChild);
			}
		}
		
		/**
		 * Deletes the last input mapping array element to the selected array.
		 */
		public function xlateRemoveArrayElement(arrayField:XML):void {
			var inputModelField:XML = getCanonicalField(arrayField.@path);
			if (inputModelField != null) {
				var childList:XMLListCollection = new XMLListCollection(arrayField.children());
				var numChildren:int = childList.length;
				var targetElement:XML = childList.getItemAt(numChildren-1) as XML;
				var name:String = targetElement.name();
				if (name != "marker") {
					XMLUtilities.deleteNode(childList.source, targetElement);
					numChildren--;
					if (numChildren == 0) {
						// Last element has been removed - put back to the marker
						arrayField.appendChild(arrayMarker.copy());
					}
				}				
			}
		}
		
		/** Sets the canonical model and name */
		public function loadCanonicalModel(dataModelName:DataModelName, modelData:XMLList):void {
			_dataModelName = dataModelName;
			_canonicalModel = new XMLListCollection(modelData);
			_dataModelIsNew = false;
		}
		
		/** Clean up the array and remove unwanted strings */
		private function cleanUpStrings(list:ArrayCollection):void {
			for (var i:int=0; i<list.length; i++) {
				var item:String = list.getItemAt(i) as String;
				if (StringUtils.trim(item).length == 0) {
					list.removeItemAt(i);
				}
			}
		}
		
		/** Sets the next data source adding to the overall set of data sources */ 
		public function loadNextDataSource(name:String,
										   parser:String,
										   inputFields:Array,
										   parserParams:Array,
										   mapping:XMLList):void {
			var dataSource:Object = new Object();
			dataSource.name = name;
			dataSource.parser = parser;			
			dataSource.inputFields = new ArrayCollection(inputFields);
			cleanUpStrings(dataSource.inputFields);
			dataSource.parserParams = new ArrayCollection(parserParams);
			dataSource.mapping = new XMLListCollection(mapping);
			_dataSources[name] = dataSource;
			_dataSourceNameList.addItem(name);
		}
	}
}
